Перем СписокДействий;

Процедура ВводНового()
	Отчет=1;
	ДатаДок = КонМесяца(РабочаяДата());
	ДатаДокумента = Дата(0);
	Автор=глПользователь;
КонецПроцедуры

Процедура ПриОткрытии()
	глПроверкаРазрешенияРедактирования(Контекст);
	Форма.КнопкаПоУмолчанию("ОК");
	ДатаДокумента = ДатаДок;
КонецПроцедуры

Процедура Проверка()
	Док=СоздатьОбъект("Документ.НачислениеИзносаОС");
	Док.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	Пока Док.ПолучитьДокумент()<>0 Цикл
		Если (Док.НомерДок<>НомерДок) и (Док.Проведен()=1) Тогда
			Сообщить("Документ не будет проведен, так как уже был начислен износ на ОС в этом месяце!");
			Статусвозврата(0);
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Печать()
	Пропись(КаталогИБ()+"Spl\"+Леи.Spl);
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("НачислениеИзносаОС");
	Таб.ПараметрыСтраницы(1,,,,,,,,,1);
	Таб.ВывестиСекцию("Отчет");  
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		 Таб.ВывестиСекцию("Строка");
	КонецЦикла;	
	Таб.ВывестиСекцию("Итог");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Начисление износа ОС № "+СокрЛП(НомерДок)+" от "+СокрЛП(ДатаДок));
КонецПроцедуры   

Процедура Заполнить() 
	УдалитьСтроки();
	Кол_воОпер=0;
	Сч01=СчетПоКоду("123");
	Сч02=СчетПоКоду("124");
 	Ит=СоздатьОбъект("БухгалтерскиеИтоги");
	Ит1=СоздатьОбъект("БухгалтерскиеИтоги");
	Ит1.Рассчитать(НачГода(ДатаДок),НачГода(ДатаДок),СчетПоКоду("124"));
	Ит.ВключатьСубсчета(1);
	Ит.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства,,1,0);
	Ит.ВыполнитьЗапрос(КонМесяца(ДатаДок),КонМесяца(ДатаДок),,,,1,,1);
	Ит.ВыбратьСубконто();
	Пока Ит.ПолучитьСубконто()=1 Цикл
		Если Ит.Субконто().Амортизация=0 Тогда
		    Продолжить;
		КонецЕсли;
		ПервоначальнаяСтоимость=0;
		ИзносНаНачПериода=0;
		ОстаточнаяСтоимость = 0;
		БалансоваяСтоимость = 0;
		Износ=0;
		ОС=Ит.Субконто();
		ОС.ИспользоватьДату(ДатаДок);
		ШифрАмортизации=ОС.ШифрАмортизации;
		ДатаВвода=ОС.ДатаВвода;
		МетодНачисления=ОС.МетодНачисленияАмортизации;
		СрокЭксплуатации=ОС.СрокЭксплуатации; 
		Если (СрокЭксплуатации=0) и (Ит.Субконто().МетодНачисленияАмортизации=Перечисление.МетодыНачислАмортизации.Прямолинейный) Тогда
		    Сообщить("Не указан срок эксплуатации "+СокрЛП(ОС.Наименование)+" полный код "+СокрЛП(ОС.ПолныйКод())+"!");
			Продолжить;
		КонецЕсли;
		ГодоваяНормаАморт=ОС.ГодНормаАмортизации;
		ОстаточнаяСтоимость=ОС.ЛиквСтоим;
		СчетЗатрат=ОС.СчетЗатрат;
		СтатьяЗатрат=ОС.СтатьяЗатрат;
		Счет_СубсчетАморт=ОС.СчетАмортизации;
		Кол_воУслОпер=ОС.КолвоУслОпераций;
		Коэффициент=ОС.КоэфИзноса;
		Амортизация=ОС.Амортизация;

		Если НачМесяца(ДатаВвода)<=НачМесяца(Датадок) Тогда
			Если Амортизация=1 Тогда
				Ит.ВыбратьСчета();
				Пока Ит.ПолучитьСчет() = 1 Цикл
					Если Ит.Счет = Сч01 Тогда
						ПервоначальнаяСтоимость=Ит.СНД();
					ИначеЕсли Ит.Счет=Сч02 Тогда
						ИзносНаНачПериода=Ит.СНК();
					КонецЕсли;
				КонецЦикла;

				Если (ПервоначальнаяСтоимость-ОстаточнаяСтоимость-ИзносНаНачПериода)>0 Тогда
					Дата1 = КонМесяца(ДатаДок);
					Дата2 = ДатаВвода;
					Год1 = ДатаГод(Дата1);
					Год2 = ДатаГод(Дата2);
					Месяц1 = ДатаМесяц(Дата1);
					Месяц2 = ДатаМесяц(Дата2);
					РазнГод = Год1 - Год2;
					РазнМесяц = Месяц1 - Месяц2;
					РазнДата=Дата1-Дата2;
					
					Если МетодНачисления = Перечисление.МетодыНачислАмортизации.Прямолинейный Тогда // Прямолинейный
						Если ((Год1=Год2) И (Месяц1=Месяц2)) Тогда
							Если Константа.СпособНачисленияИзноса=Перечисление.СпособНачисленияИзноса.СоСледМесяца Тогда
							    Сообщить("На "+ОС+" износ будет рассчитываться со след. месяца!!!");
								Продолжить;
							ИначеЕсли Константа.СпособНачисленияИзноса=Перечисление.СпособНачисленияИзноса.СДатыВвода Тогда
								Срок=Дата1-Дата2;
								Износ=(ПервоначальнаяСтоимость-ОстаточнаяСтоимость)/
								СрокЭксплуатации*Срок/365;
							Иначе
								Износ=(ПервоначальнаяСтоимость-ОстаточнаяСтоимость)/СрокЭксплуатации/12;
							КонецЕсли;
						Иначе
							Износ=(ПервоначальнаяСтоимость-ОстаточнаяСтоимость)/СрокЭксплуатации/12;
						КонецЕсли; 
					КонецЕсли;
					
					Если МетодНачисления = Перечисление.МетодыНачислАмортизации.Производственный Тогда // Производственный (кол-во операции)
						ВвестиЧисло(Кол_воОпер,"Кол-во оп. "+ОС.Наименование,12,0,);
						Износ=(ПервоначальнаяСтоимость-ОстаточнаяСтоимость)/Кол_воУслОпер*Кол_воОпер;
					КонецЕсли;
					
					Если МетодНачисления = Перечисление.МетодыНачислАмортизации.Кумулятивный Тогда // Суммы чисел (кумулятивный)
						Сум=1;
						Сум1=1;
						Пока Сум <= СрокЭксплуатации Цикл
							Сум=Сум+1;
							Сум1=Сум1+Сум;
						КонецЦикла;

						Износ=(ПервоначальнаяСтоимость-ОстаточнаяСтоимость)*
						(СрокЭксплуатации-(Год1-Год2))/Сум1/12;
					КонецЕсли;
					
					Если МетодНачисления = Перечисление.МетодыНачислАмортизации.УмОстатка Тогда

						Стоимость=ИТ1.СНКРС("124",1,,ОС,"!");
						Если Стоимость=0 Тогда
							Стоимость=ОС.ИзносНаНачГода;
						КонецЕсли;
						
						БалансСтоим=ПервоначальнаяСтоимость-Стоимость-ОстаточнаяСтоимость;
						Если ((Год1=Год2) И (Месяц1=Месяц2)) Тогда
							Срок=Дата1-Дата2;
							Износ=БалансСтоим*Коэффициент*Срок/365;
						Иначе
							Износ=БалансСтоим*Коэффициент/12;
						КонецЕсли; 
					КонецЕсли;
				КонецЕсли;
				
				
				БалансоваяСтоимость=ПервоначальнаяСтоимость-ОстаточнаяСтоимость-ИзносНаНачПериода;
				Если БалансоваяСтоимость<Износ Тогда
					Износ=БалансоваяСтоимость;
				КонецЕсли;
				
				Если Износ>0 Тогда
					Износ = ОКР(Износ,2,0);
					БСтоимость=ПервоначальнаяСтоимость-ИзносНаНачПериода-ОстаточнаяСтоимость-Износ;
					Если ОС.СчетЗатрат.Выбран()=0 Тогда
						Сообщить("Не указан счет затрат для "+ОС,"!");
					КонецЕсли;
				
                    НоваяСтрока();
					ОснСр_во=Ос;
					НачислИзнос=ИзносНаНачПериода;
					СчетД=ОС.СчетЗатрат;
					СчетК=ОС.СчетАмортизации;
					СуммаИзноса=Износ;
					ОстатСтоим=БСтоимость;
					Перв=ПервоначальнаяСтоимость;
					
					
					БалансоваяСтоимость = 0;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	


Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	Операция.Содержание="Начисл.износа ОС за "+Формат(ДатаДок,"Д ММММГГГГ");
	ЖурналДокумента=глВыбратьЖурнал(ТекущийДокумент());
КонецПроцедуры


СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Открыть  в журнале");