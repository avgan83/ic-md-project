Перем Счет;
Перем КорСчет;
Перем Субконто1;
Перем ВидСубк1;
Перем КорСубконто1;
Перем КорВидСубк1;
Перем Субконто2;
Перем ВидСубк2;
Перем КорСубконто2;
Перем КорВидСубк2;
Перем Субконто3;
Перем ВидСубк3;
Перем КорСубконто3;
Перем КорВидСубк3;
Перем ДтКт;
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ИспФильтр;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

Функция ВидСубконтоСтр(ВидСубк, Субк, КорВидСубк, КорСубк)
	с = "";
	Если (ТипЗначения(ВидСубк) = 16) Или (ТипЗначения(КорВидСубк) = 16) Тогда
		с = ";";
		Если ТипЗначения(ВидСубк) = 16 Тогда
			Если ВидСубк.Выбран() = 1 Тогда
				с = с+Строка(ВидСубк)+":"+Субк;
			КонецЕсли;
		КонецЕсли;

		с = с+",";
		Если ТипЗначения(КорВидСубк) = 16 Тогда
			Если КорВидСубк.Выбран() = 1 Тогда
				с = с+Строка(КорВидСубк)+":"+КорСубк;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат с;
КонецФункции

Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

Процедура Сформировать(Фильтр, ФлагЗакрытияФормы = 0)
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Опер = СоздатьОбъект("Операция");
	Сумма = 0;
	Количество = 0;
	ВалСумма = 0;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Если Константа.ЖирныйШрифтВОтчетах=Да Тогда
	 	Т.ИсходнаяТаблица("Таблица");
	Иначе
	 	Т.ИсходнаяТаблица("Таблица1");
	КонецЕсли;

    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
	Если ИспФильтр=1 Тогда
		Опер.ВыбратьОперацииСПроводками(Дата1, Дата2, Фильтр, ?(ПоВалюте=1,Валюта,""), ПланСчетов, РазделительУчета);
		Заголовок = Фильтр;
	Иначе
		Если МаксимальноеКоличествоСубконто() > 0 Тогда
			Опер.ИспользоватьСубконто(ВидСубк1, Субконто1);
			Опер.ИспользоватьКорСубконто(КорВидСубк1, КорСубконто1);
			Если МаксимальноеКоличествоСубконто() > 1 Тогда
				Опер.ИспользоватьСубконто(ВидСубк2, Субконто2);
				Опер.ИспользоватьКорСубконто(КорВидСубк2, КорСубконто2);
				Если МаксимальноеКоличествоСубконто() > 2 Тогда
					Опер.ИспользоватьСубконто(ВидСубк3, Субконто3);
					Опер.ИспользоватьКорСубконто(КорВидСубк3, КорСубконто3);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Опер.ВыбратьОперацииСПроводками(Дата1, Дата2, Счет, КорСчет, ДтКт, ?(ПоВалюте=1,Валюта,""), ПланСчетов, РазделительУчета);
		Если ДтКт = 1 Тогда
			Фильтр = ""+?(ТипЗначения(Счет)=0,"*",Счет)+","+?(ТипЗначения(КорСчет)=0,"*",КорСчет);
			Заголовок = Фильтр+ВидСубконтоСтр(ВидСубк1, Субконто1, КорВидСубк1, КорСубконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк2, Субконто2, КорВидСубк2, КорСубконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк3, Субконто3, КорВидСубк3, КорСубконто3);
		ИначеЕсли ДтКт = 2 Тогда
			Фильтр = ""+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+","+?(ТипЗначения(Счет)=0,"*",Счет);
			Заголовок = Фильтр+ВидСубконтоСтр(КорВидСубк1, КорСубконто1, ВидСубк1, Субконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк2, КорСубконто2, ВидСубк2, Субконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
		Иначе
			Фильтр = ""+?(ТипЗначения(Счет)=0,"*",Счет)+","+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+";"+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+","+?(ТипЗначения(Счет)=0,"*",Счет);
			Заголовок = Фильтр+ВидСубконтоСтр(ВидСубк1, Субконто1, КорВидСубк1, КорСубконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк1, КорСубконто1, ВидСубк1, Субконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк2, Субконто2, КорВидСубк2, КорСубконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк2, КорСубконто2, ВидСубк2, Субконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
		КонецЕсли;
	КонецЕсли;
	Если ПоВалюте=1 Тогда
		Заголовок = Заголовок+";"+Валюта;
	КонецЕсли;
	Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
		Заголовок = Заголовок+"; "+ВыбЖурнал;
	КонецЕсли;

	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Фильтр", Фильтр);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Расшифровка.Установить("НомерЖурнала", ВыбЖурнал);
	Если ИспФильтр=1 Тогда
		Расшифровка.Установить("ИспФильтр",1);
	Иначе
		Расшифровка.Установить("Счет",Счет);
		Расшифровка.Установить("КорСчет",КорСчет);
		Расшифровка.Установить("ДтКт",ДтКт);
		Расшифровка.Установить("ВидСубконто1",ВидСубк1);
		Расшифровка.Установить("Субконто1",Субконто1);
		Расшифровка.Установить("ВидСубконто2",ВидСубк2);
		Расшифровка.Установить("Субконто2",Субконто2);
		Расшифровка.Установить("ВидСубконто3",ВидСубк3);
		Расшифровка.Установить("Субконто3",Субконто3);
		Расшифровка.Установить("КорВидСубконто1",КорВидСубк1);
		Расшифровка.Установить("КорСубконто1",КорСубконто1);
		Расшифровка.Установить("КорВидСубконто2",КорВидСубк2);
		Расшифровка.Установить("КорСубконто2",КорСубконто2);
		Расшифровка.Установить("КорВидСубконто3",КорВидСубк3);
		Расшифровка.Установить("КорСубконто3",КорСубконто3);
	КонецЕсли;
	Т.ВывестиСекцию("Секция_4");
	Т.ВывестиСекцию("Секция_1");
	Расшифровка.УдалитьВсе();

	Пока Опер.ПолучитьПроводку() = 1 Цикл
		Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
		    Если Опер.НомерЖурнала<>ВыбЖурнал Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;
		Если (Опер.СложнаяПроводка() = 0) Или (Опер.НомерКорреспонденции() > 1) Тогда
			Сумма = Сумма+Опер.Сумма;
			Количество = Количество+Опер.Количество;
			ВалСумма = ВалСумма+Опер.ВалСумма;
		КонецЕсли;
		Расшифровка.Установить("Документ", Опер.Документ.ТекущийДокумент());
		Расшифровка.Установить("НомерПроводки", Опер.НомерПроводки());
		Расшифровка.Установить("НомерКорреспонденции", Опер.НомерКорреспонденции());
		СерНом="";
		Если Опер.Документ.Вид()="ПутевойЛист" Тогда
		    СерНом=СерНом+" "+СокрЛП(Опер.Документ.СерияПЛ)+" № "+СокрЛП(Опер.Документ.НомерПЛ);
		ИначеЕсли (Опер.Документ.Вид()="ПрихНалоговаяНакладная") или (Опер.Документ.Вид()="РасхНалоговаяНакладная") Тогда
		    СерНом=СерНом+" "+СокрЛП(Опер.Документ.СерияСФ)+" № "+СокрЛП(Опер.Документ.НомерСФ);
		ИначеЕсли (Опер.Документ.Вид()="ПриходнаяНакладная") или (Опер.Документ.Вид()="РасходнаяНакладная")Или(Опер.Документ.Вид()="ЗакупочныйАкт") Тогда
		    СерНом=СерНом+" "+СокрЛП(Опер.Документ.СерияТТН)+" № "+СокрЛП(Опер.Документ.НомерТТН);
		КонецЕсли;
		Т.ВывестиСекцию("Секция_2");
	КонецЦикла;
	Т.ВывестиСекцию("Секция_3");
	Т.ВывестиСекцию("Подпись");
    Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, 4, 0, "ОпцииПечатиОтчетыПоПроводкам", "ОтчетыПоПроводкам");
	Т.ОбластьПечати(2);
	Т.ПараметрыСтраницы(1,,,,,,,,, 1);
	Т.Показать("Журнал проводок ("+Дата1+"-"+Дата2+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

Процедура ВыбратьЖурнал()
	Журналы = СоздатьОбъект("СписокЗначений");
	ПолучитьЗначенияОтбора("НомерЖурнала", Журналы);
	Журналы.ВыбратьЗначение(ВыбЖурнал, "Выбор журнала",,, ?(Журналы.РазмерСписка()<20, 1, 0));
КонецПроцедуры

Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	ИспФильтр=1;

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		НазначитьВид(Валюта, Метаданные.Валюта.Идентификатор);
		Форма.Валюта.НеИзменятьВид(1);
	Иначе
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		ВыбПланСчетов = 0;
		Обновить = глОбновить;
		ПланСчетов = глРасшифровка.Получить("ПланСчетов");
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = глРасшифровка.Получить("Дата1");
		Дата2 = глРасшифровка.Получить("Дата2");
		Валюта = глРасшифровка.Получить("Валюта");
		ПоВалюте = глРасшифровка.Получить("ПоВалюте");
		ВыбЖурнал= глРасшифровка.Получить("НомерЖурнала");
		ИспФильтр=Число(глРасшифровка.Получить("ИспФильтр"));
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить=2 Тогда   //Не поддерживается "Настройка для варианта без фильтра"
			ИспФильтр=1;
		КонецЕсли;

		Если ИспФильтр=1 Тогда
			ВыбФильтр = глРасшифровка.Получить("Фильтр");
		Иначе
			Счет = глРасшифровка.Получить("Счет");
			КорСчет = глРасшифровка.Получить("КорСчет");
			ДтКт = глРасшифровка.Получить("ДтКт");
			ВидСубк1 = глРасшифровка.Получить("ВидСубконто1");
			Субконто1 = глРасшифровка.Получить("Субконто1");
			ВидСубк2 = глРасшифровка.Получить("ВидСубконто2");
			Субконто2 = глРасшифровка.Получить("Субконто2");
			ВидСубк3 = глРасшифровка.Получить("ВидСубконто3");
			Субконто3 = глРасшифровка.Получить("Субконто3");
			КорВидСубк1 = глРасшифровка.Получить("КорВидСубконто1");
			КорСубконто1 = глРасшифровка.Получить("КорСубконто1");
			КорВидСубк2 = глРасшифровка.Получить("КорВидСубконто2");
			КорСубконто2 = глРасшифровка.Получить("КорСубконто2");
			КорВидСубк3 = глРасшифровка.Получить("КорВидСубконто3");
			КорСубконто3 = глРасшифровка.Получить("КорСубконто3");
			ВыбФильтр = "";
		КонецЕсли;
		Если Обновить <> 2 Тогда
			Сформировать(ВыбФильтр);
			глРасшифровка = "";
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
	    Дата1 = НачалоПериодаБИ();
	    Дата2 = КонецПериодаБИ();
	КонецЕсли;
	ВосстановитьЗначение("ОЖО_Фильтры",Фильтры);
КонецПроцедуры

Процедура ПриЗакрытии()
	СохранитьЗначение("ОЖО_Фильтры",Фильтры);
КонецПроцедуры

Процедура Запомнить()        
	Если СокрЛП(ВыбФильтр) <> "" Тогда
		Фильтры.ДобавитьЗначение(ВыбФильтр);
		Фильтры.ТекущаяСтрока(Фильтры.РазмерСписка());
	КонецЕсли;
КонецПроцедуры

Процедура Удалить()
	Если Фильтры.ТекущаяСтрока()>0 Тогда
		Фильтры.УдалитьЗначение(Фильтры.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура Выбрать()
	Если Фильтры.ТекущаяСтрока()>0 Тогда
		ВыбФильтр=Фильтры.ПолучитьЗначение(Фильтры.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура Сформировать1()
	С = Фильтры.ТекущаяСтрока();
	Сформировать(Фильтры.ПолучитьЗначение(С));
КонецПроцедуры