Перем Документы;
Перем ОтобранныеДокументы;
    
Перем Т;		
Перем Обновить;
Перем Расшифровка; 
                 
Перем ВариантыГрупповогоВклИсклДокументов;
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

Процедура ВставитьВсе(СписокДокументов)
    СписокДокументов.УдалитьВсе();
	Для Инд=1 По Метаданные.Документ() Цикл
		СписокДокументов.ДобавитьЗначение(Метаданные.Документ(Инд).Идентификатор,Метаданные.Документ(Инд).Представление());
	КонецЦикла;
	СписокДокументов.СортироватьПоПредставлению();
КонецПроцедуры

Процедура ГрупповойПереносИзСпискаВСписок(Список1,Список2)
	Перем Вариант,МДДокумент,КолДокументов,Индекс;
	
	Вариант = ВариантыГрупповогоВклИсклДокументов.ПолучитьЗначение(1);
	ВариантыГрупповогоВклИсклДокументов.ВыбратьЗначение(Вариант,"",,,1);
	
	Если ПустоеЗначение(Вариант)>0 Тогда	// все
		Список1.УдалитьВсе();
		ВставитьВсе(Список2);
	Иначе									// документы определенного учета
		Вариант = ВРег(Вариант);
		КолДокументов = Метаданные.Документ();
		Для Индекс = 1 По КолДокументов Цикл	// перебираем все док-ты
			МДДокумент = Метаданные.Документ(Индекс);
			
			УдовлетворяетФильтру = 0;
			
			Если Вариант="БУХГАЛТЕРСКИЙУЧЕТ" Тогда
				УдовлетворяетФильтру = МДДокумент.БУХГАЛТЕРСКИЙУЧЕТ;
				
			ИначеЕсли Вариант="ОПЕРАТИВНЫЙУЧЕТ" Тогда             
				УдовлетворяетФильтру = МДДокумент.ОПЕРАТИВНЫЙУЧЕТ;
				
			ИначеЕсли Вариант="РАСЧЕТ" Тогда           
				УдовлетворяетФильтру = МДДокумент.РАСЧЕТ;
			КонецЕсли;	
			
			Если ПустоеЗначение(УдовлетворяетФильтру)=0 Тогда
				Поз = Список1.НайтиЗначение(МДДокумент.Идентификатор);
				Если Поз>0 Тогда	// есть в списке -> надо переносить
					Список1.УдалитьЗначение(Поз);
					Список2.ДобавитьЗначение(МДДокумент.Идентификатор,МДДокумент.Представление());
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;	
	    Список1.СортироватьПоПредставлению(); 
		Список2.СортироватьПоПредставлению();
	КонецЕсли;	
		
КонецПроцедуры

Процедура ИсключитьВсе()                       
	ГрупповойПереносИзСпискаВСписок(ВыбранныеДокументы,ВсеДокументы);
КонецПроцедуры

Процедура ВключитьВсе()
	ГрупповойПереносИзСпискаВСписок(ВсеДокументы,ВыбранныеДокументы);
КонецПроцедуры

Процедура Исключить()
	ТекПоз = ВыбранныеДокументы.ТекущаяСтрока();
	Если ТекПоз = 0 Тогда
		Возврат;
	КонецЕсли; 
	Представление = "";
	Позиция = ВыбранныеДокументы.ПолучитьЗначение(ТекПоз, Представление);
	ВсеДокументы.ДобавитьЗначение(Позиция, Представление);
	ВсеДокументы.СортироватьПоПредставлению();                
	ВыбранныеДокументы.УдалитьЗначение(ТекПоз);
	Если ВыбранныеДокументы.РазмерСписка() > 0 Тогда
		Если ТекПоз > ВыбранныеДокументы.РазмерСписка() Тогда
			ТекПоз = ВыбранныеДокументы.РазмерСписка();
		КонецЕсли;
		ВыбранныеДокументы.ТекущаяСтрока(ТекПоз);
	КонецЕсли;                                 
КонецПроцедуры

Процедура Включить()
	ТекПоз = ВсеДокументы.ТекущаяСтрока();
	Если ТекПоз = 0 Тогда
		Возврат;
	КонецЕсли;
	Представление = "";
	Позиция = ВсеДокументы.ПолучитьЗначение(ТекПоз, Представление);
	ВыбранныеДокументы.ДобавитьЗначение(Позиция, Представление);
	ВыбранныеДокументы.СортироватьПоПредставлению();
	ВсеДокументы.УдалитьЗначение(ТекПоз);
	Если ВсеДокументы.РазмерСписка() > 0 Тогда
		Если ТекПоз > ВсеДокументы.РазмерСписка() Тогда
			ТекПоз = ВсеДокументы.РазмерСписка();
		КонецЕсли;
		ВсеДокументы.ТекущаяСтрока(ТекПоз);
	КонецЕсли;                           
КонецПроцедуры

Процедура Сформировать(Режим,ЗакрытьЭкран=0)
	Перем ИтоговаяСумма;
	ВидыДок = "";
	Для Инд=1 По ВыбранныеДокументы.РазмерСписка() Цикл
		ВидыДок = ВидыДок+ВыбранныеДокументы.ПолучитьЗначение(Инд)+",";
	КонецЦикла;
	Если ПустаяСтрока(ВидыДок)=1 Тогда
		Возврат;
	КонецЕсли;     
	ВидыДок = ","+ВидыДок;
	Док=СоздатьОбъект("Документ");
	Если (ВыбКонтрагент.Выбран()=1) Тогда
		Док.ВыбратьПоЗначению(ДатаНачала,ДатаКонца,"Контрагент",ВыбКонтрагент);
	Иначе
		Док.ВыбратьДокументы(ДатаНачала,ДатаКонца);
	КонецЕсли;
	Если Режим="Печать" Тогда
		Если (ТипЗначенияСтр(Т) <> "Таблица") ИЛИ (Обновить = 0) Тогда
		   	Т = СоздатьОбъект("Таблица");
		Иначе
		 	Т.Очистить();
		КонецЕсли;
	    Т.ИсходнаяТаблица( "Таблица" );
		Расшифровка = СоздатьОбъект("СписокЗначений");
	    Расшифровка.Установить("Отчет", "ОбработкаДокументов");
		Расшифровка.Установить("ДатаНачала", ДатаНачала);
	    Расшифровка.Установить("ДатаКонца", ДатаКонца);
		Расшифровка.Установить("ВыбКонтрагент", ВыбКонтрагент);
		Расшифровка.Установить("ВыбДоговор", ВыбДоговор);
		Расшифровка.Установить("ВыбрСтрока", ВыбрСтрока);
		Расшифровка.Установить("ВклПроведенные", ВклПроведенные); 
		Расшифровка.Установить("ВклТекущие", ВклТекущие);
		Расшифровка.Установить("ВклУдаленные", ВклУдаленные);
		Расшифровка.Установить("ВыводитьИтоги", ВыводитьИтоги);
		Расшифровка.Установить("ВыбранныеДокументы", ВыбранныеДокументы);
		Расшифровка.Установить("ВсеДокументы", ВсеДокументы);
		Заг = "";

		Если ВыбКонтрагент.Выбран()>0 Тогда
			Заг=Заг+"По контрагенту "+СокрП(ВыбКонтрагент)+". ";
		КонецЕсли; 
		Если ВыбДоговор.Выбран()>0 Тогда
			Заг=Заг+"По договору "+СокрП(ВыбДоговор)+". ";
		КонецЕсли; 
		
		Т.ВывестиСекцию("Кнопки");		
		Т.ВывестиСекцию("Отчет");
		
	ИначеЕсли Режим="Обработка" Тогда	
		
		ОтобранныеДокументы = СоздатьОбъект("СписокЗначений");
		ОтобранныеДокументы.УдалитьВсе();
		
	КонецЕсли;
	
	НПП=0;             
	ИтоговаяСумма=0;
	
	Пока Док.ПолучитьДокумент()=1 Цикл
		Если (Док.ПометкаУдаления()>0) Тогда
			Если ВклУдаленные=0 Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли (Док.Проведен()>0) Тогда
			Если ВклПроведенные=0 Тогда
				Продолжить;
			КонецЕсли;	       
		Иначе	           
			Если ВклТекущие=0 Тогда
				Продолжить;
			КонецЕсли;	        
		КонецЕсли;	       
		Если ВыбДоговор.Выбран() = 0 Тогда
		ИначеЕсли глЕстьРеквизитШапки("Договор",Док.Вид())=Да Тогда
			Если (Док.Договор <> ВыбДоговор) Тогда
		    	Продолжить;
			КонецЕсли;
		ИначеЕсли глЕстьРеквизитМнЧ("Договор",Док.Вид())=Да Тогда
			Нашли = 0;
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку()>0 Цикл
				Если (Док.Договор = ВыбДоговор) Тогда
					Нашли = 1;
		    		Прервать;
				КонецЕсли;
			КонецЦикла;	      
			Если Нашли = 0 Тогда
				Продолжить;
			КонецЕсли;	
		Иначе
			Продолжить;
		КонецЕсли;
		            
		
		//Проверка строки операции
		Если ПустаяСтрока(ВыбрСтрока) = 1 Тогда
		ИначеЕсли Док.СуществуетОперация() = 0 Тогда
			Продолжить;
		ИначеЕсли Найти(ВРег(Док.Операция.Содержание),ВРег(ВыбрСтрока)) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка вида документа
		Если Найти(ВидыДок,","+Док.Вид()+",")=0 Тогда
		    Продолжить;
		КонецЕсли;

		Состояние("Обработка "+Док.ДатаДок);

		НПП=НПП+1;
		
		Если Режим="Печать" Тогда
			
			ВидДок = Док.Вид();
	    	
			Если глЕстьРеквизитШапки("Валюта",ВидДок)=Нет Тогда
				ПечВалюта = СокрЛП(Леи);
			Иначе
				ПечВалюта = СокрЛП(Док.Валюта);
			КонецЕсли;	
			
			ПечЗнак="";
			ПечСумма="";
			РеквСумма=Метаданные.Документ(ВидДок).РеквизитТабличнойЧасти("СумЛей"); 
			Если РеквСумма.Выбран()=1 Тогда
				Если РеквСумма.ИтогПоКолонке=1 Тогда
					
					ДопСуммаНП=0;
					РеквЗапретитьСторнНП=Метаданные.Документ(ВидДок).РеквизитШапки("ЗапретитьСторнироватьУчетНП"); 
					Если РеквЗапретитьСторнНП.Выбран()=1 Тогда
						Если Док.ЗапретитьСторнироватьУчетНП<>0 Тогда
							РеквСуммаНП=Метаданные.Документ(ВидДок).РеквизитТабличнойЧасти("СуммаНП");
							Если РеквСуммаНП.Выбран()=1 Тогда
								Если РеквСуммаНП.ИтогПоКолонке=1 Тогда
									ДопСуммаНП = Док.Итог("СуммаНП");
								КонецЕсли;	
							КонецЕсли;	
						КонецЕсли;	
					КонецЕсли;
					
					ПечСумма=Формат(Док.Итог("СумЛей")+ДопСуммаНП,"Ч14.2-,")+" "+ПечВалюта;
					ИтоговаяСумма = ИтоговаяСумма+Док.Итог("СумЛей")+ДопСуммаНП;
				КонецЕсли;	
			Иначе
				РеквСумма=Метаданные.Документ(ВидДок).РеквизитШапки("СумЛей");
			    Если РеквСумма.Выбран()=1 Тогда
					ПечСумма=Формат(Док.Сумма,"Ч14.2-,")+" "+ПечВалюта;
					ИтоговаяСумма = ИтоговаяСумма+Док.Сумма;
				Иначе
					Если Док.Вид()="ДвиженияДенежныхСредств" Тогда
						ПечСумма=Формат(Док.Итог("Приход"),"Ч14.2-,")+" "+ПечВалюта+РазделительСтрок+Формат(Док.Итог("Расход"),"Ч14.2-,")+" "+ПечВалюта;
						ИтоговаяСумма = ИтоговаяСумма+Док.Итог("Приход")-Док.Итог("Расход");
						ПечЗнак="+"+РазделительСтрок+"-";
					Иначе
						ПечСумма="";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;                       
			
			//ПечКлиент=Строка(Док.Графа("Клиент"));
			Если глЕстьРеквизитШапки("Клиент",ВидДок)=Да Тогда
				ПечКлиент="Клиент: "+СокрЛП(Док.Клиент);
			ИначеЕсли глЕстьРеквизитШапки("СубкПокупателя1",ВидДок)=Да Тогда
				ПечКлиент="Клиент: "+СокрЛП(Док.СубкПокупателя1);
			//ИначеЕсли ВидДок="Перемещение" Тогда
			//	ПечКлиент="Отправитель: "+СокрП(Док.Склад)+РазделительСтрок+"Получатель: "+СокрП(Док.СкладПолучатель);
			ИначеЕсли ВидДок="Списание" Тогда
				ПечКлиент="Склад: "+СокрП(Док.Склад)+"; ";
			ИначеЕсли ВидДок="ДвиженияДенежныхСредств" Тогда
//				ПечКлиент="Расчетный счет: "+СокрП(глПредставлениеРасчСчета(Док));
			ИначеЕсли глЕстьРеквизитШапки("Контрагент",ВидДок)=Да Тогда 
				ПечКлиент="Контрагент: "+СокрП(Док.Контрагент);
			ИначеЕсли глЕстьРеквизитШапки("Поставщик",ВидДок)=Да Тогда 
				ПечКлиент="Поставщик: "+СокрП(Док.Поставщик);
			ИначеЕсли глЕстьРеквизитШапки("Покупатель",ВидДок)=Да Тогда 
				ПечКлиент="Покупатель: "+СокрП(Док.Покупатель);
			ИначеЕсли (ВидДок="ИсполнительныйЛист")ИЛИ(ВидДок="ПеречислениеЗарплатыВБанк") Тогда	            
				ПечКлиент="Получатель: "+СокрП(Док.Получатель);  
			ИначеЕсли глЕстьРеквизитШапки("СубкПоставщика1",ВидДок)=Да Тогда
				Если Док.СубкПоставщика1.Вид() = "Контрагенты" Тогда 
					ПечКлиент="Контрагент: "+СокрП(Док.СубкПоставщика1); 
						КонецЕсли;
			Иначе
				ПечКлиент="";
			КонецЕсли;                   
			    
			ПечКлиент = СокрЛП(ПечКлиент);
			Если ПечКлиент<>"" Тогда
				ПечКлиент = ПечКлиент+". "
			КонецЕсли;	
			
			//ПечДоговор = Строка(Док.Графа("Договор1"));
			ПечДоговор="";
			Если глЕстьРеквизитШапки("Договор",ВидДок)=Да Тогда
			    //Если Док.Договор.Выбран()>0 Тогда
					ПечДоговор="Договор: "+СокрЛП(Док.Договор);
				//КонецЕсли;	  
			КонецЕсли;
			
			ПечДоговор = СокрЛП(ПечДоговор);
			Если ПечДоговор<>"" Тогда
				ПечДоговор = ПечДоговор+". "
			КонецЕсли;	
			
			//ПечСотрудник = Строка(Док.Графа("Сотрудник"));
			ПечСотрудник="";
			Если глЕстьРеквизитШапки("Сотрудник",ВидДок)=Да Тогда
			    Если Док.Сотрудник.Выбран()>0 Тогда
					ПечСотрудник="Сотрудник: "+СокрЛП(Док.Сотрудник)+". ";
				КонецЕсли;	
			КонецЕсли;
			
			ПечСотрудник = СокрЛП(ПечСотрудник);
			Если ПечСотрудник<>"" Тогда
				ПечСотрудник = ПечСотрудник+". "
			КонецЕсли;
			
			ПечОснование= "";
			Если ПустоеЗначение(Док.Основание) = 0 Тогда
				ПечОснование="Основ.: "+СокрЛП(Док.Основание)+". ";
			КонецЕсли;	
			
			ПечИнформация = ПечКлиент + ПечДоговор + ПечСотрудник+ПечОснование;;
			
			ПечСтатус = "";
			Если (Док.ПометкаУдаления()>0) Тогда
				ПечСтатус = "Помечен на уд.";
			ИначеЕсли (Док.Проведен()>0) Тогда
				ПечСтатус = "Проведен";
			КонецЕсли;	       
		    
			ПечУчет = "";
	    
			Т.ВывестиСекцию("Докум");
	
		ИначеЕсли Режим="Обработка" Тогда	
		
			ОтобранныеДокументы.ДобавитьЗначение(Док.ТекущийДокумент());
			
		КонецЕсли;
	
	КонецЦикла;

	Если Режим = "Печать" Тогда
		
		Т.ВывестиСекцию("КонецОтчета");
		
		Если ВыводитьИтоги<>0 Тогда  
			ПечСумма = Формат(ИтоговаяСумма,"Ч14.2-,");
			Т.ВывестиСекцию("Итоги");
		КонецЕсли;	
		
		Т.Опции(0,0,6,0,"ОпцииПечатиРеестра");
		Т.ТолькоПросмотр(1);
		Т.Показать("Реестр Документов","");
		
		Если (Обновить = 2)ИЛИ(ЗакрытьЭкран=1) Тогда
			СтрокаДействийФормы = "#Закрыть";
		КонецЕсли;

	ИначеЕсли Режим="Обработка" Тогда	
		Если ОтобранныеДокументы.РазмерСписка()=0 Тогда
			Предупреждение("Список документов пуст");
		Иначе	
			ОткрытьФорму("Обработка.ГрупповаяОбработкаДокументов",ОтобранныеДокументы);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры 		// Сформировать

Процедура ПриВыбореДоговора()	
	Если (ВыбДоговор.Выбран()>0)И(ВыбКонтрагент.Выбран()=0) Тогда
		ВыбКонтрагент = ВыбДоговор.Владелец;
	КонецЕсли;	
КонецПроцедуры 		// ПриВыбореДоговора
    
Процедура ПриНачалеВыбораЗначения(Элемент,Флаг)	// Предопределенная процедура
	Если Элемент="ВыбДоговор" Тогда	// выбираем договор
		Если (ВыбКонтрагент.Выбран()>0)И(ВыбКонтрагент.ЭтоГруппа()=0) Тогда
			ВыбДоговор.ИспользоватьВладельца(ВыбКонтрагент.ТекущийЭлемент());
		Иначе
			ВыбДоговор.ИспользоватьВладельца(0);
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры
               
Процедура ПриОткрытии()	
	Если глФлагРасшифровки = 1 Тогда 
		Обновить = глОбновить;
		ДатаНачала = глРасшифровка.Получить("ДатаНачала");
		ДатаКонца = глРасшифровка.Получить("ДатаКонца");
	                        
		ВыбФирма = глРасшифровка.Получить("ВыбФирма");
		ВыбКонтрагент = глРасшифровка.Получить("ВыбКонтрагент"); 
		ВыбДоговор = глРасшифровка.Получить("ВыбДоговор"); 
		ВыбрСтрока = глРасшифровка.Получить("ВыбрСтрока"); 
		
		ВклПроведенные = глРасшифровка.Получить("ВклПроведенные"); 
		ВклТекущие = глРасшифровка.Получить("ВклТекущие"); 
		ВклУдаленные = глРасшифровка.Получить("ВклУдаленные"); 
		                                                       
		ВыводитьИтоги = глРасшифровка.Получить("ВыводитьИтоги"); 
		
		ВыбранныеДокументы.УдалитьВсе();
	  	глРасшифровка.Получить("ВыбранныеДокументы").Выгрузить(ВыбранныеДокументы); 
	
		ВсеДокументы.УдалитьВсе();
		глРасшифровка.Получить("ВсеДокументы").Выгрузить(ВсеДокументы); 
	    
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;           
		
		Если Обновить <> 2 Тогда
			Сформировать("Печать");
			СтатусВозврата(0);
			Возврат;       
		КонецЕсли;           
	Иначе
		Обновить = 0;
	КонецЕсли;
	
КонецПроцедуры

ДатаКонца=КонецПериодаБИ();
ДатаНачала=НачалоПериодаБИ();

ВыбКонтрагент.ВыборГруппы(0);
	
Документы = СоздатьОбъект("СписокЗначений");
Документы.УдалитьВсе();
Для Инд=1 По Метаданные.Документ() Цикл
	Документы.ДобавитьЗначение(Метаданные.Документ(Инд).Идентификатор);
КонецЦикла;
	           
ВсеДокументы.УдалитьВсе();
ВставитьВсе(ВыбранныеДокументы);
		
ВклПроведенные=1; 
ВклТекущие=1; 
ВклУдаленные=1;  

ВариантыГрупповогоВклИсклДокументов = СоздатьОбъект("СписокЗначений");
ВариантыГрупповогоВклИсклДокументов.УдалитьВсе();
ВариантыГрупповогоВклИсклДокументов.ДобавитьЗначение("","все"); 
ВариантыГрупповогоВклИсклДокументов.ДобавитьЗначение("БухгалтерскийУчет","бухгалтерский учет"); 
ВариантыГрупповогоВклИсклДокументов.ДобавитьЗначение("Расчет","расчет"); 
ВариантыГрупповогоВклИсклДокументов.ДобавитьЗначение("ОперативныйУчет","оперативный учет");