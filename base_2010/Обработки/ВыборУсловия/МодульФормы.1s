Перем гТекРегистр,гСписокИсхПараметров;
Перем гРежим,гСпрМД,гДокМД;

Функция ПолучитьТип(СтрТип,Длина,Точность)
	Если ВыбАттр.ТекущаяСтрока()=0 Тогда
		Возврат "";
	КонецЕсли;
	СтрМД=ВыбАттр.ПолучитьЗначение(ВыбАттр.ТекущаяСтрока());
	Если гРежим="Регистр" Тогда
		МДАттр=гТекРегистр.Измерение(СтрМД);
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гТекРегистр.Реквизит(СтрМД);
		КонецЕсли;
		Если (МДАттр.Выбран()=0)И(СтрМД="Текущий документ") Тогда
			СтрТип="Документ";
			Тип=СтрТип;
			Длина=0;
			Точность=0;
		Иначе
			СтрТип=МДАттр.Тип;
			Тип=МДАттр.Тип;
			Вид=МДАттр.Вид;
			Если Вид<>"" Тогда
				СтрТип=СтрТип+"."+МДАттр.Вид;
			КонецЕсли;
			Длина=МДАттр.Длина;
			Точность=МДАттр.Точность;
		КонецЕсли;
    ИначеЕсли гРежим="Справочник" Тогда
		МДАттр=гСпрМД.Реквизит(СтрМД);
		Если МДАттр.Выбран()=1 Тогда
			СтрТип=МДАттр.Тип;
			Тип=МДАттр.Тип;
			Если Вид<>"" Тогда
				СтрТип=СтрТип+"."+МДАттр.Вид;
			КонецЕсли;
			Длина=МДАттр.Длина;
			Точность=МДАттр.Точность;
		Иначе
			Спр = СоздатьОбъект("Справочник." + СтрМД);
			Если СтрМД=гСпрМД.Идентификатор Тогда
				МДАттр=гСпрМД;
				ФлГруппа=1;
			ИначеЕсли МДАттр.Выбран()=0 Тогда
				МДАттр=гСпрМД.Владелец;
			КонецЕсли;
			СтрТип="Справочник"+"."+Спр.Вид();
			Тип=СтрТип;
			Длина=0;
			Точность=0;
		КонецЕсли;
	ИначеЕсли гРежим="Документ" Тогда
		МДАттр=Метаданные.ОбщийРеквизитДокумента(СтрМД);
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гДокМД.РеквизитШапки(СтрМД);
		КонецЕсли;
		Если МДАттр.Выбран()=0 Тогда
			МДАттр=гДокМД.РеквизитТабличнойЧасти(СтрМД);
		КонецЕсли;
		СтрТип=МДАттр.Тип;
		Если Вид<>"" Тогда
			СтрТип=СтрТип+"."+МДАттр.Вид;
		КонецЕсли;
		Тип=МДАттр.Тип;
		Длина=МДАттр.Длина;
		Точность=МДАттр.Точность;
 	КонецЕсли;
	Возврат Тип;
КонецФункции

Процедура ПриВыбореУсловия()
	Перем Стр,Длина,Точность;
	СтрТип=ПолучитьТип(Стр,Длина,Точность);
	ВыбЗначение=ВыбУсловие.ПолучитьЗначение(ВыбУсловие.ТекущаяСтрока());
	Если ((Найти(ВыбЗначение,"<")<>0)ИЛИ(Найти(ВыбЗначение,">")<>0))И(ВыбЗначение<>"<>") Тогда
		Если (СтрТип<>"Число")И(СтрТип<>"Строка")И(СтрТип<>"Дата") Тогда
			Предупреждение("Сравнения на больше-меньше допустимы только над значениями совпадающих базовых типов (число, строка, дата)");
			ВыбУсловие.ТекущаяСтрока(1);
		КонецЕсли;
	ИначеЕсли ВыбЗначение="В" Тогда
		Если СтрЧислоВхождений(СтрТип,"Справочник")=0 Тогда
			Предупреждение("Условие вхождения можно задавать только для справочников!");
			ВыбУсловие.ТекущаяСтрока(1);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриВыбАттр()
	Перем Стр,Длина,Точность;
	ВыбФильтр=0;
	ФлГруппа=0;
	ВыбУсловие.ТекущаяСтрока(1);
	Если ВыбАттр.ТекущаяСтрока()=0 Тогда
		Возврат;
	КонецЕсли;
	СтрМД=ВыбАттр.ПолучитьЗначение(ВыбАттр.ТекущаяСтрока());
	Если ПустаяСтрока(СтрМД)=1 Тогда
		Форма.ВыбФильтр.НазначитьТип("");
		Возврат;
	КонецЕсли;
	ПолучитьТип(Стр,Длина,Точность);
	Если Длина>0 Тогда
		Форма.ВыбФильтр.НазначитьТип(Стр,Длина,Точность);
	Иначе
		Форма.ВыбФильтр.НазначитьТип(Стр);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаВыбораЗначения(Зн,Эл,Ст)
	ВыбФильтр=Зн;
	Ст=0;
КонецПроцедуры

Процедура Выбрать()
	Перем СтрПредставления;
	ТекСтр=ВыбАттр.ТекущаяСтрока();
	Если ТекСтр=0 Тогда
		Предупреждение("Внимание!Не выбрано измерение регистра!");
		Возврат;
	КонецЕсли;
	ТекИзмерение=ВыбАттр.ПолучитьЗначение(ТекСтр,СтрПредставления);
	Если ПустоеЗначение(ТекИзмерение)=1 Тогда
		Предупреждение("Внимание!Не выбрано измерение регистра!");
		Возврат;
	КонецЕсли;
	Если (СтрПредставления="Родитель")ИЛИ(СтрПредставления="Владелец") Тогда
		ТекИзмерение=СтрПредставления;
	КонецЕсли;
	ТекСтр=ВыбУсловие.ТекущаяСтрока();
	Если ТекСтр=0 Тогда
		Предупреждение("Внимание!Не выбрано условие!");
		Возврат;
	КонецЕсли;
	ТекУсловие=ВыбУсловие.ПолучитьЗначение(ТекСтр);
	Если ПустоеЗначение(ТекИзмерение)=1 Тогда
		Предупреждение("Внимание!Не выбрано условие!");
		Возврат;
	КонецЕсли;
	гСписокИсхПараметров.УдалитьВсе();
	гСписокИсхПараметров.ДобавитьЗначение(ТекИзмерение,"Атрибут");
	гСписокИсхПараметров.ДобавитьЗначение(СтрПредставления,"Представление");
	гСписокИсхПараметров.ДобавитьЗначение(ТекУсловие,"Условие");
	гСписокИсхПараметров.ДобавитьЗначение(ВыбФильтр,"Значение");
	гСписокИсхПараметров.ДобавитьЗначение(ФлИспользовать,"ВклУсловие");
    Форма.Параметр=гСписокИсхПараметров;
	Форма.Закрыть();
КонецПроцедуры

гСписокИсхПараметров=Форма.Параметр;
Если ПустоеЗначение(гСписокИсхПараметров)=0 Тогда
	гРежим=гСписокИсхПараметров.Получить("Режим");
	ИсхАтр= гСписокИсхПараметров.Получить("Атрибут");
	ИсхУсл=СокрЛП(гСписокИсхПараметров.Получить("Условие"));
	ИсхЗнач=гСписокИсхПараметров.Получить("Значение");
	ФлИспользовать=гСписокИсхПараметров.Получить("ВклУсловие");
	Если ПустоеЗначение(ИсхАтр)=1 Тогда
		ФлИспользовать=1;
	КонецЕсли;
	ВыбАттр.УдалитьВсе();

	Если гРежим="Регистр" Тогда
		гТекРегистр=гСписокИсхПараметров.Получить("Объект");
		Для Инд=1 По гТекРегистр.Измерение() Цикл
			ВыбАттр.ДобавитьЗначение(гТекРегистр.Измерение(Инд).Идентификатор,гТекРегистр.Измерение(Инд).Представление());
		КонецЦикла;
		Для Инд=1 По гТекРегистр.Реквизит() Цикл
			ВыбАттр.ДобавитьЗначение(гТекРегистр.Реквизит(Инд).Идентификатор,гТекРегистр.Реквизит(Инд).Представление());
		КонецЦикла;
		ВыбАттр.ДобавитьЗначение("Текущий документ","Текущий документ");
	ИначеЕсли гРежим="Справочник" Тогда
		гСпрМД=гСписокИсхПараметров.Получить("Объект");
		//владелец
		Если СокрЛП(гСпрМД.Владелец)<>"Метаданные" Тогда
			ВыбАттр.ДобавитьЗначение(гСпрМД.Владелец.Идентификатор, "Владелец");
		КонецЕсли;
		//родитель
		Если гСпрМД.КоличествоУровней>0 Тогда
			ВыбАттр.ДобавитьЗначение(гСпрМД.Идентификатор, "Родитель");
		КонецЕсли;
		Для Инд = 1 По гСпрМД.Реквизит() Цикл
			Если (гСпрМД.Реквизит(Инд).Использование ="ДляОбоих")ИЛИ
				 (гСпрМД.Реквизит(Инд).Использование ="ДляЭлемента") Тогда
				ВыбАттр.ДобавитьЗначение(гСпрМД.Реквизит(Инд).Идентификатор, гСпрМД.Реквизит(Инд).Представление());
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли гРежим="Документ" Тогда
		гДокМД=гСписокИсхПараметров.Получить("Объект");
		//общие реквизиты
		Для Инд = 1 По Метаданные.ОбщийРеквизитДокумента() Цикл
			Рекв=Метаданные.ОбщийРеквизитДокумента(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор,
					Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
		// Реквизиты шапки...
		Для Инд = 1 По гДокМД.РеквизитШапки() Цикл
			Рекв=гДокМД.РеквизитШапки(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор,
					Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
		//табличная часть
		Для Инд = 1 По гДокМД.РеквизитТабличнойЧасти() Цикл
			Рекв=гДокМД.РеквизитТабличнойЧасти(Инд);
			Если (Рекв.Тип<>"Строка")ИЛИ(Рекв.Длина<>0) Тогда
				ВыбАттр.ДобавитьЗначение(Рекв.Идентификатор,
					Рекв.Представление());
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ТекСтр=ВыбАттр.НайтиЗначение(ИсхАтр);
	Если (ТекСтр=0)И(ВыбАттр.РазмерСписка()>0) Тогда
		ТекСтр=1;
	КонецЕсли;
	ВыбАттр.ТекущаяСтрока(ТекСтр);

	ПриВыбАттр();
	ВыбФильтр=ИсхЗнач;
  	ТекФильтр=ВыбФильтр;
	ВыбУсловие.ДобавитьЗначение("=");
	ВыбУсловие.ДобавитьЗначение(">");
	ВыбУсловие.ДобавитьЗначение("<");
	ВыбУсловие.ДобавитьЗначение("<=");
	ВыбУсловие.ДобавитьЗначение(">=");
	ВыбУсловие.ДобавитьЗначение("<>");
	ВыбУсловие.ДобавитьЗначение("В");

	ТекСтр=ВыбУсловие.НайтиЗначение(ИсхУсл);
	Если (ВыбУсловие.НайтиЗначение(ИсхУсл)=0) Тогда
		ТекСтр=1;
	КонецЕсли;
	ВыбУсловие.ТекущаяСтрока(ТекСтр);
КонецЕсли;
Форма.Параметр=ПолучитьПустоеЗначение();
