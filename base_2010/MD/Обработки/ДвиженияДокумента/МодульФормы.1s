Перем ТипОтчетаДвижения;
Перем КрасныйЦвет;
Перем ЗеленыйЦвет;
Перем Расшифровка;
Перем Обновить;
Перем Таб;

// Данные функции оставлены на случай использования совместно с компонентой 1сТорговля
Функция ИзмерениеРесурса(ВидРегистра,ВидЗначения,Рег)

КонецФункции
	
Функция НаименованиеРегистра(МДРег)
	
КонецФункции

Функция ЗначениеРегистра(ВидРегистра,ВидЗначения,Рег)
	
КонецФункции

Функция РасшифровкаОбновить(Обновить,Расшифровка)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

Процедура ПоказатьТаблицу(Таб, ИмяОтчета, ЗагОкна, ФиксСтрок = 1, ФиксСтолбцов = 0, ПечатьВерх = 2, ПечатьПраво = 0)
	
	Если ТипЗначенияСтр(Таб) <> "Таблица" Тогда
		Предупреждение("Ошибка при попытке показать отчет
		               |""" + ЗагОкна + """.
		               |Попробуйте еще раз.", 20);
		Возврат;
	КонецЕсли;
	ФлагЧБпросмотра = ?(глЗначениеПоУмолчанию("ПоказыватьОтчетыВЦвете") = Нет, 1, 0);
	ФлагЧБПечати = ?(глЗначениеПоУмолчанию("ПечататьОтчетыВЦвете") = Да, 0, 1);
	ПечатьПраво = ?(ПечатьПраво = 0, Таб.ШиринаТаблицы(), ПечатьПраво);

	Таб.Опции(0, 0, ФиксСтрок, ФиксСтолбцов, "ИмяОпцийПечати" + ИмяОтчета, "ИмяРазмераОкна" + ИмяОтчета, ФлагЧБпросмотра);
	Таб.ОбластьПечати(ПечатьВерх, 2, , ПечатьПраво);
	Таб.ПараметрыСтраницы( , , , , , , , , , , ФлагЧБПечати, );
	Таб.ТолькоПросмотр(1);
	Таб.Показать(ЗагОкна, "");
	
КонецПроцедуры

Функция ФорматСум(Сумма, ОтображениеНулевыхЗначений = "")
	Возврат СокрЛ(Формат(Сумма, "Ч " + ОтображениеНулевыхЗначений + "15.2.,"));
КонецФункции

Процедура ДвиженияРегистров()
	
	Если Метаданные.Документ(ВыбДокумент.Вид()).ОперативныйУчет = 1 Тогда
		ЕстьДвиженияРегистров=0;
		Таб.ВывестиСекцию("ШапкаДвиженийРегистров");
		
		Для РНом = 1 По Метаданные.Регистр() Цикл
			МДРег = Метаданные.Регистр(РНом);
			Рег = СоздатьОбъект("Регистр." + МДРег.Идентификатор);
			Если Рег.ВыбратьДвиженияДокумента(ВыбДокумент) = 1 Тогда
				ЕстьДвиженияРегистров=1;
				Состояние("Обработка регистра: " + МДРег.Представление());

				Таб.ВывестиСекцию("ИмяРегистра");

				МаксКоличествоАтрибутов = Макс(МДРег.Измерение(), МДРег.Ресурс(), МДРег.Реквизит());

				Индекс = 0;
				Пока Рег.ПолучитьДвижение() = 1 Цикл
					Индекс = Индекс + 1;
					Состояние("Обработка регистра: " + МДРег.Представление() + ", движение: " + Индекс);

					Для х = 1 По МаксКоличествоАтрибутов Цикл
						СекцияДвижение = Таб.ПолучитьСекцию("Движение");
						Если х = 1 Тогда
							СекцияДвижение.ВидДвижения.ЦветТекста(?(Рег.Приход = 1, ЗеленыйЦвет, КрасныйЦвет));
							СекцияДвижение.ВидДвижения = ?(Рег.Приход = 1, "+", "-");
							СекцияДвижение.НомерСтроки = Рег.НомерСтроки();
						КонецЕсли;
						Если х <= МДРег.Измерение() Тогда
							Если (МДРег.Измерение(х).Идентификатор="Фирма") и (ПустоеЗначение(Рег.ПолучитьАтрибут(МДРег.Измерение(х).Идентификатор))=1) Тогда
								СекцияДвижение.Область(1, 7, 1, 30).Объединить();
								СекцияДвижение.Область(1, 7, 1, 30).ЦветТекста(КрасныйЦвет);
								СекцияДвижение.Область(1, 7, 1, 30).Полужирный(0);
								СекцияДвижение.Область(1, 7, 1, 30).РазмерШрифта(10);
								СекцияДвижение.Область(1, 7, 1, 30).ВертикальноеПоложение(3);
								СекцияДвижение.Область(1, 7, 1, 30).ГоризонтальноеПоложение(3);
								СекцияДвижение.Область(1, 7, 1, 30).Текст = "--!!! УПРАВЛЕНЧЕСКИЙ УЧЕТ !!!--";
							Иначе
								СекцияДвижение.Измерение = МДРег.Измерение(х).Представление();
								ЗначениеОбъекта=ЗначениеРегистра(МДРег.Идентификатор,МДРег.Измерение(х).Идентификатор,Рег);
								СекцияДвижение.ЗначениеИзмерения = ЗначениеОбъекта;
								
								Если ((ТипЗначенияСтр(ЗначениеОбъекта)="Справочник") или (ТипЗначенияСтр(ЗначениеОбъекта)="Документ")) и 
								(ПустоеЗначение(ЗначениеОбъекта)=0) Тогда
									СекцияДвижение.ЗначениеИзмерения.Расшифровка(ЗначениеОбъекта,1);
								КонецЕсли;
								
							КонецЕсли;
						КонецЕсли;
						Если х <= МДРег.Ресурс() Тогда
							СекцияДвижение.Ресурс = МДРег.Ресурс(х).Представление();
							СекцияДвижение.ЗначениеРесурса = ЗначениеРегистра(МДРег.Идентификатор,МДРег.Ресурс(х).Идентификатор,Рег);
							СекцияДвижение.ИзмерениеРесурса = ИзмерениеРесурса(МДРег.Идентификатор,МДРег.Ресурс(х).Идентификатор,Рег);
						КонецЕсли;
						Если х <= МДРег.Реквизит() Тогда
							СекцияДвижение.Реквизит = МДРег.Реквизит(х).Представление();
							ЗначениеОбъекта=ЗначениеРегистра(МДРег.Идентификатор,МДРег.Реквизит(х).Идентификатор,Рег);
							СекцияДвижение.ЗначениеРеквизита = ЗначениеОбъекта;
							
							Если ((ТипЗначенияСтр(ЗначениеОбъекта)="Справочник") или (ТипЗначенияСтр(ЗначениеОбъекта)="Документ")) и 
							(ПустоеЗначение(ЗначениеОбъекта)=0) Тогда
								СекцияДвижение.ЗначениеРеквизита.Расшифровка(ЗначениеОбъекта,1);
							КонецЕсли;
							
						КонецЕсли;
						Таб.ВывестиСекцию(СекцияДвижение);
					КонецЦикла;
					ВысТаб = Таб.ВысотаТаблицы();
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).Объединить();
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).ЦветТекста(?(Рег.Приход = 1, ЗеленыйЦвет, КрасныйЦвет));
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).РазмерШрифта(16);
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).Полужирный(1);
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).ВертикальноеПоложение(3);
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).ГоризонтальноеПоложение(3);
					Таб.Область(ВысТаб-МаксКоличествоАтрибутов+1, 2, ВысТаб, 3).Текст = ?(Рег.Приход = 1, "+", "-");
					Таб.Область(ВысТаб, 2, ВысТаб, 70).РамкаСнизу(4);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		Если ЕстьДвиженияРегистров=0 Тогда
			Таб.ВывестиСекцию("НаличиеЗаписей");
			Таб.Область(Таб.ВысотаТаблицы(), 2, Таб.ВысотаТаблицы(), 2).Текст = "Движений регистров по документу не обнаружено!";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроводкиДокумента()
	
	Если Метаданные.Документ(ВыбДокумент.Вид()).БухгалтерскийУчет = 1 Тогда
		ЕстьПроводки=0;
		Таб.ВывестиСекцию("ШапкаПроводок");
		О = СоздатьОбъект("Операция");
		Если О.НайтиОперацию(ВыбДокумент)>0 Тогда
			СекцияПроводка = Таб.ПолучитьСекцию("Проводка");
			О.ВыбратьПроводки();
			Пока О.ПолучитьПроводку() = 1 Цикл
				ЕстьПроводки=1;
				НомерПроводки = "" + О.НомерПроводки() + ?(О.СложнаяПроводка() = 1, "/" + О.НомерКорреспонденции(), "");
				Состояние("Обработка проводок. Проводка: " + НомерПроводки);
				СекцияПроводка.НомерПроводки = НомерПроводки;
				Расшифровка = СоздатьОбъект("СписокЗначений");
				Расшифровка.Установить("Документ", О.Документ.ТекущийДокумент());
				Расшифровка.Установить("НомерПроводки", О.НомерПроводки());
				Расшифровка.Установить("НомерКорреспонденции", О.НомерКорреспонденции());
				СекцияПроводка.Область().Расшифровка(Расшифровка,1);
				СекцияПроводка.СчетДт = О.Дебет.Счет;
				СекцияПроводка.СубконтоДт1 = О.Дебет.Субконто(1);
				СекцияПроводка.СубконтоДт2 = О.Дебет.Субконто(2);
				СекцияПроводка.СубконтоДт3 = О.Дебет.Субконто(3);
				Для Инд=1 По О.Дебет.Счет.КоличествоСубконто() Цикл
					Если ((ТипЗначенияСтр(О.Дебет.Субконто(Инд))="Справочник") или (ТипЗначенияСтр(О.Дебет.Субконто(Инд))="Документ")) и 
					(ПустоеЗначение(О.Дебет.Субконто(Инд))=0) Тогда
						СекцияПроводка.Область(Инд, 7, Инд, 19).Расшифровка(О.Дебет.Субконто(Инд),1);
					КонецЕсли;
				КонецЦикла;
				СекцияПроводка.СчетКт = О.Кредит.Счет;
				СекцияПроводка.СубконтоКт1 = О.Кредит.Субконто(1);
				СекцияПроводка.СубконтоКт2 = О.Кредит.Субконто(2);
				СекцияПроводка.СубконтоКт3 = О.Кредит.Субконто(3);
				Для Инд=1 По О.Кредит.Счет.КоличествоСубконто() Цикл
					Если ((ТипЗначенияСтр(О.Кредит.Субконто(Инд))="Справочник") или (ТипЗначенияСтр(О.Кредит.Субконто(Инд))="Документ")) и 
					(ПустоеЗначение(О.Кредит.Субконто(Инд))=0) Тогда
						СекцияПроводка.Область(Инд, 24, Инд, 35).Расшифровка(О.Кредит.Субконто(Инд),1);
					КонецЕсли;
				КонецЦикла;
				СекцияПроводка.ЕдВалСумма = О.Валюта;
				СекцияПроводка.ВалСумма = ФорматСум(О.ВалСумма, "0");
				СекцияПроводка.Количество = Формат(О.Количество, "Ч 015.3.,");
				ЕдКоличество="";
				Для Инд=1 По О.Дебет.Счет.КоличествоСубконто() Цикл
					Если ТипЗначения(О.Дебет.Субконто(Инд))=11 Тогда
						Если О.Дебет.Субконто(Инд).Вид()="Товары" Тогда
							ЕдКоличество=О.Дебет.Субконто(Инд).ЕдИзм;
						ИначеЕсли О.Дебет.Субконто(Инд).Вид()="Материалы" Тогда
							ЕдКоличество=О.Дебет.Субконто(Инд).ЕдИзм;
						ИначеЕсли О.Дебет.Субконто(Инд).Вид()="МБП" Тогда
							ЕдКоличество=О.Дебет.Субконто(Инд).ЕдИзм;
						КонецЕсли;
					КонецЕсли;
					Если ПустоеЗначение(ЕдКоличество)=0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ПустоеЗначение(ЕдКоличество)=1 Тогда
					Для Инд=1 По О.Кредит.Счет.КоличествоСубконто() Цикл
						Если ТипЗначения(О.Кредит.Субконто(Инд))=11 Тогда
							Если О.Кредит.Субконто(Инд).Вид()="Товары" Тогда
								ЕдКоличество=О.Кредит.Субконто(Инд).ЕдИзм;
							ИначеЕсли О.Кредит.Субконто(Инд).Вид()="Материалы" Тогда
								ЕдКоличество=О.Кредит.Субконто(Инд).ЕдИзм;
							ИначеЕсли О.Кредит.Субконто(Инд).Вид()="МБП" Тогда
								ЕдКоличество=О.Кредит.Субконто(Инд).ЕдИзм;
							КонецЕсли;
						КонецЕсли;
						Если ПустоеЗначение(ЕдКоличество)=0 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Если (О.Дебет.Счет.Количественный=0) и (О.Кредит.Счет.Количественный=0) Тогда
					ЕдКоличество="";
				КонецЕсли;
				
				СекцияПроводка.ЕдКоличество = ЕдКоличество;
				СекцияПроводка.Сумма = ФорматСум(О.Сумма, "0");
				СекцияПроводка.ЕдСумма = Леи;
				СекцияПроводка.Содержание = О.СодержаниеПроводки;
				СекцияПроводка.ЖО = О.НомерЖурнала;
				Таб.ВывестиСекцию(СекцияПроводка);
			КонецЦикла;
			Таб.Область(Таб.ВысотаТаблицы(), 2, Таб.ВысотаТаблицы(), 70).РамкаСнизу(4);
		КонецЕсли;
		Если ЕстьПроводки=0 Тогда
			Таб.ВывестиСекцию("НаличиеЗаписей");
			Таб.Область(Таб.ВысотаТаблицы(), 2, Таб.ВысотаТаблицы(), 2).Текст = "Бухгалтерские проводки по документу не сформированы!";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасчетыДокумента()
	
КонецПроцедуры

Процедура РеквизитыСправочников()
	
	Пер = СоздатьОбъект("Периодический");
	Если (Пер.ВыбратьПоДокументу(ВыбДокумент)=1) Тогда
		ВидСправочника="";
		ЭлементСправочника="";
		Таб.ВывестиСекцию("ШапкаРеквизитов");
		Пока Пер.ПолучитьЗначение()=1 Цикл
			СекцияРеквизит = Таб.ПолучитьСекцию("Реквизит");
			Если ЭлементСправочника<>Пер.ТекущийОбъект() Тогда
				СекцияРеквизит.Область(1, 13, 1, 57).РамкаСверху(3);
				ЭлементСправочника=Пер.ТекущийОбъект();
				СекцияРеквизит.КодЭлементаС	= СокрЛП(ЭлементСправочника.Код);
				СекцияРеквизит.ЭлементС 		= СокрЛП(ЭлементСправочника.Наименование);
			КонецЕсли; 
			
			Если ВидСправочника<>Пер.ТекущийОбъект().Вид() Тогда
				СекцияРеквизит.Область(1, 2, 1, 57).РамкаСверху(6);
				ВидСправочника=Пер.ТекущийОбъект().Вид();
				СекцияРеквизит.СправочникС = СокрЛП(Пер.ТекущийОбъект().ПредставлениеВида());
			КонецЕсли; 
			СекцияРеквизит.НомерСтрокиС = Пер.НомерСтроки();
			СекцияРеквизит.РеквизитС = Метаданные.Справочник(ВидСправочника).Реквизит(СтрЗаменить(Пер.ТекущийРеквизит(),ВидСправочника+".",""));
			СекцияРеквизит.ДатаЗнач = СокрЛП(Формат(Пер.ДатаЗнач,"ДДДММГГГГ"));
			СекцияРеквизит.Значение = Пер.Значение;
			СекцияРеквизит.Область(1, 2, 1, 57).Расшифровка(ЭлементСправочника,1);
			Таб.ВывестиСекцию(СекцияРеквизит);
		КонецЦикла;
		Таб.Область(Таб.ВысотаТаблицы(), 2, Таб.ВысотаТаблицы(), 57).РамкаСнизу(4);

	КонецЕсли;
	
КонецПроцедуры

Процедура Сформировать()

	Если ВыбДокумент.Выбран() = 0 Тогда
		Предупреждение("Выберите документ!");
		Возврат;
	КонецЕсли;
	Пер = СоздатьОбъект("Периодический");
	Если (Метаданные.Документ(ВыбДокумент.Вид()).ОперативныйУчет = 0) и
	     (Метаданные.Документ(ВыбДокумент.Вид()).БухгалтерскийУчет = 0) и
		 (Метаданные.Документ(ВыбДокумент.Вид()).Расчет = 0) и 
		 (Пер.ВыбратьПоДокументу(ВыбДокумент)=0) Тогда
		Предупреждение("Выберите документ, формирующий бухгалтерские проводки, движения по регистрам,"+
		РазделительСтрок+"записи журналов расчетов или устанавливающий реквизиты справочников!");
		Возврат;
	КонецЕсли;
	Если (ВыбДокумент.Проведен() = 0) и (ВыбДокумент.Вид() <> "Операция") и
	(Метаданные.Документ(ВыбДокумент.Вид()).Расчет = 0) Тогда
		Предупреждение("Выберите проведенный документ!");
		Возврат;
	КонецЕсли;

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
	Если ТипОтчетаДвижения = 0 Тогда
		ТипОтчетаДвижения = 1;
		ВсегоДействий=0;
		СписокДействий = СоздатьОбъект("СписокЗначений");
		СписокДействий.ДобавитьЗначение(1,"<<Все движения>>");
		Если (Метаданные.Документ(ВыбДокумент.Вид()).БухгалтерскийУчет = 1) Тогда
			СписокДействий.ДобавитьЗначение(2,"Бухгалтерские проводки");
			ВсегоДействий=ВсегоДействий+1;
		КонецЕсли;
		Если (Метаданные.Документ(ВыбДокумент.Вид()).ОперативныйУчет = 1) Тогда
			СписокДействий.ДобавитьЗначение(3,"Движения регистров");
			ВсегоДействий=ВсегоДействий+1;
		КонецЕсли;
		Если (Метаданные.Документ(ВыбДокумент.Вид()).Расчет = 1) Тогда
			СписокДействий.ДобавитьЗначение(4,"Записи журналов расчетов");
			ВсегоДействий=ВсегоДействий+1;
		КонецЕсли;
		Если (Пер.ВыбратьПоДокументу(ВыбДокумент)=1) Тогда
			СписокДействий.ДобавитьЗначение(5,"Реквизиты справочников");
			ВсегоДействий=ВсегоДействий+1;
		КонецЕсли;
		Если ВсегоДействий>1 Тогда
			Если СписокДействий.ВыбратьЗначение(ТипОтчетаДвижения ,,,,1)=0 Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ДвиженияДокумента");
	Расшифровка.Установить("Док", ВыбДокумент);
	Расшифровка.Установить("ТипОтчетаДвижения", ТипОтчетаДвижения);

	Если (ТипЗначенияСтр(Таб) <> "Таблица") или (Обновить = 0) Тогда
		Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	 
	Таб.ИсходнаяТаблица("ДвиженияДокумента");
	Таб.ВывестиСекцию("Кнопки");
	Таб.ВывестиСекцию("Заголовок");
	СекцияЦвета = Таб.ПолучитьСекцию("Цвета");
	ЗеленыйЦвет = СекцияЦвета.ЗеленыйЦвет.ЦветФона();
	КрасныйЦвет = СекцияЦвета.КрасныйЦвет.ЦветФона();
	Если (ТипОтчетаДвижения = 1) или (ТипОтчетаДвижения = 2) Тогда
		ПроводкиДокумента();
	КонецЕсли;
	Если (ТипОтчетаДвижения = 1) или (ТипОтчетаДвижения = 3) Тогда
		ДвиженияРегистров();
	КонецЕсли;
	Если (ТипОтчетаДвижения = 1) или (ТипОтчетаДвижения = 4) Тогда
		РасчетыДокумента();
	КонецЕсли;
	Если (ТипОтчетаДвижения = 1) или (ТипОтчетаДвижения = 5) Тогда
		РеквизитыСправочников();
	КонецЕсли;
	ПоказатьТаблицу(Таб, "ДвиженияДокумента", "Движения документа " + глПредставлениеДокумента(ВыбДокумент));
	ТипОтчетаДвижения=0;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если (ТипЗначенияСтр(Форма.Параметр)="Документ") или (Обновить = 1) Тогда
		ВыбДокумент=Форма.Параметр;
		Сформировать();
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Обновить = 0;
	Если глФлагРасшифровки = 1 Тогда
		Обновить = Число(глРасшифровка.Получить("Обновить"));
		ВыбДокумент = глРасшифровка.Получить("Док");
		ТипОтчетаДвижения = глРасшифровка.Получить("ТипОтчетаДвижения");
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;
		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

ВыбВалюта=Доллары;
ТипОтчетаДвижения = 0
