Процедура ПриОткрытии()
	ТекОпер=ВосстановитьЗначение("ТекОпер");
	АктивизироватьОбъект(ТекОпер);
КонецПроцедуры

Процедура ПриЗакрытии()
	СохранитьЗначение("ТекОпер",Документ.ТекущийДокумент());
КонецПроцедуры

Процедура Печать()
	Перем ИмяОтбора;
	Перем ЗначениеОтбора;
	Перем Док;
	Перем ВклПроводки;
	Док = 0;

	Меню=СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение(0,"Не выводить проводки");
	Меню.ДобавитьЗначение(1,"Выводить проводки");
	Если Меню.ВыбратьЗначение(ВклПроводки,"",,, 1) = 0 Тогда
		Возврат;
	КонецЕсли;

	ПолучитьОтбор(ИмяОтбора,ЗначениеОтбора);
	Дата1=НачалоИнтервала();
	Дата2=КонецИнтервала();
	Опер=СоздатьОбъект("Операция");
	Если Строка(ИмяОтбора)="" Тогда
		Опер.ВыбратьОперации(Дата1,Дата2);
	ИначеЕсли (ИмяОтбора="Содержание") ИЛИ (ИмяОтбора="СуммаОперации") Тогда
		Опер.ВыбратьПоЗначению(Дата1,Дата2,ИмяОтбора,ЗначениеОтбора);
	ИначеЕсли (ИмяОтбора="Контрагент") Тогда
		Док=СоздатьОбъект("Документ");
		Док.ВыбратьПоЗначению(Дата1,Дата2,ИмяОтбора,ЗначениеОтбора);
	Иначе
		Док=СоздатьОбъект("Документ."+ИмяОтбора);
		Док.ВыбратьДокументы(Дата1,Дата2);
	КонецЕсли;
	Таб=СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Отчет");
	Таб.ВывестиСекцию(?(ВклПроводки=1,"ШапкаП","Шапка"));
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	НПП=0;
	Если ТипЗначенияСтр(Док) <> "Документ" Тогда
		Пока Опер.ПолучитьОперацию()=1 Цикл
			Состояние("Вывод "+Опер.ДатаОперации);
			НПП=НПП+1;
			Таб.ВывестиСекцию("Операция");
			Если ВклПроводки=1 Тогда
				Опер.ВыбратьПроводки();
				Пока Опер.ПолучитьПроводку()=1 Цикл
					Таб.ВывестиСекцию("Проводка");
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Пока Док.ПолучитьДокумент()=1 Цикл
			Если Док.СуществуетОперация()=1 Тогда
				Опер=Док.Операция;
				Состояние("Вывод "+Опер.ДатаОперации);
				НПП=НПП+1;
				Таб.ВывестиСекцию("Операция");
				Если ВклПроводки=1 Тогда
					Опер.ВыбратьПроводки();
					Пока Опер.ПолучитьПроводку()=1 Цикл
						Таб.ВывестиСекцию("Проводка");
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Таб.ВывестиСекцию("КонецОтчета");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Журнал операций","");
КонецПроцедуры
