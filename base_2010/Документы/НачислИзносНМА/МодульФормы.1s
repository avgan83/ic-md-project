Перем СписокДействий;

Процедура ВводНового()
	Отчет=1;
	ДатаДок = КонМесяца(РабочаяДата());
	ДатаДокумента = Дата(0);
	Автор=глПользователь;
КонецПроцедуры

Процедура ПриОткрытии()
	глПроверкаРазрешенияРедактирования(Контекст);
	
	Форма.КнопкаПоУмолчанию("ОК");
	ДатаДокумента = ДатаДок;
КонецПроцедуры

Процедура Проверка()
	Док=СоздатьОбъект("Документ.НачислИзносНМА");
	Док.ВыбратьДокументы(НачМесяца(ДатаДок),КонМесяца(ДатаДок));
	Пока Док.ПолучитьДокумент()<>0 Цикл
		Если (Док.НомерДок<>НомерДок) и (Док.Проведен()=1) Тогда
			Сообщить("Документ не будет проведен, так как уже была начислена амортизация на НМА в этом месяце!");
			Статусвозврата(0);
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	Операция.Содержание="Начисл.износа НМА за "+Формат(ДатаДок,"Д ММММГГГГ");
	ЖурналДокумента=глВыбратьЖурнал(ТекущийДокумент());
КонецПроцедуры

Процедура Печать()
	Пропись(КаталогИБ()+"Spl\"+Леи.Spl);
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("НачислениеИзносаНМА");
	Таб.ВывестиСекцию("Отчет");
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	Таб.ВывестиСекцию("Итог");
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,0,0);
	Таб.Показать("Амортизация НМА № "+СокрЛП(НомерДок)+" от "+СокрЛП(ДатаДок));
КонецПроцедуры


Процедура Заполнить()
	УдалитьСтроки();
	ПервоначальнаяСтоимость = 0;
	ИзносНаНачало = 0;
	БалансоваяСтоимость = 0;
	Кол_воОпер=0;
	Сч04=СчетПоКоду("111");
	Сч05=СчетПоКоду("113");
	Ит=СоздатьОбъект("БухгалтерскиеИтоги");
	Ит1=СоздатьОбъект("БухгалтерскиеИтоги");
	Ит1.Рассчитать(НачГода(ДатаДок),НачГода(ДатаДок),СчетПоКоду("113"));
	Ит.ИспользоватьСубконто(ВидыСубконто.НематАктивы,,1,0);
	Ит.ВыполнитьЗапрос(КонМесяца(ДатаДок),,,,,1);
	Ит.ВыбратьСубконто();
	Пока Ит.ПолучитьСубконто()=1 Цикл
		Если Ит.Субконто().Амортизация=0 Тогда
			Продолжить;
		КонецЕсли;
		НМА=Ит.Субконто();
		НМА.ИспользоватьДату(ДатаДок);
		ДатаВвода=НМА.ДатаВвода;
		МетодНачисления=НМА.МетодНачисленияАмортизации;
		СрокИспользования=НМА.СрокИспользования;
		СчетЗатрат=НМА.СчетЗатрат;
		СтатьяЗатрат=НМА.СтатьяЗатрат;
		Счет_СубсчетАморт=НМА.СчетАмортизации;
		Кол_воУслОпер=НМА.КолвоУслОпераций;
		Коэффициент=НМА.КоэфИзноса;
		Амортизация=НМА.Амортизация;
		Если НачМесяца(НМА.ДатаВвода)<=НачМесяца(ДатаДок) тогда
			Если НМА.СрокИспользования = 0 Тогда
				Сообщить("Не задан срок использования нематериального актива """+НМА+""".","!");
				Сообщить("Расчет амортизации по нему произведен не будет.","I");
				Продолжить;
			КонецЕсли;
			Ит.ВыбратьСчета();
			ПервоначальнаяСтоимость=0;
			ИзносНаНачало=0;
			Пока Ит.ПолучитьСчет() = 1 Цикл
				Если Ит.Счет = Сч04 Тогда
					ПервоначальнаяСтоимость=Ит.СНД();
				ИначеЕсли Ит.Счет = Сч05 Тогда
					ИзносНаНачало=Ит.СНК();
				КонецЕсли;
			КонецЦикла;
			Износ=0;
			Если НМА.Амортизация=1 Тогда
				Дата1 = КонМесяца(ДатаДок);
				Дата2 = ДатаВвода;
				Год1 = ДатаГод(Дата1);
				Год2 = ДатаГод(Дата2);
				Месяц1 = ДатаМесяц(Дата1);
				Месяц2 = ДатаМесяц(Дата2);
				РазнГод = Год1 - Год2;
				РазнМесяц = Месяц1 - Месяц2;
				РазнДата=Дата1-Дата2;
				Если МетодНачисления = Перечисление.МетодыНачислАмортНМА.Прямолинейный Тогда // Прямолинейный
					Если ((Год1=Год2) И (Месяц1=Месяц2)) Тогда
						Если Константа.СпособНачисленияИзноса=Перечисление.СпособНачисленияИзноса.СоСледМесяца Тогда
							Сообщить("На "+НМА+" износ будет рассчитываться со след. месяца!!!");
							Продолжить;
						ИначеЕсли Константа.СпособНачисленияИзноса=Перечисление.СпособНачисленияИзноса.СДатыВвода Тогда
							Срок=Дата1-Дата2;
							Износ=ПервоначальнаяСтоимость/НМА.СрокИспользования/ДатаЧисло(Дата1)*Срок;
						Иначе
							Износ=ПервоначальнаяСтоимость/НМА.СрокИспользования;
						КонецЕсли;	
					Иначе
						Износ=ПервоначальнаяСтоимость/НМА.СрокИспользования;
					КонецЕсли; 
				КонецЕсли;
				
				Если МетодНачисления = Перечисление.МетодыНачислАмортНМА.Производственный Тогда // Производственный (кол-во операции)
					ВвестиЧисло(Кол_воОпер,"Кол-во оп. "+НМА.Наименование,12,0,);
					Износ=ПервоначальнаяСтоимость/Кол_воУслОпер*Кол_воОпер;
				КонецЕсли;
				
				Если МетодНачисления = Перечисление.МетодыНачислАмортНМА.УмОстатка Тогда
					Стоимость=ИТ1.СНКРС("113",1,,НМА,"!");
					Если Стоимость=0 Тогда
						Стоимость=НМА.ИзносНаНачГода;
					КонецЕсли;
					БалансСтоим=ПервоначальнаяСтоимость-Стоимость;
					Износ=БалансСтоим*Коэффициент/12;
				КонецЕсли;
				
				БалансоваяСтоимость=ПервоначальнаяСтоимость-ИзносНаНачало;
				
				Если БалансоваяСтоимость<Износ Тогда
					Износ=БалансоваяСтоимость;
				КонецЕсли;
				
				БалансоваяСтоимость=ПервоначальнаяСтоимость-ИзносНаНачало-Износ;
				
				Если Износ<>0 Тогда
					Если НМА.СчетЗатрат.Выбран()=0 Тогда
						Сообщить("Не указан счет затрат для "+НМА,"!");
					Иначе
						Износ = ОКР(Износ,2,0);
						//Таб.ВывестиСекцию("Строка");
						НоваяСтрока();
						Актив=НМА;
						НачислИзнос=ИзносНаНачало;
						СчетД=НМА.СчетЗатрат;
						СчетК=НМА.СчетАмортизации;
						СуммаИзноса=Износ;
						ОстатСтоим=БалансоваяСтоимость;
						Перв=ПервоначальнаяСтоимость;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Открыть  в журнале");
