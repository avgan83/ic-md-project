
Перем гУсл1,гУсл2,гУсл3,гУсл4,гУсл5,гУсл6,гУсл7,гУсл8,гУсл9,гУсл10;
Перем гУсл11,гУсл12,гУсл13,гУсл14,гУсл15,гУсл16,гУсл17,гУсл18,гУсл19,гУсл20;

Перем гСекцияСтрРеквизит,гСекцияФункции,гУровеньРекурсии,гКоличествоСтрок;
Перем гСпШиринаКолонок,гПрефиксТаб ;
Перем гРеквТЧ, гОбщРекв, гРеквШ;
Перем гПред, гСпВариантов, гДлина, гВидыФункций;
Перем гСпРеквВсе;

Процедура ОбФлагТЧ()
	ФормПечШапки = ФормФлагТЧ;
    Форма.ФормПечШапки.Доступность(ФормФлагТЧ);
	Форма.Обновить(1);
КонецПроцедуры

Функция СписокРеквизитовДляПечати(пДокМД)
    Перем Представление;

	Реквизиты = СоздатьОбъект("ТаблицаЗначений");
	Реквизиты.НоваяКолонка("Имя");
	Реквизиты.НоваяКолонка("Представление");
	Реквизиты.НоваяКолонка("Длина");
	Реквизиты.НоваяКолонка("Тип");
	Номер = 1; гРеквШ = 0;
	Для Инд = 1 По ФормРеквизиты.РазмерСписка() Цикл
		Если Инд = гРеквТЧ Тогда
		    гРеквШ = Номер;
		КонецЕсли;

		Если ФормРеквизиты.Пометка(Инд) = 1 Тогда
		    Реквизиты.НоваяСтрока();
			Реквизиты.Имя = ФормРеквизиты.ПолучитьЗначение(Инд, Представление);
			Реквизиты.Представление = СтрЗаменить(Представление,гПрефиксТаб,"");

			Если Инд > гРеквТЧ Тогда
			    Рек = пДокМД.РеквизитТабличнойЧасти(Реквизиты.Имя);
				Реквизиты.Тип = ?(ФормФлагТЧ = 0, "Итог", "Таблица");
			ИначеЕсли Инд > гОбщРекв Тогда
				Рек = пДокМД.РеквизитШапки(Реквизиты.Имя);
				Реквизиты.Тип = "Шапка";
			Иначе
				Рек = Метаданные.ОбщийРеквизитДокумента(Реквизиты.Имя);
				Реквизиты.Тип = "Шапка";
			КонецЕсли;

			Реквизиты.Длина = Мин(Макс(Рек.ДлинаПредставленияЗначения(4,30,30),
				СтрДлина(СтрЗаменить(Представление,гПрефиксТаб,""))), 50);

			Если (Инд = гРеквТЧ) И (ФормФлагТЧ = 1) Тогда
				Реквизиты.НоваяСтрока();
				Реквизиты.Имя = "НомерСтроки";
				Реквизиты.Представление = "№ стр.";
				Реквизиты.Тип = "Таблица";
				Реквизиты.Длина = 7;
				Номер = Номер + 1;
			КонецЕсли;

			Номер = Номер + 1;
		КонецЕсли;

	КонецЦикла;

	Возврат Реквизиты;
КонецФункции

Функция ПечатьУсловия(СтрУсловия,СтрРасшифровки) Далее

Функция ПечатьШапкиОтчета(пТаб, пДокМД, пРеквизиты,СекСостояние,СекОбязательные)
	Перем Флаг,СтрУсловие,СтрРасшифровка;
	ПечатьУсловия(СтрУсловие,СтрРасшифровка);
    СпКол=СоздатьОбъект("СписокЗначений");
	Флаг = 0;
	пТаб.ВывестиСекцию("Заголовок");
    гСпШиринаКолонок.УдалитьВсе();
	пТаб.ВывестиСекцию("Шапка|Состояние");
	пТаб.ПрисоединитьСекцию("Шапка|Обязательные");
	Сек = пТаб.ПолучитьСекцию("Шапка|Реквизит");
    КолСтолбцов=4;
	КолСтрок=1;

	СпКол.ДобавитьЗначение(КолСтолбцов);
	пРеквизиты.ВыбратьСтроки();
	Пока пРеквизиты.ПолучитьСтроку() = 1 Цикл
		Сек.ИмяРеквизита.Текст = пРеквизиты.Представление;
		Если (ФормФлагТЧ = 1) И (ФормПечШапки = 1) И (пРеквизиты.Тип <> "Шапка") И (Флаг = 0) Тогда
			Флаг = 1;
			пТаб.ВывестиСекцию("Шапка|Состояние");
			КолСтрок=КолСтрок+1;
			СпКол.ДобавитьЗначение(1);
		КонецЕсли;
		пТаб.ПрисоединитьСекцию(Сек);
		СпКол.УстановитьЗначение(КолСтрок,СпКол.ПолучитьЗначение(КолСтрок)+1);
	КонецЦикла;

	гСпШиринаКолонок.УдалитьВсе();
	гСпШиринаКолонок.ДобавитьЗначение(10);
	гСпШиринаКолонок.ДобавитьЗначение(9);
	гСпШиринаКолонок.ДобавитьЗначение(9);
	НомСтр=-3;
	Для Инд =1  По СпКол.РазмерСписка()  Цикл
		КолКолонок=СпКол.ПолучитьЗначение(Инд);
	    Если КолСтолбцов<КолКолонок Тогда
			КолСтолбцов=КолКолонок;
		КонецЕсли;
		Для Инд2 =1  По КолКолонок-1  Цикл
			НомСтр=НомСтр+1;
			Если НомСтр>0 Тогда
				ТекДлина=пРеквизиты.ПолучитьЗначение(НомСтр,"Длина");
				Если гСпШиринаКолонок.РазмерСписка()<Инд2 Тогда
					 гСпШиринаКолонок.ДобавитьЗначение(ТекДлина);
				Иначе
			        Если пРеквизиты.ПолучитьЗначение(НомСтр,"Длина")>гСпШиринаКолонок.ПолучитьЗначение(Инд2) Тогда
						 гСпШиринаКолонок.УстановитьЗначение(Инд2,ТекДлина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

    Возврат КолСтолбцов;
КонецФункции

Процедура ПечатьРеквизитовШапки(пТаб, пДок, пРеквизиты,СекСостояние,СекОбязательные)
	Если пДок.Проведен() = 1 Тогда
		СекСостояние.Флаг.Текст = "V";
	ИначеЕсли пДок.ПометкаУдаления() = 1 Тогда
		СекСостояние.Флаг.Текст = "X";
	Иначе
		СекСостояние.Флаг.Текст = " ";
	КонецЕсли;
	пТаб.ВывестиСекцию(СекСостояние);
	СекОбязательные.Номер.Текст = пДок.НомерДок;
	СекОбязательные.Дата.Текст = пДок.ДатаДок;
	СекОбязательные.Время.Текст = пДок.ПолучитьВремя();
	СекОбязательные.Номер.Расшифровка(пДок.ТекущийДокумент(), 1);
	пТаб.ПрисоединитьСекцию(СекОбязательные);
КонецПроцедуры

Функция ПечатьРеквизитов(пТаб, пДок, пРеквизиты, пВид,Сек,СекСостояние,СекОбязательные)
	КолСтолбцов=0;
    Если пВид = "Шапка" Тогда
        ПечатьРеквизитовШапки(пТаб, пДок, пРеквизиты,СекСостояние,СекОбязательные);
		КолСтолбцов=2;
	КонецЕсли;
	пРеквизиты.ВыбратьСтроки();
	Пока пРеквизиты.ПолучитьСтроку() = 1 Цикл
		Если пРеквизиты.Тип <> пВид Тогда
		    Продолжить;
		КонецЕсли;

		Если пВид = "Итог" Тогда
		    Значение = пДок.Итог(пРеквизиты.Имя);
		Иначе
			Значение = пДок.ПолучитьАтрибут(пРеквизиты.Имя);
		КонецЕсли;

		Сек.Имя.Текст = СокрЛП(Значение);
		Если ТипЗначенияСтр(Значение) = "Число" Тогда
			Сек.Имя.ГоризонтальноеПоложение(2);
		Иначе
			Сек.Имя.ГоризонтальноеПоложение(1);
		КонецЕсли;
		пТаб.ПрисоединитьСекцию(Сек);
		КолСтолбцов=КолСтолбцов+1;
	КонецЦикла;
    Возврат КолСтолбцов;
КонецФункции

Процедура ВывестиДокумент(КолСтрок,Док,Таб,Реквизиты,СекСтрРеквизит,СекСостояние,СекОбязательные,КолСтолбцов)
		Если (ФормПечШапки = 1) ИЛИ (ФормФлагТЧ = 0) Тогда
			ПечатьРеквизитов(Таб, Док, Реквизиты, "Шапка",СекСтрРеквизит,СекСостояние,СекОбязательные);
			Если ФормФлагТЧ = 1 Тогда
				Таб.Область(Таб.ВысотаТаблицы(), 1, Таб.ВысотаТаблицы(), КолСтолбцов)
					.Полужирный(1);
            КонецЕсли;
		КонецЕсли;
        КолСтрок=КолСтрок+1;
		КолСтрПечать=Цел(КолСтрок/50);
		Если КолСтрПечать>0 Тогда
			Состояние(" Обработка строк "+КолСтрПечать*50);
		КонецЕсли;

		Если ФормФлагТЧ = 1 Тогда
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку() = 1 Цикл
				Если ФормПечШапки = 0 Тогда
					ПечатьРеквизитов(Таб, Док, Реквизиты, "Шапка",СекСтрРеквизит,СекСостояние,СекОбязательные);
				Иначе
					Таб.ВывестиСекцию("Строка|Состояние");
					КолСтрок=КолСтрок+1;
				КонецЕсли;
				ПечатьРеквизитов(Таб, Док, Реквизиты, "Таблица",СекСтрРеквизит,СекСостояние,СекОбязательные);
			КонецЦикла;
			Таб.Область(Таб.ВысотаТаблицы(), 1, Таб.ВысотаТаблицы(), КолСтолбцов)
				.РамкаСнизу(4);
		Иначе
			ПечатьРеквизитов(Таб, Док, Реквизиты, "Итог",СекСтрРеквизит,СекСостояние,СекОбязательные);
		КонецЕсли;
КонецПроцедуры

Функция ПечатьУсловия(СтрУсловия,СтрРасшифровки)
	СтрУсловия="";
	СтрРасшифровки="";
	ТабУсловия.ВыбратьСтроки();
	Рез=0;
	Пока ТабУсловия.ПолучитьСтроку()=1  Цикл
		Если СокрЛП(ТабУсловия.ФлВкл)<>"+" Тогда
			Продолжить;
		КонецЕсли;
		Рез=Рез+1;
		Если ТабУсловия.ТекущаяСтрока()<=3 Тогда
	  		СтрУсловия=СтрУсловия+ТабУсловия.КолПредставление+" "+ТабУсловия.КолУсловие+" "+ТабУсловия.КолЗначение+РазделительСтрок;
	  	КонецЕсли;
	  	СтрРасшифровки=СтрРасшифровки+ТабУсловия.КолПредставление+" "+ТабУсловия.КолУсловие+" "+ТабУсловия.КолЗначение+РазделительСтрок;
	КонецЦикла;
	Возврат Рез;
КонецФункции

Функция ПостроитьЗапрос(Режим) Далее

Процедура КнПечать()
	Таб=СоздатьОбъект("Таблица");

    СекСтрРеквизит = Таб.ПолучитьСекцию("Строка|Реквизит");
	СекСостояние = Таб.ПолучитьСекцию("Строка|Состояние");
	СекОбязательные = Таб.ПолучитьСекцию("Строка|Обязательные");
	ДокМД = Метаданные.Документ(ФормДокумент.ТекущаяСтрока());
	Док = СоздатьОбъект("Документ." + ДокМД.Идентификатор);
	Реквизиты = СписокРеквизитовДляПечати(ДокМД);
	КолСтолбцов=ПечатьШапкиОтчета(Таб, ДокМД, Реквизиты,СекСостояние,СекОбязательные);
    КолСтрок=0;
	ФлПровПометки=ВклПомеч-1;
	ФлПровПроведенные=ВклПроведенныеВсе-1;
	Если Форма.ВклПроведенныеВсе.Доступность()=0 Тогда
		ФлПровПроведенные=0;
	КонецЕсли;
	Если ТабУсловия.КоличествоСтрок()=0 Тогда
		Если ФлПровПроведенные=1 Тогда
			Док.УстановитьФильтр(1,0);
		ИначеЕсли ФлПровПроведенные=2 Тогда
			Док.УстановитьФильтр(0,1);
		КонецЕсли;
		Док.ВыбратьДокументы(ФормДатаНач, ФормДатаКон);
		Пока Док.ПолучитьДокумент() = 1 Цикл
			ТекДок=Док.ТекущийДокумент();
			Если ((ФлПровПометки=1)И (ТекДок.ПометкаУдаления()=1))ИЛИ((ФлПровПометки=2)И(ТекДок.ПометкаУдаления()=0)) Тогда
				Продолжить;
			КонецЕсли;
			ВывестиДокумент(КолСтрок,ТекДок,Таб,Реквизиты,СекСтрРеквизит,СекСостояние,СекОбязательные,КолСтолбцов);
 		КонецЦикла;
 	Иначе
		Если (ФормДатаНач>ФормДатаКон)И(ФормДатаКон<>Дата(0)) Тогда
			Предупреждение("Внимание!Дата начала выбора превышает дату конца.Запрос не выполнен!");
			Возврат;
		КонецЕсли;
		ТекстЗапроса = ПостроитьЗапрос("Реестр");
		Запрос = СоздатьОбъект("Запрос");
		Попытка
	    	Запрос.выполнить(ТекстЗапроса);
		Исключение
	    	Предупреждение(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		Пока Запрос.Группировка(1) = 1 Цикл
			ВывестиДокумент(КолСтрок,Запрос.Док,Таб,Реквизиты,СекСтрРеквизит,СекСостояние,СекОбязательные,КолСтолбцов);
		КонецЦикла;
	КонецЕсли;
	НачСтрока=3;
	Для  Инд = 1 По гСпШиринаКолонок.РазмерСписка()  Цикл
		 Таб.Область(,Инд+1,,Инд+1)
				.ШиринаСтолбца(гСпШиринаКолонок.ПолучитьЗначение(Инд));
		 НаимКолонки=Таб.Область(НачСтрока,Инд+1,НачСтрока,Инд+1).Текст;
		 Если СтрДлина(НаимКолонки)>гСпШиринаКолонок.ПолучитьЗначение(Инд)  Тогда
		 	 Таб.Область(НачСтрока,Инд+1,НачСтрока,Инд+1).Расшифровка(НаимКолонки);
		 КонецЕсли;
	КонецЦикла;
	Если (ФормФлагТЧ = 1) И (ФормПечШапки = 1) Тогда
		Таб.Область(3,1,3,КолСтолбцов).РамкаСнизу(4);
		Таб.Опции(0,0,4,0);
	Иначе
		Таб.Опции(0,0,3,4);
	КонецЕсли;
	Таб.Область(3, 1, Таб.ВысотаТаблицы(), КолСтолбцов).РамкаОбвести(4,4,4,4);
    Таб.Область(4, 1, Таб.ВысотаТаблицы(), КолСтолбцов).ВертикальноеПоложение(2);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Документы " + ДокМД.Представление());
КонецПроцедуры

Процедура ДобавитьТабЧасть()
	ДокМД = Метаданные.Документ(ФормДокумент.ТекущаяСтрока());
	Для Инд = 1 По ДокМД.РеквизитТабличнойЧасти() Цикл
		Поз=ФормРеквизиты.НайтиЗначение(ДокМД.РеквизитТабличнойЧасти(Инд).Идентификатор);
		Если (Поз=0)И(ФормФлагТЧ = 1) Тогда
			ФормРеквизиты.ДобавитьЗначение(ДокМД.РеквизитТабличнойЧасти(Инд).Идентификатор,
			ДокМД.РеквизитТабличнойЧасти(Инд).Представление()+гПрефиксТаб);
			ФормРеквизиты.Пометка(ФормРеквизиты.РазмерСписка(), 1);
		ИначеЕсли (Поз>0)И(ФормФлагТЧ = 0) Тогда
			Если ДокМД.РеквизитТабличнойЧасти(Инд).ИтогПоКолонке =0 Тогда
				ФормРеквизиты.УдалитьЗначение(Поз);
			Иначе
				ФормРеквизиты.Пометка(Поз, 0);
			КонецЕсли;
		ИначеЕсли (Поз>0)И(ФормФлагТЧ = 1) Тогда
			ФормРеквизиты.Пометка(Поз, 1);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОбДокумент()
	Перем СтрПредставление;
	ДокМД = Метаданные.Документ(ФормДокумент.ТекущаяСтрока());
	ФормРеквизиты.УдалитьВсе();
	ФормВозмФункции.УдалитьВсе();
	ФормВозмГруппы.УдалитьВсе();
	ФормФункции.УдалитьВсе();
	ФормГруппы.УдалитьВсе();
	гДлина.УдалитьВсе();
	гСпРеквВсе.УдалитьВсе();
    ТабУсловия.УдалитьСтроки();
	гОбщРекв = Метаданные.ОбщийРеквизитДокумента();

	Для Инд = 1 По Метаданные.ОбщийРеквизитДокумента() Цикл
		ОбщРекв=Метаданные.ОбщийРеквизитДокумента(Инд);
		ФормРеквизиты.ДобавитьЗначение(ОбщРекв.Идентификатор,
			ОбщРекв.Представление());
		ФормРеквизиты.Пометка(Инд, 1);
		Если (ОбщРекв.Тип<>"Строка")ИЛИ(ОбщРекв.Длина<>0) Тогда
			ФормВозмГруппы.ДобавитьЗначение(ОбщРекв.Идентификатор,
				ОбщРекв.Представление());
			гДлина.ДобавитьЗначение(ОбщРекв.ДлинаПредставленияЗначения(СтрДлина(ОбщРекв.Представление()), 40, 20),
				ОбщРекв.Идентификатор);
		КонецЕсли;
		Если ОбщРекв.Тип="Число" Тогда
			ФормФункции.ДобавитьЗначение(ОбщРекв.Идентификатор,
			ОбщРекв.Представление());
		КонецЕсли;
	КонецЦикла;
	Для Инд = 1 По ДокМД.РеквизитШапки() Цикл
		РеквШапки=ДокМД.РеквизитШапки(Инд);
		ФормРеквизиты.ДобавитьЗначение(РеквШапки.Идентификатор,
			РеквШапки.Представление());
		ФормРеквизиты.Пометка(Инд + гОбщРекв, 1);
		Если ДокМД.РеквизитШапки(Инд).Тип="Число" Тогда
			ФормФункции.ДобавитьЗначение(ДокМД.РеквизитШапки(Инд).Идентификатор,
			ДокМД.РеквизитШапки(Инд).Представление());
		КонецЕсли;
		Если (РеквШапки.Тип<>"Строка")ИЛИ(РеквШапки.Длина<>0) Тогда
			ФормВозмГруппы.ДобавитьЗначение(РеквШапки.Идентификатор,
				РеквШапки.Представление());
			гДлина.ДобавитьЗначение(РеквШапки.ДлинаПредставленияЗначения(СтрДлина(РеквШапки.Представление()), 40, 20),
				РеквШапки.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	гРеквТЧ = гОбщРекв + ДокМД.РеквизитШапки();
	Инд2 = 1;
	Для Инд = 1 По ДокМД.РеквизитТабличнойЧасти() Цикл
		РеквТаб=ДокМД.РеквизитТабличнойЧасти(Инд);
		Если (РеквТаб.Тип<>"Строка")ИЛИ(РеквТаб.Длина<>0) Тогда
			ФормВозмГруппы.ДобавитьЗначение(РеквТаб.Идентификатор,
				РеквТаб.Представление());
			гДлина.ДобавитьЗначение( РеквТаб.ДлинаПредставленияЗначения(СтрДлина(РеквТаб.Представление()), 40, 20),
				РеквТаб.Идентификатор);
		КонецЕсли;
		Если ДокМД.РеквизитТабличнойЧасти(Инд).Тип="Число" Тогда
			ФормФункции.ДобавитьЗначение(ДокМД.РеквизитТабличнойЧасти(Инд).Идентификатор,
			ДокМД.РеквизитТабличнойЧасти(Инд).Представление());
		КонецЕсли;
		Если (ДокМД.РеквизитТабличнойЧасти(Инд).ИтогПоКолонке = 1)
		  И (ФормФлагТЧ = 0) Тогда
			ФормРеквизиты.ДобавитьЗначение(РеквТаб.Идентификатор,
				РеквТаб.Представление()+гПрефиксТаб);
			ФормРеквизиты.Пометка(Инд2 + гРеквТЧ, 1);
			Инд2 = Инд2+1;
		ИначеЕсли ФормФлагТЧ = 1 Тогда
			ФормРеквизиты.ДобавитьЗначение(РеквТаб.Идентификатор,
				РеквТаб.Представление()+гПрефиксТаб);
			ФормРеквизиты.Пометка(Инд2 + гРеквТЧ, 1);
			Инд2 = Инд2+1;
		КонецЕсли;
	КонецЦикла;
	ФормВозмГруппы.Выгрузить(гСпРеквВсе);
	Для Инд = 1 По гПред.РазмерСписка() Цикл
		ЗначИД=гПред.ПолучитьЗначение(Инд,СтрПредставление);
		ФормВозмГруппы.ДобавитьЗначение(гПред.ПолучитьЗначение(Инд),СтрПредставление);
	КонецЦикла;
КонецПроцедуры

Процедура Контроль()
	ФормФлагТЧ = 0;
	ФормПечШапки = 0;
КонецПроцедуры

Процедура ВыбратьУсловие(НомТекСтр)
	Перем СтрПуть,СтрИмя,СтрИмяДиалога;

	Если НомТекСтр>ТабУсловия.КоличествоСтрок() Тогда
		НомТекСтр=0;
	КонецЕсли;

	Если (НомТекСтр>0)И(ТабУсловия.ТекущаяКолонка()="ФлВкл") Тогда
		Если  СокрЛП(ТабУсловия.ФлВкл)="" Тогда
			ТабУсловия.ФлВкл="+";
		Иначе
			ТабУсловия.ФлВкл="";
		КонецЕсли;
		Возврат;
	КонецЕсли;

	СписокПарам=СоздатьОбъект("СписокЗначений");
	СтрИмяДиалога="DlgQuery.ert";
	РасположениеФайла(СтрПуть,СтрИмя);
	СтрПуть=КаталогИб();

	СписокПарам.ДобавитьЗначение("Документ","Режим");
	СписокПарам.ДобавитьЗначение(Метаданные.Документ(ФормДокумент.ТекущаяСтрока()),"Объект");
	Если НомТекСтр>0 Тогда
		Если СокрЛП(ТабУсловия.ПолучитьЗначение(НомТекСтр,"ФлВкл"))="+" Тогда
			Вкл=1;
		Иначе
			Вкл=0;
		КонецЕсли;
		СписокПарам.ДобавитьЗначение(Вкл,"ВклУсловие");
		СписокПарам.ДобавитьЗначение(ТабУсловия.ПолучитьЗначение(НомТекСтр,"КолИдентификатор"),"Атрибут");
		СписокПарам.ДобавитьЗначение(ТабУсловия.ПолучитьЗначение(НомТекСтр,"КолПредставление"),"Представление");
		СписокПарам.ДобавитьЗначение(ТабУсловия.ПолучитьЗначение(НомТекСтр,"КолУсловие"),"Условие");
		СписокПарам.ДобавитьЗначение(ТабУсловия.ПолучитьЗначение(НомТекСтр,"КолЗначение"),"Значение");
	КонецЕсли;
	ПеремКонт=СписокПарам;
    ОткрытьФормуМодально("Обработка.ВыборУсловия",ПеремКонт) ;
	Если ПустоеЗначение(ПеремКонт)=1 Тогда
		Возврат;
	КонецЕсли;
	Если ПеремКонт.РазмерСписка()=0 Тогда
		Возврат;
	КонецЕсли;
	ВыбАтриб=ПеремКонт.Получить("Атрибут");
	Если НомТекСтр=0 Тогда
		ТабУсловия.ВыбратьСтроки();
		Пока ТабУсловия.ПолучитьСтроку()=1  Цикл
	        Если ВыбАтриб=ТабУсловия.КолИдентификатор Тогда
				Предупреждение("Внимание,условие для "+Строка(ВыбАтриб)+" уже определено!");
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если НомТекСтр<=0 Тогда
		ТабУсловия.НоваяСтрока();
		НомТекСтр=ТабУсловия.ТекущаяСтрока();
	КонецЕсли;
	СтрФл="";
	Если ПеремКонт.Получить("ВклУсловие")=1 Тогда
		СтрФл="+";
	КонецЕсли;
	ТабУсловия.УстановитьЗначение(НомТекСтр,"ФлВкл",СтрФл);
	ТабУсловия.УстановитьЗначение(НомТекСтр,"КолИдентификатор",ВыбАтриб);
	ТабУсловия.УстановитьЗначение(НомТекСтр,"КолПредставление",ПеремКонт.Получить("Представление"));
	ТабУсловия.УстановитьЗначение(НомТекСтр,"КолУсловие",ПеремКонт.Получить("Условие"));
	ТабУсловия.УстановитьЗначение(НомТекСтр,"КолЗначение",ПеремКонт.Получить("Значение"));
КонецПроцедуры

Процедура УправлениеЗакладками()
	Форма.Закладки.ДобавитьЗначение("Реестр","Реестр");
	Форма.Закладки.ДобавитьЗначение("Итоги","Итоги");
	Форма.Закладки.ДобавитьЗначение("Условия","Условия");
	Форма.Закладки.ДобавитьЗначение("Настройки","Настройки");
	Форма.ИспользоватьСлой("Основной,Реквизиты",2);
КонецПроцедуры

Процедура УдалУсловие()
	Если ТабУсловия.ТекущаяСтрока()>0 Тогда
		ТабУсловия.УдалитьСтроку(ТабУсловия.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ИзмУсловие()
	Если ТабУсловия.ТекущаяСтрока()>0 Тогда
		ВыбратьУсловие(ТабУсловия.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

Процедура ВклАтрибуты(Фл)
	Для Инд =1  По ФормРеквизиты.РазмерСписка()  Цикл
	   ФормРеквизиты.Пометка(Инд,Фл)
	КонецЦикла;
КонецПроцедуры

Процедура УстВариант (Вариант, Список, ВозмСписок,Реж)
	Поз = Найти(Вариант, ",");
	Что = СокрЛП(Лев(Вариант, Поз - 1));
	Куда = СокрЛП(Прав(Вариант, СтрДлина(Вариант) - Поз));
	Реж="";
    Если СтрЧислоВхождений(Куда,",")>0 Тогда
		Поз = Найти(Куда, ",");
		Реж = СокрЛП(Прав(Куда, СтрДлина(Куда) - Поз));
		Куда= СокрЛП(Лев(Куда, Поз - 1));
	КонецЕсли;
	Если Что = "Группы" Тогда
		Список = ФормГруппы;
		ВозмСписок = ФормВозмГруппы;
	Иначе
		Список = ФормФункции;
		ВозмСписок = ФормВозмФункции;
	КонецЕсли;

	Если Куда = "Вправо" Тогда
		ВремСписок = Список;
		Список = ВозмСписок;
		ВозмСписок = ВремСписок;
	КонецЕсли;
КонецПроцедуры

Процедура КнДвигать (Вариант)
	Перем Список, ВозмСписок, Пред,Реж;

	УстВариант(Вариант, Список, ВозмСписок,Реж);

	Если ВозмСписок.ТекущаяСтрока() = 0 Тогда
		Возврат;
	КонецЕсли;
	Зн = ВозмСписок.ПолучитьЗначение(ВозмСписок.ТекущаяСтрока(), Пред);
	Список.ДобавитьЗначение(Зн, Пред);
	ВозмСписок.УдалитьЗначение(ВозмСписок.ТекущаяСтрока());
КонецПроцедуры

Процедура КнДвигатьВсе (Вариант)
	Перем Список, ВозмСписок, Пред,Реж;

	УстВариант(Вариант, Список, ВозмСписок,Реж);

	Для Инд = 1 По ВозмСписок.РазмерСписка() Цикл
		зн = ВозмСписок.ПолучитьЗначение(Инд, Пред);
		Если Реж="измерения" Тогда
			Если гПред.Принадлежит(зн) = 1 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Список.ВставитьЗначение(1,Зн, Пред);
	КонецЦикла;
    Если Реж="измерения" Тогда
		КолСтрок=ВозмСписок.РазмерСписка();
		Инд=1;
		Пока Инд<=КолСтрок Цикл
			зн = ВозмСписок.ПолучитьЗначение(Инд);
			Если гПред.Принадлежит(зн) = 0 Тогда
			    ВозмСписок.УдалитьЗначение(Инд);
				КолСтрок=КолСтрок-1;
			Иначе
				Инд=Инд+1;
			КонецЕсли;
		КонецЦикла;
		Возврат;
	КонецЕсли;
	ВозмСписок.УдалитьВсе();
КонецПроцедуры

Процедура КнПорядок(Вариант)
	Перем Список, ВозмСписок,Реж;

	УстВариант(Вариант, Список, ВозмСписок,Реж);

	Поз = Список.ТекущаяСтрока();
	ПозЗапятой = Найти(Вариант, ",");
	Куда = СокрЛП(Прав(Вариант, СтрДлина(Вариант) - ПозЗапятой));

	Если (Куда = "Вверх") И (Поз > 1) Тогда
		Список.СдвинутьЗначение(-1, Поз);
	КонецЕсли;

	Если (Куда = "Вниз") И (Поз < Список.РазмерСписка()) И (Поз > 0) Тогда
		Список.СдвинутьЗначение(1, Поз);
	КонецЕсли;
КонецПроцедуры

Процедура СохранитьПараметрыФормы()
	Перем Путь,ИмяФайла;
	Перем СтрПредст;
	РасположениеФайла(Путь,ИмяФайла);

	ПараметрыУстановки=СоздатьОбъект("СписокЗначений");
	СписВар=СоздатьОбъект("СписокЗначений");
	СохрТекст=СоздатьОбъект("Текст");
	ПараметрыУстановки.УдалитьВсе();
	гСпВариантов.Выгрузить(СписВар);
	ПараметрыУстановки.ДобавитьЗначение(СписВар,"СпВариантов");

	СохрТекст.ДобавитьСтроку(ЗначениеВСтрокуВнутр(ПараметрыУстановки));
	ИмяФайла=СокрЛП(Лев(ИмяФайла,СтрДлина(ИмяФайла)-4))+".prm";
	СохрТекст.Записать(СокрЛП(КаталогПользователя())+ИмяФайла);
КонецПроцедуры

Функция ВосстановитьПараметрыФормы()
	Перем Путь,ИмяФайла;
	ФАЙЛ= СоздатьОбъект("ФС");
    РасположениеФайла(Путь,ИмяФайла);
	ИмяФайла=СокрЛП(Лев(ИмяФайла,СтрДлина(ИмяФайла)-4))+".prm";
	Если ФАЙЛ.СуществуетФайл(КаталогПользователя()+ИмяФайла)=0 Тогда
	    Возврат 0;
	КонецЕсли;
	ПараметрыУстановки=СоздатьОбъект("СписокЗначений");
	СохрТекст=СоздатьОбъект("Текст");
	СохрТекст.Открыть(КаталогПользователя()+ИмяФайла);
	Стр="";
	Для Инд =1  По СохрТекст.КоличествоСтрок()  Цикл
	   Стр=Стр+СохрТекст.ПолучитьСтроку(Инд);
	КонецЦикла;
	ПараметрыУстановки=ЗначениеИзСтрокиВнутр(Стр);
	Форма.Обновить(1);
	Возврат гСпВариантов.РазмерСписка();
КонецФункции
Процедура ПриИзменВключения() Далее
Процедура СоздатьВариант(ПараметрыУстановки)
	ПараметрыУстановки.УдалитьВсе();
	СписФормГруппы=СоздатьОбъект("СписокЗначений");
	СписФормВозмГруппы=СоздатьОбъект("СписокЗначений");
	СписФормФункции=СоздатьОбъект("СписокЗначений");
	СписФормВозмФункции=СоздатьОбъект("СписокЗначений");
	СписФормРеквизиты=СоздатьОбъект("СписокЗначений");
	СписФормВидыФункций=СоздатьОбъект("СписокЗначений");
	СписУсловий=СоздатьОбъект("ТаблицаЗначений");

	ПараметрыУстановки.ДобавитьЗначение(ФормДокумент.ПолучитьЗначение(ФормДокумент.ТекущаяСтрока()),"Документ");
	ПараметрыУстановки.ДобавитьЗначение(ФормПечШапки,"ФормПечШапки");
	ПараметрыУстановки.ДобавитьЗначение(ФормФлагТЧ,"ФормФлагТЧ");

	ФормГруппы.Выгрузить(СписФормГруппы);
	ПараметрыУстановки.ДобавитьЗначение(СписФормГруппы,"ФормГруппы");
	ФормВозмГруппы.Выгрузить(СписФормВозмГруппы);
	ПараметрыУстановки.ДобавитьЗначение(СписФормВозмГруппы,"ФормВозмГруппы");

	ФормФункции.Выгрузить(СписФормФункции);
	ПараметрыУстановки.ДобавитьЗначение(СписФормФункции,"ФормФункции");
	ФормВозмФункции.Выгрузить(СписФормВозмФункции);
	ПараметрыУстановки.ДобавитьЗначение(СписФормВозмФункции,"ФормВозмФункции");
    ФормВидыФункций.Выгрузить(СписФормВидыФункций);
	ПараметрыУстановки.ДобавитьЗначение(СписФормВидыФункций,"ФормВидыФункций");

	ТабУсловия.Выгрузить(СписУсловий);
	ПараметрыУстановки.ДобавитьЗначение(СписУсловий,"ТабУсловия");
	ФормРеквизиты.Выгрузить(СписФормРеквизиты);
	ПараметрыУстановки.ДобавитьЗначение(СписФормРеквизиты,"ФормРеквизиты");

	ПараметрыУстановки.ДобавитьЗначение(ВклПомеч,"ВклПомеч");
	ПараметрыУстановки.ДобавитьЗначение(ВклПроведенныеВсе,"ВклПроведенныеВсе");
	ПараметрыУстановки.ДобавитьЗначение(ФлОднаКолГруппы,"ФлОднаКолГруппы");
КонецПроцедуры

Процедура РежимТА() Далее

Процедура ВосстановитьВариант(НаимВарианта)
	Перем Стр;
	СпПарам=гСпВариантов.Получить(НаимВарианта);
	Если ПустоеЗначение(СпПарам)=1 Тогда
	   Возврат;
	КонецЕсли;
	ТекДок=СпПарам.Получить("Документ");
	Ном=ФормДокумент.НайтиЗначение(ТекДок);
	Если Ном>0 Тогда
	    ФормДокумент.ТекущаяСтрока(Ном);
		Форма.Обновить(0);
		ОбДокумент();
	Иначе
		Возврат ;
	КонецЕсли;
	ФормПечШапки=СпПарам.Получить("ФормПечШапки");
	ФормФлагТЧ=СпПарам.Получить("ФормФлагТЧ");

	СпПарам.Получить("ФормГруппы").Выгрузить(ФормГруппы);
	СпПарам.Получить("ФормВозмГруппы").Выгрузить(ФормВозмГруппы);
	СпПарам.Получить("ФормФункции").Выгрузить(ФормФункции);
	СпПарам.Получить("ФормВозмФункции").Выгрузить(ФормВозмФункции);
	СпПарам.Получить("ФормВидыФункций").Выгрузить(ФормВидыФункций);

	СпПарам.Получить("ТабУсловия").Выгрузить(ТабУсловия);
	ТабУсловия.ВидимостьКолонки("КолИдентификатор",0);
	СпПарам.Получить("ФормРеквизиты").Выгрузить(ФормРеквизиты);
	обФлагТЧ();
    ФлОднаКолГруппы= СпПарам.Получить("ФлОднаКолГруппы");

	ВклПомеч=СпПарам.Получить("ВклПомеч");
	ВклПроведенныеВсе=СпПарам.Получить("ВклПроведенныеВсе");
	ПриИзменВключения();
КонецПроцедуры

Процедура ЗаписьВарианта()
	Если СокрЛП(ВыбВариант)="" Тогда
	    Предупреждение("Внимание!Не введено наименование настройки!");
		Возврат;
	КонецЕсли;
	СпПарам=гСпВариантов.Получить(СокрЛП(ВыбВариант));
	Если ПустоеЗначение(СпПарам)=1 Тогда
	   СпПарам=СоздатьОбъект("СписокЗначений");
	   СоздатьВариант(СпПарам);
	   гСпВариантов.ДобавитьЗначение(СпПарам,СокрЛП(ВыбВариант));
	Иначе
	   СоздатьВариант(СпПарам);
	   гСпВариантов.Установить(СокрЛП(ВыбВариант),СпПарам);
	КонецЕсли;
    гСпВариантов.ТекущаяСтрока(гСпВариантов.НайтиЗначение(СпПарам));
	СохранитьПараметрыФормы();
КонецПроцедуры

Процедура УдалитьВариант()
	ТекСпис=гСпВариантов.Получить(СокрЛП(ВыбВариант));
	Ном=гСпВариантов.НайтиЗначение(ТекСпис);
	ВыбВариант="";
	Если Ном>0 Тогда
		гСпВариантов.УдалитьЗначение(Ном);
		СохранитьПараметрыФормы();
	КонецЕсли;
КонецПроцедуры

Процедура ПриНачалеВыбораЗначения(ЭлемДиалога,ФлагСтандОбр)
	Перем ВыбПоз,СтрПредст;
	Если ЭлемДиалога="ВыбВариант" Тогда
		ФлагСтандОбр=0;
		ВыбЗн="";
		ТекСпис=гСпВариантов.Получить(СокрЛП(ВыбВариант));
		Ном=гСпВариантов.НайтиЗначение(ТекСпис);
		Если Ном>0 Тогда
			гСпВариантов.ТекущаяСтрока(Ном);
		КонецЕсли;
		Если гСпВариантов.РазмерСписка()=0 Тогда
			Возврат;
		КонецЕсли;
		ВыбПоз=0;
		гСпВариантов.ВыбратьЗначение(ВыбЗн,,ВыбПоз,,2);
		Если (ВыбПоз>0) И(ВыбПоз<=гСпВариантов.РазмерСписка()) Тогда
			гСпВариантов.ПолучитьЗначение(ВыбПоз,СтрПредст);
			ВыбВариант=СтрПредст;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПеремУсловия(ТекДок,СписИзмерений)
	Текст="";
	ТабУсловия.ВыбратьСтроки();
	Пока ТабУсловия.ПолучитьСтроку()>0 Цикл
		ТекЗнач = ТабУсловия.КолИдентификатор;
		Если (ПустоеЗначение(ТекЗнач)=1)ИЛИ(ТабУсловия.ФлВкл="") Тогда
			Продолжить;
		КонецЕсли;
		Если СписИзмерений.Принадлежит(ТекЗнач) = 0 Тогда
			Текст = Текст + "
			|" + ТекЗнач + " = Документ." + ТекДок + "." + ТекЗнач + ";";
		КонецЕсли;
	КонецЦикла;
	Возврат Текст;
КонецФункции

Функция ПрисвоитьУсловие(Ном,Значен)
	Если Ном=1 Тогда
		гУсл1=Значен;
	ИначеЕсли Ном=2 Тогда
		гУсл2=Значен;
	ИначеЕсли Ном=3 Тогда
		гУсл3=Значен;
	ИначеЕсли Ном=4 Тогда
		гУсл4=Значен;
	ИначеЕсли Ном=5 Тогда
		гУсл5=Значен;
	ИначеЕсли Ном=6 Тогда
		гУсл6=Значен;
	ИначеЕсли Ном=7 Тогда
		гУсл7=Значен;
	ИначеЕсли Ном=8 Тогда
		гУсл8=Значен;
	ИначеЕсли Ном=9 Тогда
		гУсл9=Значен;
	ИначеЕсли Ном=10 Тогда
		гУсл10=Значен;
	ИначеЕсли Ном=11 Тогда
		гУсл11=Значен;
	ИначеЕсли Ном=12 Тогда
		гУсл12=Значен;
	ИначеЕсли Ном=13 Тогда
		гУсл13=Значен;
	ИначеЕсли Ном=14 Тогда
		гУсл14=Значен;
	ИначеЕсли Ном=15 Тогда
		гУсл15=Значен;
	ИначеЕсли Ном=16 Тогда
		гУсл16=Значен;
	ИначеЕсли Ном=16 Тогда
		гУсл17=Значен;
	ИначеЕсли Ном=18 Тогда
		гУсл18=Значен;
	ИначеЕсли Ном=19 Тогда
		гУсл19=Значен;
	ИначеЕсли Ном=20 Тогда
		гУсл20=Значен;
	Иначе
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

Функция ПолучитьУсловие()
	СтрРезультат="";
	НомСтр=0;
	СтрРазд="";
	ТабУсловия.ВыбратьСтроки();
	Пока ТабУсловия.ПолучитьСтроку()>0 Цикл
		Если (ПустоеЗначение(ТабУсловия.КолИдентификатор)=1)ИЛИ(СокрЛП(ТабУсловия.ФлВкл)="") Тогда
			Продолжить;
		КонецЕсли;
		НомСтр=НомСтр+1;
		Если ПрисвоитьУсловие(НомСтр,ТабУсловия.КолЗначение)=0 Тогда
		     Предупреждение("Внимание!Допустимо использовать не более "+НомСтр+" условий!");
			 Возврат СтрРезультат;
		КонецЕсли;
		СтрРезультат= СтрРезультат+СтрРазд+"Условие ("+ТабУсловия.КолИдентификатор+" "+ТабУсловия.КолУсловие+"гУсл"+Строка(НомСтр)+");";
		СтрРазд="
			|";
	КонецЦикла;
	Возврат СтрРезультат;
КонецФункции

Функция ПостроитьЗапрос(Режим)
	Спис=СоздатьОбъект("СписокЗначений");
	Текст = "Период с ФормДатаНач по ФормДатаКон;";
	Если ВклПомеч=2  Тогда
		СтрОбраб="Обрабатывать НеПомеченныеНаУдаление;";
	ИначеЕсли ВклПомеч=3  Тогда
		СтрОбраб="Обрабатывать ПомеченныеНаУдаление;";
	Иначе
		СтрОбраб="Обрабатывать Все;";
	КонецЕсли;
	Если (ВклПроведенныеВсе=2)И(Форма.ВклПроведенныеВсе.Доступность()=1)  Тогда
		СтрОбраб = СтрОбраб +"
				|ОбрабатыватьДокументы Проведенные;";
	ИначеЕсли (ВклПроведенныеВсе=3)И(Форма.ВклПроведенныеВсе.Доступность()=1)  Тогда
		СтрОбраб = СтрОбраб +"
				|ОбрабатыватьДокументы Непроведенные;";
	Иначе
		СтрОбраб = СтрОбраб +"
				|ОбрабатыватьДокументы Все;";
	КонецЕсли;
	Текст = Текст +СтрОбраб;
	ТекДок = ФормДокумент.ПолучитьЗначение(ФормДокумент.ТекущаяСтрока());
    Если Режим="Итоги" Тогда
		Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
			ТекЗнач = ФормГруппы.ПолучитьЗначение(Инд);
			Если гПред.Принадлежит(ТекЗнач) = 0 Тогда
				Текст = Текст + "
					|" + ТекЗнач + " = Документ." + ТекДок + "." + ТекЗнач + ";";
			КонецЕсли;
		КонецЦикла;
		Для Инд = 1 По ФормФункции.РазмерСписка() Цикл
			ТекЗнач = ФормФункции.ПолучитьЗначение(Инд);
			Текст = Текст + "
				|пз" + ТекЗнач + " = Документ." + ТекДок + "." + ТекЗнач + ";";
		КонецЦикла;
	ИначеЕсли Режим="Реестр" Тогда
		Текст = Текст + "
					|Док = Документ." + ТекДок + ".ТекущийДокумент;";
	КонецЕсли;
	Если Режим="Итоги" Тогда
		ФормГруппы.Выгрузить(Спис);
	КонецЕсли;
	Если ТабУсловия.КоличествоСтрок()>0 Тогда
		Текст=Текст+ПеремУсловия(ТекДок,Спис);
	КонецЕсли;

    Если Режим="Итоги" Тогда
		Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
			ТекЗнач = ФормГруппы.ПолучитьЗначение(Инд);
			Текст = Текст + "
			|Группировка " + ТекЗнач + ";";
		КонецЦикла;
		ВыбТипФунк=ФормВидыФункций.ПолучитьЗначение(ФормВидыФункций.ТекущаяСтрока());
 		Для Инд = 1 По ФормФункции.РазмерСписка() Цикл
			ТекЗнач = ФормФункции.ПолучитьЗначение(Инд);
				Текст = Текст + "
				|Функция " + ВыбТипФунк + ТекЗнач +
				" = " + ВыбТипФунк+ "(пз" + ТекЗнач + ");";
		КонецЦикла;
	ИначеЕсли Режим="Реестр" Тогда
		Текст = Текст + "
			|Группировка Док;";
	КонецЕсли;
	Если ТабУсловия.КоличествоСтрок()>0 Тогда
		Текст=Текст+ПолучитьУсловие();
	КонецЕсли;
	Возврат Текст;
КонецФункции

Процедура ВывестиРеквизиты(гТаб,Запрос)

	гТаб.ВывестиСекцию("Строка|гНачало");
	Таб = гСекцияСтрРеквизит;

	Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
		Если Инд <= гУровеньРекурсии Тогда
			НаимГруппы=ФормГруппы.ПолучитьЗначение(Инд);
			Значение = Запрос.ПолучитьАтрибут(НаимГруппы);
			Если ТипЗначенияСтр(Значение) = "Справочник" Тогда
				Таб.Имя.Расшифровка(Значение);
 				Значение=Запрос.ЗначениеУпорядочивания(Инд);
			ИначеЕсли ТипЗначенияСтр(Значение) = "Документ" Тогда
				Таб.Имя.Расшифровка(Значение);
			Иначе
				Таб.Область(1,1).Расшифровка("",2);
			КонецЕсли;
			Таб.Имя.Текст = СокрЛП(Значение);
			Если Запрос.ЭтоГруппа(Инд) = 1 Тогда
				Таб.Имя.Полужирный(1);
			Иначе
				Таб.Имя.Полужирный(0);
			КонецЕсли;
		Иначе
			СтрТекст=Таб.Имя.Текст;
			Если ФлОднаКолГруппы=0 Тогда
				Таб.Имя.Текст = "(все)";
				Таб.Область(1,1).Расшифровка("",2);
			Иначе
				Таб.Имя.Текст =СокрЛП(СтрТекст);
			КонецЕсли;
			Таб.Область(1,1).Полужирный(1);
		КонецЕсли;
		Если (ФлОднаКолГруппы=1)И(Инд < ФормГруппы.РазмерСписка()) Тогда
  			Продолжить;
		КонецЕсли;
 		гТаб.ПрисоединитьСекцию(Таб);
	КонецЦикла;
	Если  ФормГруппы.РазмерСписка()=0 Тогда
		Таб.Имя.Текст = "(все)";
		Таб.Имя.Полужирный(1);
		гТаб.ПрисоединитьСекцию(Таб);
	ИначеЕсли ФлОднаКолГруппы=1 Тогда
		Таб.Имя.Текст = "(все)";
    КонецЕсли;
КонецПроцедуры

Процедура ВывестиФункции(гТаб,Запрос)
	Таб = гСекцияФункции;
	НачЗначение = 1 + ФормГруппы.РазмерСписка();
	Для Инд = 1 По ФормФункции.РазмерСписка() Цикл
		Таб.Знач.Текст  = Формат(Запрос.ПолучитьАтрибут
			(ФормВидыФункций.ПолучитьЗначение(ФормВидыФункций.ТекущаяСтрока())+ФормФункции.ПолучитьЗначение(Инд)), "Ч012.2");
		гТаб.ПрисоединитьСекцию(Таб);
	КонецЦикла;
	НачЗначение = НачЗначение + ФормФункции.РазмерСписка();
КонецПроцедуры


Процедура ОбработкаРезультатов(гТаб,Запрос)
	гУровеньРекурсии = гУровеньРекурсии + 1;
    Если ФормГруппы.РазмерСписка()< гУровеньРекурсии Тогда
		Возврат;
	КонецЕсли;
	Пока Запрос.Группировка(ФормГруппы.ПолучитьЗначение(гУровеньРекурсии))=1 Цикл
		ВывестиРеквизиты(гТаб,Запрос);
		ВывестиФункции(гТаб,Запрос);
 		гКоличествоСтрок=гКоличествоСтрок+1;
		КолСтрПечать=Цел(гКоличествоСтрок/50);
		Если КолСтрПечать>0 Тогда
			Состояние(" Обработка строк "+КолСтрПечать*50);
		КонецЕсли;

		Если гУровеньРекурсии < ФормГруппы.РазмерСписка() Тогда
			ОбработкаРезультатов(гТаб,Запрос);
		КонецЕсли;
	КонецЦикла;
	гУровеньРекурсии = гУровеньРекурсии - 1;
КонецПроцедуры

Процедура РежимТА()
	Если РежТА=1 Тогда
		ФормДатаКон=Дата(0);
		Форма.ФормДатаКон.Доступность(0);
		Форма.КнВыбПериод.Доступность(0);
	Иначе
		Форма.ФормДатаКон.Доступность(1);
		Форма.КнВыбПериод.Доступность(1);
	КонецЕсли;
КонецПроцедуры


Процедура СформироватьИтоги()
	Перем Час,Минута,Сек,СтрПредставление;
	Перем СтрУсловие,СтрРасшифровка;
	ДокМД = Метаданные.Документ(ФормДокумент.ТекущаяСтрока());
	Если (ФормДатаНач>ФормДатаКон)И(ФормДатаКон<>Дата(0)) Тогда
		Предупреждение("Внимание!Дата начала выбора превышает дату конца.Запрос не выполнен!");
		Возврат;
	КонецЕсли;
	ДатаКон=ФормДатаКон;
	Если РежТА=1 Тогда
		Попытка
			ДатаКон=Строка(ПолучитьДатуТА())+" "+ПолучитьВремяТА(Час,Минута,Сек);
		Исключение
		    ДатаКон=Строка(РабочаяДата());
		КонецПопытки;
	КонецЕсли;
	ТекстЗапроса = ПостроитьЗапрос("Итоги");
	Запрос = СоздатьОбъект("Запрос");
	Попытка
	    Запрос.выполнить(ТекстЗапроса);
	Исключение
	    Предупреждение(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	гТаб = СоздатьОбъект("Таблица");
	гТаб.ИсходнаяТаблица("Итоги");
    гСекцияСтрРеквизит = гТаб.ПолучитьСекцию("Строка|Реквизит");
	гСекцияФункции=гТаб.ПолучитьСекцию("Строка|Функция");

	гТаб.Опции(0,0,гТаб.ВысотаТаблицы(),0);
	// Нарисуем шапку...
	ПечатьУсловия(СтрУсловие,СтрРасшифровка);
	гТаб.ВывестиСекцию("Заголовок");
	гТаб.ВывестиСекцию("Шапка|гНачало");

	СекцШапкаРекв = гТаб.ПолучитьСекцию("Шапка|Реквизит");
	Если ФлОднаКолГруппы=0 Тогда
		//колонки группировок
		Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
			ФормГруппы.ПолучитьЗначение(Инд,СтрПредставление);
			СекцШапкаРекв.Реквизит.Текст = СтрПредставление;
			гТаб.ПрисоединитьСекцию(СекцШапкаРекв);
		КонецЦикла;
	Иначе
		//одна колонка группировки
		СтрКолГруппы="";
		Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
			ФормГруппы.ПолучитьЗначение(Инд,СтрПредставление);
			Если Инд<ФормГруппы.РазмерСписка() Тогда
				СтрКолГруппы = СтрКолГруппы+СтрПредставление+"/ ";
			Иначе
				СтрКолГруппы = СтрКолГруппы+СтрПредставление;
			КонецЕсли;
		КонецЦикла;
		Если ФормГруппы.РазмерСписка()>0 Тогда
			СекцШапкаРекв.Реквизит.Текст = СтрКолГруппы;
			СекцШапкаРекв.Реквизит.Расшифровка(СтрКолГруппы);
			гТаб.ПрисоединитьСекцию(СекцШапкаРекв);
		КонецЕсли;
	КонецЕсли;
    Если ФормГруппы.РазмерСписка()=0 Тогда
		СекцШапкаРекв.Реквизит.Текст = "";
		гТаб.ПрисоединитьСекцию(СекцШапкаРекв);
	КонецЕсли;

	Таб = гТаб.ПолучитьСекцию("Шапка|Функция");

	Если (ФлОднаКолГруппы=0)ИЛИ(ФормГруппы.РазмерСписка()=0)  Тогда
		КолГрупп=ФормГруппы.РазмерСписка();
	Иначе
		КолГрупп=1;
	КонецЕсли;
	НачЗначение = 1 + КолГрупп;
	Если ФормГруппы.РазмерСписка()=0 Тогда
		НачЗначение=НачЗначение+1;
	КонецЕсли;

	ВидФунк=ФормВидыФункций.ПолучитьЗначение(ФормВидыФункций.ТекущаяСтрока());
	Для Инд = 1 По ФормФункции.РазмерСписка() Цикл
		ФормФункции.ПолучитьЗначение(Инд,СтрПредставление);
		Таб.Функ.Текст = СтрПредставление;
		гТаб.ПрисоединитьСекцию(Таб);
	КонецЦикла;
	КолОбл=ФормФункции.РазмерСписка() + НачЗначение;
	Если КолОбл>=НачЗначение+1 тогда
		Обл = гТаб.Область(4,НачЗначение+1, 4, КолОбл );
		Обл.Объединить();
		Обл.РамкаСлева(3);
		Обл.РамкаСправа(3);
		Обл.РамкаСнизу(3);
    	Обл.Текст=ВидФунк;
	КонецЕсли;

	гУровеньРекурсии=0;
	гКоличествоСтрок=0;
	ОбработкаРезультатов(гТаб,Запрос);
	Запрос.вНачалоВыборки();
    ВывестиРеквизиты(гТаб,Запрос);
	ВывестиФункции(гТаб,Запрос);

	Для Инд = 1 По ФормГруппы.РазмерСписка() Цикл
		Ширина = гДлина.Получить(ФормГруппы.ПолучитьЗначение(Инд));
		Если ПустоеЗначение(Ширина) = 1 Тогда
			Ширина = 16;
		КонецЕсли;
		Если ФлОднаКолГруппы=0 Тогда
			гТаб.Область(,Инд+1,, Инд+1).ШиринаСтолбца(Ширина);
		ИначеЕсли гТаб.Область(,2,, 2).ШиринаСтолбца()<Ширина*8 Тогда
			гТаб.Область(,2,, 2).ШиринаСтолбца(Ширина);
		КонецЕсли;
	КонецЦикла;

	Для Инд = 1 По ФормФункции.РазмерСписка() Цикл
		Ширина = гДлина.Получить(ФормФункции.ПолучитьЗначение(Инд));
		Если ПустоеЗначение(Ширина) = 1 Тогда
			Ширина = 16;
		КонецЕсли;
		Если Ширина<СтрДлина(ФормФункции.ПолучитьЗначение(Инд)) Тогда
			 Ширина=СтрДлина(ФормФункции.ПолучитьЗначение(Инд))+1;
		КонецЕсли;
  		гТаб.Область(, Инд + НачЗначение,, Инд + НачЗначение).ШиринаСтолбца(Ширина);
	КонецЦикла;
	гТаб.Область(4,2,гТаб.ВысотаТаблицы(), НачЗначение+ФормФункции.РазмерСписка()).РамкаОбвести(4,4,4,4);
	гТаб.Область(5,2,гТаб.ВысотаТаблицы(), НачЗначение+ФормФункции.РазмерСписка()).ВертикальноеПоложение(2);
	гТаб.Опции(0,0,5,0);
	гТаб.ТолькоПросмотр(1);
	гТаб.Показать("Документ " + ДокМД.Представление());
КонецПроцедуры

Процедура ПриВыбореЗакладки(Ном,Значен)
	Если Значен="Реестр" Тогда
		Форма.ИспользоватьСлой("Основной,Реквизиты",2);
	ИначеЕсли Значен="Условия" Тогда
		Форма.ИспользоватьСлой("Основной,Условия",2);
	ИначеЕсли Значен="Итоги" Тогда
		Форма.ИспользоватьСлой("Основной,Итоги",2);
	ИначеЕсли Значен="Настройки" Тогда
		Форма.ИспользоватьСлой("Основной,Настройки",2);
	КонецЕсли;
	ТекЗакладка=Значен;
КонецПроцедуры

Процедура ПриОткрытии()
	УправлениеЗакладками();
	ФормДокумент.УдалитьВсе();ТС=0;
	Для Инд=1 По Метаданные.Документ() Цикл
		ФормДокумент.ДобавитьЗначение(Метаданные.Документ(Инд).Идентификатор,
			Метаданные.Документ(Инд).Представление());
		Если СокрЛП(Форма.Параметр)=СокрЛП(Метаданные.Документ(Инд).Идентификатор) Тогда
			ТС=Инд;
		КонецЕсли;
	КонецЦикла;
	Если ФормДокумент.РазмерСписка()>0 Тогда
	    ФормДокумент.ТекущаяСтрока(?(ТС=0,1,ТС));
		Контроль();
		обДокумент();
	Иначе
		Предупреждение("Внимание!В текущей конфигурации документы не созданы!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Если ВосстановитьПараметрыФормы()=0 Тогда
		ВыбВариант="Основной";
	КонецЕсли;
	Если ПустоеЗначение(ФормДатаНач) = 1 Тогда
	    ФормДатаКон = РабочаяДата();
		ФормДатаНач = НачКвартала(ФормДатаКон);
	КонецЕсли;
	ПриВыбореЗакладки(1,"Реестр");
	
КонецПроцедуры

Процедура ПриЗакрытии()
	СохранитьПараметрыФормы();
КонецПроцедуры

Процедура ПриВыбореРекв()
	ТекСтр=ФормРеквизиты.ТекущаяСтрока();
	Если ТекСтр>0 Тогда
		Фл=1;
		Если ФормРеквизиты.Пометка(ТекСтр)=1 Тогда
			Фл=0;
		КонецЕсли;
		ФормРеквизиты.Пометка(ТекСтр,Фл)
	КонецЕсли;
КонецПроцедуры

Процедура ПриИзменВключения()
	Если ВклПомеч=3 Тогда
		ВклПроведенныеВсе=3;
		Форма.ВклПроведенныеВсе.Доступность(0);
		Форма.ВклПроведенныеПров.Доступность(0);
		Форма.ВклПроведенныеНеПров.Доступность(0);
	Иначе
		Форма.ВклПроведенныеВсе.Доступность(1);
		Форма.ВклПроведенныеПров.Доступность(1);
		Форма.ВклПроведенныеНеПров.Доступность(1);
	КонецЕсли;
КонецПроцедуры

гДлина = СоздатьОбъект("СписокЗначений");
гПред = СоздатьОбъект("СписокЗначений");
гСпВариантов=СоздатьОбъект("СписокЗначений");
гСпРеквВсе = СоздатьОбъект("СписокЗначений");
гСпШиринаКолонок = СоздатьОбъект("СписокЗначений");

гПред.ДобавитьЗначение("Документ","Документ");
гПред.ДобавитьЗначение("СтрокаДокумента","СтрокаДокумента");
гПред.ДобавитьЗначение("День","День");
гПред.ДобавитьЗначение("Неделя","Неделя");
гПред.ДобавитьЗначение("Месяц","Месяц");
гПред.ДобавитьЗначение("Год","Год");

Форма.ИспользоватьЗакладки(1);
ТабУсловия.НоваяКолонка("ФлВкл",,3,,"+",3,,);
ТабУсловия.НоваяКолонка("КолПредставление",,12,,"Данные",12);
ТабУсловия.НоваяКолонка("КолУсловие","Строка","3",,"Условие",3);
ТабУсловия.НоваяКолонка("КолЗначение",,,,"Значение",12);
ТабУсловия.НоваяКолонка("КолИдентификатор");
ТабУсловия.ВидимостьКолонки("КолИдентификатор",0);

ФормВидыФункций.ДобавитьЗначение("Сумма");
ФормВидыФункций.ДобавитьЗначение("Среднее");
ФормВидыФункций.ДобавитьЗначение("Минимум");
ФормВидыФункций.ДобавитьЗначение("Максимум");
ФормВидыФункций.ТекущаяСтрока(1);

гПрефиксТаб=" (табл.часть)";
ВклПроведенныеВсе=1;
ВклПомеч=1;
ФлОднаКолГруппы=1;
ПриИзменВключения();
