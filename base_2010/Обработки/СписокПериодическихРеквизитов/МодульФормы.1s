Перем ПараметрыРеквизитов;

Перем ИзменяемыеДокументом;

Перем Элемент;

Перем Просмотр;

Перем Разделители;

Перем ТипТокена;

Перем Спр;



Функция ПолучитьТокен(Стр, П)
	Длина = СтрДлина(Стр);
	Пока П <= Длина Цикл
		Сим = Сред(Стр, П, 1);
		Если КодСимв(Сим) > КодСимв(" ") Тогда
			Прервать;
		КонецЕсли;
		П = П+1;
	КонецЦикла;

	Если П > Длина Тогда
		Возврат "";
	КонецЕсли;

	А = П+1;
	Если Сим = """" Тогда
		ТипТокена = "Строка";
		Пока А <= Длина Цикл
			Сим = Сред(Стр, А, 1);
			Если Сим = """" Тогда
				Прервать;
			КонецЕсли;
			А = А+1;
		КонецЦикла;
	ИначеЕсли Найти("0123456789", Сим) <> 0 Тогда
		ТипТокена = "Число";
		Пока А <= Длина Цикл
			Сим = Сред(Стр, А, 1);
			Если Найти("0123456789", Сим) = 0 Тогда
				Прервать;
			КонецЕсли;
			А = А+1;
		КонецЦикла;
	ИначеЕсли (КодСимв(Сим) <= КодСимв(" ")) Или (Найти(Разделители, Сим) <> 0) Тогда
		ТипТокена = "";
	Иначе
		ТипТокена = "Идентификатор";
		Пока А <= Длина Цикл
			Сим = Сред(Стр, А, 1);
			Если (КодСимв(Сим) <= КодСимв(" ")) Или (Найти(Разделители, Сим) <> 0) Тогда
				Прервать;
			КонецЕсли;
			А = А+1;
		КонецЦикла;
	КонецЕсли;
	Значение = Сред(Стр, П, А-П);
	П = А;
	Возврат Значение;
КонецФункции

Функция ПолучитьРеквизит(Реквизиты, Позиция, Рекв, Счет = "", НомерСубконто = 0, Владелец = "", ТипПоля = "", ЗначенияСписка = "", Ширина = 0, Ф = "")
    Счет = "";
	НомерСубконто = 0;
	Владелец = "";
	ТипПоля = "";
	ЗначенияСписка = "";
	Ширина = 0;
	Ф = "";
	Рекв = ПолучитьТокен(Реквизиты, Позиция);
	Если (Рекв = "") Или (ТипТокена <> "Идентификатор") Тогда
		Возврат 0;
	КонецЕсли;

	Р1 = ПолучитьТокен(Реквизиты, Позиция);
	Если ТипТокена = "Число" Тогда
		Ширина = Число(Р1);
		Р1 = ПолучитьТокен(Реквизиты, Позиция);
	КонецЕсли;

	Если Р1 = "(" Тогда
		Р1 = ПолучитьТокен(Реквизиты, Позиция);
		Пока (Р1 <> ")") И (Р1 <> "") Цикл
			Если Число(Р1) <> 0 Тогда
				НомерСубконто = Число(Р1);
				Р1 = ВРег(ПолучитьТокен(Реквизиты, Позиция));
				Если ТипТокена = "Идентификатор" Тогда
					Счет = Р1;
				КонецЕсли;
			ИначеЕсли ВРег(Р1) = "ФЛАЖОК" Тогда
				ТипПоля = ВРег(Р1);
			ИначеЕсли ВРег(Р1) = "ПОЛЕСОСПИСКОМ" Тогда
				ТипПоля = ВРег(Р1);
				ЗначенияСписка = СоздатьОбъект("СписокЗначений");
				Если ПолучитьТокен(Реквизиты, Позиция) = "(" Тогда
					Р1 = " ";
					Пока (Р1 <> ")") И (Р1 <> "") Цикл
						П1 = Позиция;
						Р1 = ПолучитьТокен(Реквизиты, Позиция);
						Пока (Р1 <> ",") И (Р1 <> ")") И (Р1 <> "") Цикл
							Р1 = ПолучитьТокен(Реквизиты, Позиция);
						КонецЦикла;
						ЗначенияСписка.ДобавитьЗначение(Сред(Реквизиты, П1, Позиция-П1-1));
					КонецЦикла;
				КонецЕсли;
			ИначеЕсли ВРег(Р1) = "ФОРМА" Тогда
				Если ПолучитьТокен(Реквизиты, Позиция) = "(" Тогда
					Ф = ПолучитьТокен(Реквизиты, Позиция);
					ПолучитьТокен(Реквизиты, Позиция);
				КонецЕсли;
			ИначеЕсли ТипТокена = "Идентификатор" Тогда
				Владелец = ВРег(Р1);
			КонецЕсли;
			Р1 = ПолучитьТокен(Реквизиты, Позиция);
		КонецЦикла;
		ПолучитьТокен(Реквизиты, Позиция);
	КонецЕсли;

	Возврат 1;
КонецФункции

Процедура ПолучитьТипРеквизита(Д, Кол, Тип, Счет, НомерСубконто, Владелец)
	СтрРеквизита = "";
	ПараметрыРеквизитов.НайтиЗначение(Кол, СтрРеквизита, "Идентификатор");
	ПараметрыРеквизитов.ПолучитьСтрокуПоНомеру(СтрРеквизита);
	Тип = ПараметрыРеквизитов.Реквизит;
	Счет = "";
	НомерСубконто = 0;
	Если ПараметрыРеквизитов.Счет <> "" Тогда
		П = СоздатьОбъект("Периодический");
		П.ИспользоватьОбъект(ПараметрыРеквизитов.Счет, Элемент);
		Счет = П.ЗначениеНаДату(Д);
		НомерСубконто = ПараметрыРеквизитов.НомерСубконто;
	КонецЕсли;

	Владелец = "";
	Если ПараметрыРеквизитов.Владелец <> "" Тогда
		П = СоздатьОбъект("Периодический");
		П.ИспользоватьОбъект(ПараметрыРеквизитов.Владелец, Элемент);
		Владелец = П.ЗначениеНаДату(Д);
	КонецЕсли;
КонецПроцедуры

Функция ЗначениеВДопКолонке(Кол, ЗначенияСписка = "")
	СтрРеквизита = "";
	ЗначенияСписка = "";
	ПараметрыРеквизитов.НайтиЗначение(Кол, СтрРеквизита, "Идентификатор");
	Если ПараметрыРеквизитов.ПолучитьЗначение(СтрРеквизита, "ТипПоля") = "ПОЛЕСОСПИСКОМ" Тогда
		ЗначенияСписка = ПараметрыРеквизитов.ПолучитьЗначение(СтрРеквизита, "ЗначенияСписка");
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции

Функция ДобавитьЗначениеВТаблицу(Д, Кол, Документ, Значение)
	Перем ЗначенияСписка;
	Стр = "";
	Если Документ.Выбран() = 1 Тогда
		Если Т.НайтиЗначение(Документ, Стр, "Документ") = 0 Тогда
			Стр = Т.НоваяСтрока();
			Т.Дата = Д;
			Если ИзменяемыеДокументом = 1 Тогда
				Т.Документ = Документ;
			КонецЕсли;
		Иначе  
			Т.ПолучитьСтрокуПоНомеру(Стр);
		КонецЕсли;
	ИначеЕсли Т.НайтиЗначение(Д, Стр, "Дата") = 1 Тогда
		Т.ПолучитьСтрокуПоНомеру(Стр);
		Если ИзменяемыеДокументом = 1 Тогда
			Если Т.Документ.Выбран() = 1 Тогда
				Т.НоваяСтрока(Стр);
				Т.Дата = Д;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Стр = Т.НоваяСтрока();
		Т.Дата = Д;
	КонецЕсли;
	Если ЗначениеВДопКолонке(Кол, ЗначенияСписка) = 1 Тогда
		Т.УстановитьЗначение(Стр, Кол+"_Доп", Значение);
		Если Значение <> 0 Тогда
			Т.УстановитьЗначение(Стр, Кол, ЗначенияСписка.ПолучитьЗначение(Значение));
		Иначе
			Т.УстановитьЗначение(Стр, Кол, "");
		КонецЕсли;
	Иначе
		Т.УстановитьЗначение(Стр, Кол, Значение);
	КонецЕсли;
	Возврат Стр;
КонецФункции

Процедура ДобавитьЗначениеВИБ(Д, Кол, Значение, Тип)
	П = СоздатьОбъект("Периодический");
	П.ИспользоватьОбъект(Кол, Элемент);
	П.ДатаЗнач = Д;
	Если Тип <> "" Тогда
		П.НазначитьТип(Тип);
	КонецЕсли;
	П.Значение = Значение;
	П.Записать();
КонецПроцедуры

Функция ДобавитьЗначение(Д, Кол, Значение, Тип)
	ДобавитьЗначениеВИБ(Д, Кол, Значение, Тип);
	Возврат ДобавитьЗначениеВТаблицу(Д, Кол, ПолучитьПустоеЗначение("Документ"), Значение);
КонецФункции

Процедура УдалитьЗначениеИзИБ(Д, Кол)
	П = СоздатьОбъект("Периодический");
	П.ИспользоватьОбъект(Кол, Элемент);
	Если П.НайтиЗначение(Д, 0) = 1 Тогда
		Если П.ТекущийДокумент().Выбран() = 0 Тогда
			П.Удалить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьЗначениеИзТаблицы(Д, Кол)
	Стр = "";
	Т.НайтиЗначение(Д, Стр, "Дата");
	Т.УстановитьЗначение(Стр, Кол, "");
	Если ЗначениеВДопКолонке(Кол) = 1 Тогда
		Т.УстановитьЗначение(Стр, Кол+"_Доп", "");
	КонецЕсли;
	УдалитьСтроку = 1;
	ПараметрыРеквизитов.ВыбратьСтроки();
	Пока ПараметрыРеквизитов.ПолучитьСтроку() = 1 Цикл
		П = СоздатьОбъект("Периодический");
		П.ИспользоватьОбъект(ПараметрыРеквизитов.Идентификатор, Элемент);
		Если П.НайтиЗначение(Д, 0) = 1 Тогда
			УдалитьСтроку = 0;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если УдалитьСтроку = 1 Тогда
		Т.УдалитьСтроку(Стр);
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьЗначение(Д, Кол)
	УдалитьЗначениеИзИБ(Д, Кол);
	УдалитьЗначениеИзТаблицы(Д, Кол);
КонецПроцедуры

Функция ПолучитьЗначениеИзТаблицы(Стр, Кол)
	Если ЗначениеВДопКолонке(Кол) = 1 Тогда
		Возврат Т.ПолучитьЗначение(Стр, Кол+"_Доп");
	Иначе
		Возврат Т.ПолучитьЗначение(Стр, Кол);
	КонецЕсли;
КонецФункции

Процедура НазначитьТипыЗависимыхРеквизитов(Стр, Кол)
	Перем Д2;
	Д1 = Т.ПолучитьЗначение(Стр, "Дата");
	П = СоздатьОбъект("Периодический");
	П.ИспользоватьОбъект(Кол, Элемент);
	Если П.НайтиЗначение(Д1+1, 1) = 1 Тогда
		Д2 = П.ДатаЗнач-1;
	Иначе
		Д2 = '30.12.9999';
	КонецЕсли;

	ПараметрыРеквизитов.ВыбратьСтроки();
	Пока ПараметрыРеквизитов.ПолучитьСтроку() = 1 Цикл
		Если ПараметрыРеквизитов.Идентификатор = Кол Тогда
			Продолжить;
		КонецЕсли;

		Тип = "";
		Если ПараметрыРеквизитов.Счет = Кол Тогда
			Счет = ПолучитьЗначениеИзТаблицы(Стр, ПараметрыРеквизитов.Счет);
			Тип = Счет.ВидСубконто(ПараметрыРеквизитов.НомерСубконто);
		КонецЕсли;

		Владелец = "";
		Если ПараметрыРеквизитов.Владелец = Кол Тогда
			Владелец = ПолучитьЗначениеИзТаблицы(Стр, ПараметрыРеквизитов.Владелец);
		КонецЕсли;

		Если (Тип <> "") Или (Владелец <> "") Тогда
			П = СоздатьОбъект("Периодический");
			П.ИспользоватьОбъект(ПараметрыРеквизитов.Идентификатор, Элемент);
			П.ВыбратьЗначения(Д1, Д2);
			Пока П.ПолучитьЗначение() = 1 Цикл
				Если П.ТекущийДокумент().Выбран() = 1 Тогда
					Продолжить;
				КонецЕсли;
				Если Тип <> "" Тогда
					П.НазначитьТип(Тип);
				КонецЕсли;

                Если (ТипЗначенияСтр(П.Значение) = "Справочник") И (ТипЗначенияСтр(Владелец) = "Справочник") Тогда
					Если П.Значение.Выбран() = 1 Тогда
						Если Владелец.Выбран() = 0 Тогда
							П.Значение = "";
						Иначе
							Если Метаданные.Справочник(П.Значение.Вид()).Владелец.Выбран() = 1 Тогда
								Если ВРег(Метаданные.Справочник(П.Значение.Вид()).Владелец.Идентификатор) = ВРег(Владелец.Вид()) Тогда
									Если П.Значение.Владелец <> Владелец Тогда
										П.Значение = "";
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;

				П.Записать();

				ДобавитьЗначениеВТаблицу(П.ДатаЗнач, ПараметрыРеквизитов.Идентификатор, П.ТекущийДокумент(), П.Значение);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьИзменить(Добавление)
	Если Просмотр = 1 Тогда
		Возврат;
	КонецЕсли;
	Если (Добавление = 0) И (Т.КоличествоСтрок() = 0) Тогда
		Добавление = 1;
	КонецЕсли;
	Стр = Т.ТекущаяСтрока();
	Кол = Т.ТекущаяКолонка();
	Если (Кол = "Дата") Или (Кол = "Документ") Тогда
		Если ПараметрыРеквизитов.КоличествоСтрок() > 0 Тогда
			Кол = ПараметрыРеквизитов.ПолучитьЗначение(1, "Идентификатор");
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
    Если (ИзменяемыеДокументом = 1) И (Добавление = 0) Тогда
		Если Т.Документ.Выбран() = 1 Тогда
			Предупреждение("Введенные документом значения редактировать нельзя!");
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Тип = "";
	Владелец = "";
	Счет = "";
	НомерСубконто = 0;
	ПолучитьТипРеквизита(Т.Дата, Кол, Тип, Счет, НомерСубконто, Владелец);
	Если ТипЗначенияСтр(Счет) = "Счет" Тогда
		Если Счет.ВидСубконто(НомерСубконто).Выбран() = 0 Тогда
			Предупреждение("Для заданного счета "+НомерСубконто+"-й вид субконто не определен!");
			Возврат;
		КонецЕсли
	Иначе
		Если Тип.Тип = "Неопределенный" Тогда
			Предупреждение("Реквизит неопределенного типа редактировать нельзя!");
			Возврат;
		КонецЕсли;
	КонецЕсли;

	СЗ = СоздатьОбъект("СписокЗначений");
	СЗ.ДобавитьЗначение(ПараметрыРеквизитов.Реквизит, "Реквизит");
	СЗ.ДобавитьЗначение(Тип, "Тип");
	СЗ.ДобавитьЗначение(Счет, "Счет");
	СЗ.ДобавитьЗначение(НомерСубконто, "НомерСубконто");
	СЗ.ДобавитьЗначение(Владелец, "Владелец");
	Если Добавление = 1 Тогда
		СЗ.ДобавитьЗначение(ТекущаяДата(), "Дата");
	Иначе
		СЗ.ДобавитьЗначение(Т.Дата, "Дата");
	КонецЕсли;
	СЗ.ДобавитьЗначение(ПараметрыРеквизитов.ТипПоля, "ТипПоля");
	СЗ.ДобавитьЗначение(ПараметрыРеквизитов.ЗначенияСписка, "ЗначенияСписка");
	Если (Стр > 0) И (Стр <= Т.КоличествоСтрок()) Тогда
		СЗ.ДобавитьЗначение(ПолучитьЗначениеИзТаблицы(Стр, Кол), "Значение");
	КонецЕсли;
	Ф = ПараметрыРеквизитов.Форма;
	Если Ф = "" Тогда
		Ф = "РедактированиеПериодическогоРеквизита";
	КонецЕсли;
	Ф = "Отчет."+Ф;
	ОткрытьФормуМодально(Ф, СЗ);
	Если СЗ.Получить("ОК") = 1 Тогда
		Д = СЗ.Получить("Дата");
		Значение = СЗ.Получить("Значение");

		Если (Добавление = 0) И (Д <> Т.Дата) Тогда
			УдалитьЗначение(Т.Дата, Кол);
		КонецЕсли;

		Если ТипЗначенияСтр(Счет) = "Счет" Тогда
			Тип = Счет.ВидСубконто(НомерСубконто);
		КонецЕсли;
		ДобавитьЗначение(Д, Кол, Значение, Тип);
		Если ИзменяемыеДокументом = 0 Тогда
			Т.Сортировать("Дата");
		Иначе
			Т.Сортировать("Дата, Документ", 1);
		КонецЕсли;
		Т.ТекущаяСтрока(Т.НомерСтроки);
		НазначитьТипыЗависимыхРеквизитов(Т.НомерСтроки, Кол);
	КонецЕсли;
КонецПроцедуры

Процедура Удалить()
	Стр = Т.ТекущаяСтрока();
	Если Стр = 0 Тогда
	    Возврат;
	КонецЕсли; 

	Кол = Т.ТекущаяКолонка();
	Если (Кол = "Дата") Или (Кол = "Документ") Тогда
		Если ПараметрыРеквизитов.КоличествоСтрок() > 0 Тогда
			Кол = ПараметрыРеквизитов.ПолучитьЗначение(1, "Идентификатор");
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
    Если ИзменяемыеДокументом = 1 Тогда
		Если Т.Документ.Выбран() = 1 Тогда
			Предупреждение("Введенные документом значения удалять нельзя!");
			Возврат;
		КонецЕсли;
	КонецЕсли;

	УдалитьЗначение(Т.Дата, Кол);
	Если Стр > 1 Тогда
		НазначитьТипыЗависимыхРеквизитов(Стр-1, Кол);
	КонецЕсли;
	Если Стр <= Т.КоличествоСтрок() Тогда
		Т.ТекущаяСтрока(Стр);
	ИначеЕсли Стр > 1 Тогда
		Т.ТекущаяСтрока(Стр-1);
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьНаДату()
	Стр = Т.ТекущаяСтрока();
	Если Стр = 0 Тогда
	    Возврат;
	КонецЕсли; 
	
	Кол = Т.ТекущаяКолонка();
	Если ИзменяемыеДокументом = 1 Тогда
		Если Т.Документ.Выбран() = 1 Тогда
			Предупреждение("Введенные документом значения удалять нельзя!");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Д = Т.Дата;
	Т.УдалитьСтроку(Стр);
	ПараметрыРеквизитов.ВыбратьСтроки();
	Пока ПараметрыРеквизитов.ПолучитьСтроку() = 1 Цикл
		УдалитьЗначениеИзИБ(Д, ПараметрыРеквизитов.Идентификатор);
	КонецЦикла;
	Если Стр <= Т.КоличествоСтрок() Тогда
		Т.ТекущаяСтрока(Стр);
	ИначеЕсли Стр > 1 Тогда
		Т.ТекущаяСтрока(Стр-1);
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Перем Идентификатор, Счет, НомерСубконто, Владелец, ТипПоля, ЗначенияСписка, Ширина, Ф;
	Если (ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений") Тогда
		Форма.Заголовок(Форма.Параметр.Получить("Заголовок"));
		Конт = Форма.Параметр.Получить("Контекст");
		Реквизиты = Форма.Параметр.Получить("Реквизиты");
		Просмотр = Форма.Параметр.Получить("Просмотр");
		Элемент = Конт.ТекущийЭлемент();

		Спр = СоздатьОбъект("Справочник."+Конт.Вид());
		Спр.НайтиЭлемент(Элемент);
		Если Просмотр = 1 Тогда
			Форма.Добавить.Видимость(0);
			Форма.Изменить.Видимость(0);
			Форма.Удалить.Видимость(0);
			Форма.УдалитьНаДату.Видимость(0);
		Иначе
			Если Спр.Блокировка(1) = 0 Тогда
				Предупреждение("Запись заблокирована!");
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЕсли;

		П = СоздатьОбъект("Периодический");
		Справочник = Метаданные.Справочник(Конт.Вид());
		Т.НоваяКолонка("Дата", "Дата",,,, 13);
		Т.Фиксировать(, 1);
		Т.НоваяКолонка("Документ", "Документ",,,, 20);
		ПараметрыРеквизитов = СоздатьОбъект("ТаблицаЗначений");
		ПараметрыРеквизитов.НоваяКолонка("Идентификатор");
		ПараметрыРеквизитов.НоваяКолонка("Реквизит");
		ПараметрыРеквизитов.НоваяКолонка("Счет");
		ПараметрыРеквизитов.НоваяКолонка("НомерСубконто");
		ПараметрыРеквизитов.НоваяКолонка("Владелец");
		ПараметрыРеквизитов.НоваяКолонка("ТипПоля");
		ПараметрыРеквизитов.НоваяКолонка("ЗначенияСписка");
		ПараметрыРеквизитов.НоваяКолонка("Форма");

		ИзменяемыеДокументом = 0;

		Если Реквизиты = "" Тогда
			Для А=1 По Справочник.Реквизит() Цикл
				Реквизит = Справочник.Реквизит(А);
				Если Реквизит.Периодический = 0 Тогда
					Продолжить;
				КонецЕсли;
				Если Реквизит.ИзменяетсяДокументами = 1 Тогда
					ИзменяемыеДокументом = 1;
				КонецЕсли;
				ПараметрыРеквизитов.НоваяСтрока();
				ПараметрыРеквизитов.Идентификатор = ВРег(Реквизит.Идентификатор);
				ПараметрыРеквизитов.Реквизит = Реквизит;
				ПараметрыРеквизитов.Счет = "";
				ПараметрыРеквизитов.НомерСубконто = 0;
				ПараметрыРеквизитов.Владелец = "";
				ПараметрыРеквизитов.ТипПоля = "";
				ПараметрыРеквизитов.ЗначенияСписка = "";
                ПараметрыРеквизитов.Форма = "";

				Т.НоваяКолонка(ПараметрыРеквизитов.Идентификатор, Реквизит,,, ""+Реквизит);
				П.ИспользоватьОбъект(Реквизит.Идентификатор, Элемент);
				П.ВыбратьЗначения();
				Пока П.ПолучитьЗначение() = 1 Цикл
					ДобавитьЗначениеВТаблицу(П.ДатаЗнач, ПараметрыРеквизитов.Идентификатор, П.ТекущийДокумент(), П.Значение);
				КонецЦикла;
			КонецЦикла;
		Иначе
			НевидимыеКолонки = "";
			Позиция = 1;
			Пока ПолучитьРеквизит(Реквизиты, Позиция, Идентификатор, Счет, НомерСубконто, Владелец, ТипПоля, ЗначенияСписка, Ширина, Ф) = 1 Цикл
				Реквизит = Справочник.Реквизит(Идентификатор);
				Если Реквизит.Выбран() = 0 Тогда
					Продолжить;
				КонецЕсли;
				Если Реквизит.Периодический = 0 Тогда
					Продолжить;
				КонецЕсли;
				Если Реквизит.ИзменяетсяДокументами = 1 Тогда
					ИзменяемыеДокументом = 1;
				КонецЕсли;
				ПараметрыРеквизитов.НоваяСтрока();
				ПараметрыРеквизитов.Идентификатор = ВРег(Реквизит.Идентификатор);
				ПараметрыРеквизитов.Реквизит = Реквизит;
				ПараметрыРеквизитов.Счет = Счет;
				ПараметрыРеквизитов.НомерСубконто = НомерСубконто;
				ПараметрыРеквизитов.Владелец = Владелец;
				ПараметрыРеквизитов.ТипПоля = ТипПоля;
				ПараметрыРеквизитов.ЗначенияСписка = ЗначенияСписка;
				ПараметрыРеквизитов.Форма = Ф;
				Если ПараметрыРеквизитов.ТипПоля = "ПОЛЕСОСПИСКОМ" Тогда
					Т.НоваяКолонка(ПараметрыРеквизитов.Идентификатор,,,, ""+Реквизит, Ширина);
					Т.НоваяКолонка(ПараметрыРеквизитов.Идентификатор+"_Доп", Реквизит);
					Если НевидимыеКолонки <> "" Тогда
						НевидимыеКолонки = НевидимыеКолонки+",";
					КонецЕсли;
		            НевидимыеКолонки = НевидимыеКолонки+ПараметрыРеквизитов.Идентификатор+"_Доп";
				Иначе
					Т.НоваяКолонка(ПараметрыРеквизитов.Идентификатор, Реквизит,,, ""+Реквизит, Ширина);
				КонецЕсли;
				П.ИспользоватьОбъект(Реквизит.Идентификатор, Элемент);
				П.ВыбратьЗначения();
				Пока П.ПолучитьЗначение() = 1 Цикл
					ДобавитьЗначениеВТаблицу(П.ДатаЗнач, ПараметрыРеквизитов.Идентификатор, П.ТекущийДокумент(), П.Значение);
				КонецЦикла;
			КонецЦикла;
			Т.ВидимостьКолонки(НевидимыеКолонки, 0);
		КонецЕсли;

		Если ИзменяемыеДокументом = 0 Тогда
			Т.УдалитьКолонку("Документ");
			Т.Сортировать("Дата");
			Т.ТекущаяКолонка(2);
		Иначе
			Т.Сортировать("Дата, Документ", 1);
			Т.ТекущаяКолонка(3);
		КонецЕсли;
	Иначе
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗакрытии()
	Если Спр.Блокировка() = 1 Тогда
	    Спр.Блокировка(0);
	КонецЕсли;
КонецПроцедуры 

Разделители = " .,;:()[]{}'""/\?!@#$%^&*+-=<>~`|";
