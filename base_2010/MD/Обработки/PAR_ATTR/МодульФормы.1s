Перем гСпрИД, гСпрМД, гСтрФорм, гРежим; // общие
Перем гТабРеквизитов, гСпЗаголовок; // для загрузки
Перем гСпРеквизиты; // для выгрузки

Процедура Выбрать() 
 	Если гРежим = "Выгрузка" Тогда	
   		СпРеквизитов.Выгрузить(гСпРеквизиты);
    ИначеЕсли гРежим = "Загрузка" Тогда
   		СпРекв = СоздатьОбъект("СписокЗначений");
		ТабРекв = СоздатьОбъект("ТаблицаЗначений"); 
		ТабРеквизиты.Выгрузить(ТабРекв);
		СпРекв.ДобавитьЗначение(ТабРекв, "ТабСоответствий");
		СпРекв.ДобавитьЗначение(ВыбВладелец, "ВладелецУмолч");
	КонецЕсли;
   Форма.Параметр = СпРекв;
   Форма.Закрыть();
КонецПроцедуры

Процедура ВклРекв(Фл)
	Для Инд = 1  По СпРеквизитов.РазмерСписка()  Цикл
	   СпРеквизитов.Пометка(Инд, Фл)
	КонецЦикла;
КонецПроцедуры

Процедура ВыбратьТипЗначения(НомТекСтр)
Перем ВыбПоз, СтрПредст, ВыбЗн;
	Если НомТекСтр>ТабРеквизиты.КоличествоСтрок() Тогда
		НомТекСтр = 0;
	КонецЕсли;
    СпЗнач = СоздатьОбъект("СписокЗначений");
	гТабРеквизитов.ВыбратьСтроки();
	Пока гТабРеквизитов.ПолучитьСтроку() = 1 Цикл
		СпЗнач.ДобавитьЗначение(гТабРеквизитов.Идентификатор, гТабРеквизитов.Представление);
	КонецЦикла;
	СпЗнач.ВставитьЗначение(1, "<не загружать>");
	СпЗнач.ВставитьЗначение(2, "<значение>");
	Если (НомТекСтр > 0) И (ТабРеквизиты.ТекущаяКолонка() = "ПредстЗагруж") Тогда
		ВыбПоз = 0;
		СпЗнач.ВыбратьЗначение(ВыбЗн, , ВыбПоз, , 2);
		Если (ВыбПоз > 0) И (ВыбПоз <= СпЗнач.РазмерСписка()) Тогда
			Если СокрЛП(ВыбЗн) = "<не загружать>" Тогда
				Значен = ПолучитьПустоеЗначение(); 
				ТабРеквизиты.ПредстЗагруж = Значен;
				ТабРеквизиты.ИДЗагруж = Значен;
				ТабРеквизиты.Значение = Значен;
				Возврат;
			ИначеЕсли СокрЛП(ВыбЗн) = "<значение>" Тогда
				ТипТек = гСпрМД.Реквизит(ТабРеквизиты.ИДТек).Тип ;
				Длина = гСпрМД.Реквизит(ТабРеквизиты.ИДТек).Длина;
				Точность = гСпрМД.Реквизит(ТабРеквизиты.ИДТек).Точность;
				Если (ТипТек = "Справочник")ИЛИ(ТипТек = "Документ")ИЛИ(ТипТек = "Перечисление")ИЛИ(ТипТек = "Счет") Тогда
					Если ПустоеЗначение(гСпрМД.Реквизит(ТабРеквизиты.ИДТек).Вид) = 0 Тогда
						ТипТек = ТипТек+"."+гСпрМД.Реквизит(ТабРеквизиты.ИДТек).Вид;
					КонецЕсли;
				КонецЕсли;
				ВвестиЗначение(Значен, "Выберете значения реквизита '"+ТабРеквизиты.ПредстТек+"'", ТипТек, Длина, Точность);
				ТабРеквизиты.Значение = Значен;
				ТабРеквизиты.ПредстЗагруж = "<"+Значен+">";
				ТабРеквизиты.ИДЗагруж = "<значение>";
				Возврат;
			КонецЕсли;
			Поз2 = 0;
            Если (гТабРеквизитов.НайтиЗначение(ВыбЗн, Поз2, "Идентификатор") = 0) Тогда
            	Возврат;	
            КонецЕсли;
			Если (гТабРеквизитов.ПолучитьЗначение(Поз2, "Тип")<>ТабРеквизиты.ТипТек) Тогда
				 Предупреждение("Внимание! Типы выбранных реквизитов не совпадают!");
			КонецЕсли;
			Значен = СпЗнач.ПолучитьЗначение(ВыбПоз, СтрПредст);
			Если ПустоеЗначение(Значен) = 0 Тогда
				ТабРеквизиты.ПредстЗагруж = СтрПредст;
			Иначе
				ТабРеквизиты.ПредстЗагруж = Значен;
			КонецЕсли;
			ТабРеквизиты.ИДЗагруж = Значен;
			ТабРеквизиты.Значение = ПолучитьПустоеЗначение();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

Процедура ВклРеквизиты()
	Перем СтрПредстЗагруж;
	//заполнить таблицу соответствий реквизитов справочников
	ТабРеквизиты.УдалитьСтроки();
	Для Инд = 1 По гСпрМД.Реквизит() Цикл
		ТабРеквизиты.НоваяСтрока();
		ТабРеквизиты.ПредстТек = гСпрМД.Реквизит(Инд).Представление();
		ТабРеквизиты.ИДТек = гСпрМД.Реквизит(Инд).Идентификатор;
		ТабРеквизиты.ТипТек = гСпрМД.Реквизит(Инд).Тип;
		ТабРеквизиты.ИспользованиеТек = гСпрМД.Реквизит(Инд).Использование;
		НомСтр = 0;
		Поз = гТабРеквизитов.НайтиЗначение(СокрЛП(ТабРеквизиты.ИДТек), НомСтр, "Идентификатор");
		ТекТип = СокрЛП(гСпрМД.Реквизит(Инд).Тип);
		Если Поз>0  Тогда
 			ИДСпрЗагруж = гТабРеквизитов.ПолучитьЗначение(НомСтр, "Идентификатор");
			ТабРеквизиты.ИДЗагруж = ИДСпрЗагруж;
			ТабРеквизиты.ПредстЗагруж = гТабРеквизитов.ПолучитьЗначение(НомСтр, "Представление");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОтклРекв()
	Для Инд = 1  По ТабРеквизиты.КоличествоСтрок()  Цикл 
	   	 ТабРеквизиты.УстановитьЗначение(Инд, "ИДЗагруж", ПолучитьПустоеЗначение());
	   	ТабРеквизиты.УстановитьЗначение(Инд, "ПредстЗагруж", ПолучитьПустоеЗначение());
	КонецЦикла;
КонецПроцедуры

Процедура ДоступГрУмолч(Фл)
	Форма.ВыбВладелец.Доступность(Фл);
	Если Фл = 0 Тогда
		ВыбВладелец = 0;	
	КонецЕсли;
КонецПроцедуры

СпПараметров = Форма.Параметр;
Форма.ИспользоватьЗакладки(1);
ТабРеквизиты.НоваяКолонка("ПредстТек", , 12, , "Рекв.текущ.", 12);
ТабРеквизиты.НоваяКолонка("ПредстЗагруж", , 12, , "Рекв.загруж.", 12);
ТабРеквизиты.НоваяКолонка("ИДТек"); 
ТабРеквизиты.НоваяКолонка("ТипТек");            
ТабРеквизиты.НоваяКолонка("ИДЗагруж");
ТабРеквизиты.НоваяКолонка("Значение");
ТабРеквизиты.НоваяКолонка("ИспользованиеТек", "Строка");
ТабРеквизиты.ВидимостьКолонки("ИДТек", 0);
ТабРеквизиты.ВидимостьКолонки("ИДЗагруж", 0);
ТабРеквизиты.ВидимостьКолонки("ТипТек", 0);
ТабРеквизиты.ВидимостьКолонки("ИспользованиеТек", 0);
ТабРеквизиты.ВидимостьКолонки("Значение", 0);

Если ПустоеЗначение(СпПараметров) = 0 Тогда
	гСпрИД = СпПараметров.Получить("Идентификатор");
	гСпрМД = Метаданные.Справочник(гСпрИД);
	гСпрПредставление = гСпрМД.Представление();
	гРежим = СпПараметров.Получить("Режим");
	Если гРежим = "Выгрузка" Тогда 
		Форма.ИспользоватьСлой("Основной, Выгрузка", 2);
		гСтрФорм = "Укажите реквизиты справочника '"+гСпрПредставление+"' для выгрузки:";
		НомПоз = 0;  
		гСпРеквизиты = СпПараметров.Получить("Реквизиты");
		гСпРеквизиты.Выгрузить(СпРеквизитов);
	ИначеЕсли гРежим = "Загрузка" Тогда
		Форма.ИспользоватьСлой("Основной, Загрузка", 2);
		гСтрФорм = "Выберете реквизиты для загрузки справочника '"+гСпрПредставление+"':";
		гТабРеквизитов = СпПараметров.Получить("ТабЗагружРеквизитов");
		гСпЗаголовок = СпПараметров.Получить("ЗаголовокЗагружСпр");
		ТабСоответствий = СпПараметров.Получить("ТабСоответствий"); 
		Если гСпрМД.Владелец.Выбран() = 0 Тогда
			Форма.ФлВладелец.Доступность(0);
			Форма.СтрПодчГруппе.Доступность(0);
		Иначе
			Форма.ВыбВладелец.НазначитьТип("Справочник."+гСпрМД.Владелец.Идентификатор);
			ВыбВладелец = СпПараметров.Получить("ВладелецУмолч");
			Если ПустоеЗначение(ВыбВладелец) = 0 Тогда
			    ФлВладелец = 1;
			КонецЕсли;
		КонецЕсли; 
		Форма.Обновить(1);
		Если ПустоеЗначение(ТабСоответствий) = 0 Тогда
			ТабСоответствий.Выгрузить(ТабРеквизиты);
			ТабРеквизиты.ВидимостьКолонки("ИДТек", 0);
			ТабРеквизиты.ВидимостьКолонки("ИДЗагруж", 0);
			ТабРеквизиты.ВидимостьКолонки("ТипТек", 0);
			ТабРеквизиты.ВидимостьКолонки("ИспользованиеТек", 0);
			ТабРеквизиты.ВидимостьКолонки("Значение", 0);
		КонецЕсли;
		Если ТабРеквизиты.КоличествоСтрок() = 0 Тогда
			ВклРеквизиты();
		КонецЕсли;
	КонецЕсли;
КонецЕсли;

Форма.Параметр = ПолучитьПустоеЗначение();
