// по всем вопросам обращаться dzsysop@mail.ru
Процедура ОбработкаПроведения()

	БИ=СоздатьОбъект("БухгалтерскиеИтоги");
	БИ.ИспользоватьСубконто(ВидыСубконто.Склады,РасхСклад,2);
	БИ.ИспользоватьСубконто(ВидыСубконто.Товары,,1);
	БИ.ВыполнитьЗапрос(ДатаДок,ДатаДок,"217.1",,,1);
	БИ.ВыбратьСубконто(ВидыСубконто.Товары);

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		НДС=0;
		СуммаСеб=0;
		Если (Количество=0) или (СуммаРозн=0) Тогда
			Сообщить("Нулевые сумма или количество в строке "+НомерСтроки+"!");
			Продолжить;
		ИначеЕсли Товар.Выбран()=0 Тогда
			Сообщить("Не указан товар в строке "+НомерСтроки+"!");
			Продолжить;
		КонецЕсли;
		Если БИ.ПолучитьСубконто(ВидыСубконто.Товары,,Товар)=0 Тогда
			Сообщить("Не найдены остатки по "+Товар+" на складе "+РасхСклад+" в строке "+НомерСтроки+"!");
			Продолжить;
		ИначеЕсли БИ.СКД("К")<Количество Тогда
			Сообщить("Остаток "+Товар+" на складе "+РасхСклад+" = "+СокрЛП(БИ.СКД("К"))
			+", что меньше "+СокрЛП(Строка(Количество))+" в строке "+НомерСтроки+"!");
			Продолжить;
		Иначе
			Если Количество=БИ.СКД("К") Тогда
				СуммаСеб=БИ.СКД("С");
			Иначе
				СуммаСеб=БИ.СКД("С")*Количество/БИ.СКД("К");
			КонецЕсли;
		КонецЕсли;
		
		Операция.НоваяПроводка();
		Операция.Дебет.Счет = СчетПоКоду("217.21",ПланыСчетов.Основной);
		Операция.Дебет.СтавкиНДС = Товар.НДС.Получить(ДатаДок);
		Операция.Дебет.Склады = ПрихСклад;
		Операция.Кредит.Счет = СчетПоКоду("217.1",ПланыСчетов.Основной);
		Операция.Кредит.Товары = Товар;
		Операция.Кредит.Склады = РасхСклад;
		Операция.Количество = Количество;
		Операция.Сумма=СуммаСеб;
		
		СтНДС=Товар.НДС.Получить(ДатаДок).Ставка;
		НДС=СуммаРозн*СтНДС/(100+СтНДС);
		Если НДС>0 Тогда
			Операция.НоваяПроводка();
			Операция.Дебет.Счет = СчетПоКоду("217.21",ПланыСчетов.Основной);
			Операция.Дебет.СтавкиНДС = Товар.НДС.Получить(ДатаДок);
			Операция.Дебет.Склады = ПрихСклад;
			Операция.Кредит.Счет = СчетПоКоду("825.21",ПланыСчетов.Основной);
			Операция.Кредит.СтавкиНДС = Товар.НДС.Получить(ДатаДок);
			Операция.Кредит.Склады = ПрихСклад;
			Операция.Сумма=НДС;
		КонецЕсли;
		
		Если СуммаРозн-СуммаСеб-НДС<0 Тогда
			Сообщить("Вы продаете "+СокрЛП(Товар)+" ниже его себестоимости!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		
		Операция.НоваяПроводка();
		Операция.Дебет.Счет = СчетПоКоду("217.21",ПланыСчетов.Основной);
		Операция.Дебет.СтавкиНДС = Товар.НДС.Получить(ДатаДок);
		Операция.Дебет.Склады = ПрихСклад;
		Операция.Кредит.Счет = СчетПоКоду("821.21",ПланыСчетов.Основной);
		Операция.Кредит.СтавкиНДС = Товар.НДС.Получить(ДатаДок);
		Операция.Кредит.Склады = ПрихСклад;
		Операция.Сумма=СуммаРозн-СуммаСеб-НДС;

		Если ЦенаРозн<>Товар.ОтпускЦена.Получить(ДатаДок) Тогда
			УстановитьРеквизитСправочника(Товар,"ОтпускЦена",ЦенаРозн,ДатаДок)
		КонецЕсли;
	КонецЦикла;
	Если Операция.КоличествоПроводок()=0 Тогда
		Сообщить("Документ не проведен!","!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Операция.Записать();

КонецПроцедуры
