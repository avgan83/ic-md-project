Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем СНД, СНК, СКД, СКК;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем РазделительУчета, Субконто;
Перем Ит, РазвСальдоИт, ТЗСальдо;
Перем ПредставлениеРУ;
Перем тРазворотСубконто;
Перем Меню;             

//----------------------------------------------------------------------
Функция РазвСальдоЕстьПравило(ПарамСчет, Стр = 0)
	// ПАРАМЕТРЫ:
	//  ДЕЙСТВИЕ: 0 - правила нет. 2 - Есть. 1 - есть в поддереве.

	Если ФормРазворот.НайтиЗначение(ПарамСчет, Стр,"Счет") = 1 Тогда
		Возврат 2;
	КонецЕсли;

	Если ПарамСчет.ЭтоГруппа() = 1 Тогда
		ФормРазворот.ВыбратьСтроки();
		Пока ФормРазворот.ПолучитьСтроку() = 1 Цикл
			Если ФормРазворот.Счет.ПринадлежитГруппе(ПарамСчет) = 1 Тогда
				Возврат 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат 0;
КонецФункции

//----------------------------------------------------------------------
Процедура РассчитатьСальдоПоПравилам(ПланСчетов)
	ТЗСальдо = СоздатьОбъект("ТаблицаЗначений");
	ТЗСальдо.НоваяКолонка("СумСальдо");
	Если ДанныеПоВалютам = 1 Тогда
		ТЗСальдо.НоваяКолонка("ВалСальдо");
	КонецЕсли;

	ФормРазворот.ВыбратьСтроки();
	Пока ФормРазворот.ПолучитьСтроку() = 1 Цикл
		ТЗСальдо.НоваяСтрока();

		ТЗ = СоздатьОбъект("ТаблицаЗначений");
		ТЗ.НоваяКолонка("Валюта");
		ТЗ.НоваяКолонка("СНД");
		ТЗ.НоваяКолонка("СНК");
		ТЗ.НоваяКолонка("СКД");
		ТЗ.НоваяКолонка("СКК");
		ТЗСальдо.СумСальдо = ТЗ;

		Если (ФормРазворот.Счет.Валютный = 1) И (ДанныеПоВалютам = 1) Тогда
			ТЗВ = СоздатьОбъект("ТаблицаЗначений");
			ТЗВ.НоваяКолонка("Валюта");
			ТЗВ.НоваяКолонка("СНД");
			ТЗВ.НоваяКолонка("СНК");
			ТЗВ.НоваяКолонка("СКД");
			ТЗВ.НоваяКолонка("СКК");
			ТЗСальдо.ВалСальдо = ТЗВ;
		КонецЕсли;

		Если ПланСчетов <> ФормРазворот.Счет.ПланСчетов() Тогда
			Продолжить;
		КонецЕсли;

		Если ФормРазворот.Субсчета = "Х" Тогда
			// ...по субсчетам...
			Уровень = ФормРазворот.Счет.Уровень()+1;
			Если Ит.ПолучитьСчет(, ФормРазворот.Счет) = 1 Тогда
				ТЗ.НоваяСтрока();
				ТЗ.СНД = 0;
				ТЗ.СНК = 0;
				ТЗ.СКД = 0;
				ТЗ.СКК = 0;
				Пока Ит.ПолучитьСчет() = 1 Цикл
					Ит.Опции(ДанныеПоЗабалансовым);
					
					Если Ит.Счет.Уровень() < Уровень Тогда
						Прервать;
					КонецЕсли;
					Если Ит.Счет.Уровень() > Уровень Тогда
						Продолжить;
					КонецЕсли;
					СН = Ит.СНД(1)-Ит.СНК(1);
					Если СН > 0 Тогда
						ТЗ.СНД = ТЗ.СНД+СН;
					Иначе
						ТЗ.СНК = ТЗ.СНК-СН;
					КонецЕсли;

					СК = Ит.СКД(1)-Ит.СКК(1);
					Если СК > 0 Тогда
						ТЗ.СКД = ТЗ.СКД+СК;
					Иначе
						ТЗ.СКК = ТЗ.СКК-СК;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

			Если (ФормРазворот.Счет.Валютный = 1) И (ДанныеПоВалютам = 1) Тогда
				Ит.ПолучитьСчет(, "");
				Ит.ВыбратьВалюты();
				Пока Ит.ПолучитьВалюту() = 1 Цикл
					Если Ит.ПолучитьСчет(, ФормРазворот.Счет) = 1 Тогда
						Ит.Опции(ДанныеПоЗабалансовым);
						ТЗ.НоваяСтрока();
						ТЗ.Валюта = Ит.Валюта;
						ТЗ.СНД = 0;
						ТЗ.СНК = 0;
						ТЗ.СКД = 0;
						ТЗ.СКК = 0;
						ТЗВ.НоваяСтрока();
						ТЗВ.Валюта = Ит.Валюта;
						ТЗВ.СНД = 0;
						ТЗВ.СНК = 0;
						ТЗВ.СКД = 0;
						ТЗВ.СКК = 0;
						Пока Ит.ПолучитьСчет() = 1 Цикл
							Если Ит.Счет.Уровень() < Уровень Тогда
								Прервать;
							КонецЕсли;
							Если Ит.Счет.Уровень() > Уровень Тогда
								Продолжить;
							КонецЕсли;
							СН = Ит.СНД(1)-Ит.СНК(1);
							Если СН > 0 Тогда
								ТЗ.СНД = ТЗ.СНД+СН;
							Иначе
								ТЗ.СНК = ТЗ.СНК-СН;
							КонецЕсли;

							СК = Ит.СКД(1)-Ит.СКК(1);
							Если СК > 0 Тогда
								ТЗ.СКД = ТЗ.СКД+СК;
							Иначе
								ТЗ.СКК = ТЗ.СКК-СК;
							КонецЕсли;

							СН = Ит.СНД(2)-Ит.СНК(2);
							Если СН > 0 Тогда
								ТЗВ.СНД = ТЗВ.СНД+СН;
							Иначе
								ТЗВ.СНК = ТЗВ.СНК-СН;
							КонецЕсли;

							СК = Ит.СКД(2)-Ит.СКК(2);
							Если СК > 0 Тогда
								ТЗВ.СКД = ТЗВ.СКД+СК;
							Иначе
								ТЗВ.СКК = ТЗВ.СКК-СК;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				Ит.ПолучитьВалюту(, "");
			КонецЕсли;
		Иначе
			// ...по субконто - выполним запрос...
			//
			Субконто = ФормРазворот.Список;
			Для А = 1 По ФормРазворот.Счет.КоличествоСубконто() Цикл
				Если Субконто.НайтиЗначение(ФормРазворот.Счет.ВидСубконто(А)) <> 0 Тогда
					РазвСальдоИт.ИспользоватьСубконто(ФормРазворот.Счет.ВидСубконто(А),,1);
				КонецЕсли;
			КонецЦикла;
			РазвСальдоИт.Опции(ДанныеПоЗабалансовым);
			Если (ДанныеПоВалютам = 1) И (ФормРазворот.Счет.Валютный = 1) Тогда
				РазвСальдоИт.ВыполнитьЗапрос(Дата1, Дата2, ФормРазворот.Счет,,,  1,, "СВ");
			Иначе
				РазвСальдоИт.ВыполнитьЗапрос(Дата1, Дата2, ФормРазворот.Счет,,,  1,, "С");
			КонецЕсли;
			РазвСальдоИт.Опции(ДанныеПоЗабалансовым);

			ТЗ.НоваяСтрока();
			ТЗ.СНД = РазвСальдоИт.СНДРС(1);
			ТЗ.СНК = РазвСальдоИт.СНКРС(1);
			ТЗ.СКД = РазвСальдоИт.СКДРС(1);
			ТЗ.СКК = РазвСальдоИт.СККРС(1);
			Если (ДанныеПоВалютам = 1) И (ФормРазворот.Счет.Валютный = 1) Тогда
				РазвСальдоИт.ВыбратьВалюты(,,, 1);
				Пока РазвСальдоИт.ПолучитьВалюту() = 1 Цикл
					СНД = РазвСальдоИт.СНДРС(1);
					СНК = РазвСальдоИт.СНКРС(1);
					СКД = РазвСальдоИт.СКДРС(1);
					СКК = РазвСальдоИт.СККРС(1);

					СНДВ = РазвСальдоИт.СНДРС(2);
					СНКВ = РазвСальдоИт.СНКРС(2);
					СКДВ = РазвСальдоИт.СКДРС(2);
					СККВ = РазвСальдоИт.СККРС(2);
					Если (СНД  = 0) И (СНК  = 0) И (СКД  = 0) И (СКК  = 0) И
					     (СНДВ = 0) И (СНКВ = 0) И (СКДВ = 0) И (СККВ = 0) Тогда
						Продолжить;
					КонецЕсли;

					ТЗ.НоваяСтрока();
					ТЗ.Валюта = РазвСальдоИт.Валюта;
					ТЗ.СНД = СНД;
					ТЗ.СНК = СНК;
					ТЗ.СКД = СКД;
					ТЗ.СКК = СКК;

					ТЗВ.НоваяСтрока();
					ТЗВ.Валюта = РазвСальдоИт.Валюта;
					ТЗВ.СНД = СНДВ;
					ТЗВ.СНК = СНКВ;
					ТЗВ.СКД = СКДВ;
					ТЗВ.СКК = СККВ;
				КонецЦикла;
			КонецЕсли;
			Если (ТЗ.КоличествоСтрок() = 1) И (ТЗ.СНД = 0) И (ТЗ.СНК = 0) И (ТЗ.СКД = 0) И (ТЗ.СКК = 0) Тогда
				ТЗ.УдалитьСтроку(1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//----------------------------------------------------------------------
Функция СальдоПолучить(ПарамСчет, СНД, СНК, СКД, СКК, ТипСуммы, ПарамВалюта)
	// ПАРАМЕТРЫ: ФормРазворот содержит правила разорота сальдо.
	//  ДЕЙСТВИЕ: возвращает разверутое сальдо по ПарамСчет в соотв. с зад. правилами.

	Перем ВремСНД, ВремСНК, ВремСКД, ВремСКК;

	Стр = 0;
	Правило = РазвСальдоЕстьПравило(ПарамСчет, Стр);
	Если Правило = 2 Тогда
		// Считаем сальдо в соотв. с правилами...
		// Развернутое сальдо...
		ТЗСальдо.ПолучитьСтрокуПоНомеру(Стр);
		Если ТипСуммы = 1 Тогда
			ТЗ = ТЗСальдо.СумСальдо;
		Иначе
			ТЗ = ТЗСальдо.ВалСальдо;
		КонецЕсли;
		Стр = 0;
		Если ТЗ.НайтиЗначение(ПарамВалюта, Стр, "Валюта") = 0 Тогда
			Возврат 0;
		КонецЕсли;
		ТЗ.ПолучитьСтрокуПоНомеру(Стр);
		СНД = ТЗ.СНД;
		СНК = ТЗ.СНК;
		СКД = ТЗ.СКД;
		СКК = ТЗ.СКК;
	ИначеЕсли Правило = 0 Тогда
		// Считаем простое сальдо, т. к. правил нет...
		Если ПарамСчет <> Ит.Счет Тогда
			Возврат 0;
		КонецЕсли;
		Если (ПарамВалюта <> ПолучитьПустоеЗначение()) И (ПарамВалюта <> Ит.Валюта) Тогда
			Возврат 0;
		КонецЕсли;

		СНД = Ит.СНД(ТипСуммы);
		СНК = Ит.СНК(ТипСуммы);
		СКД = Ит.СКД(ТипСуммы);
		СКК = Ит.СКК(ТипСуммы);
	Иначе // = 1
		// Есть правила в поддереве. Рекурсия...
		Уровень = ПарамСчет.Уровень()+1;
        Найдено = 0;

        // Запомнить позицию в объекте бух. итогов
		Счет = Ит.Счет;
		Валюта = Ит.Валюта;

		Если ПарамВалюта <> ПолучитьПустоеЗначение() Тогда
			Ит.ПолучитьСчет(, "");
			Если Ит.ПолучитьВалюту(, ПарамВалюта) = 1 Тогда
				Если Ит.ПолучитьСчет(, ПарамСчет) = 1 Тогда
					Ит.ПолучитьСчет();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Субсчета = СоздатьОбъект("Счет");
		Субсчета.ИспользоватьРодителя(ПарамСчет);
		Субсчета.ВыбратьСчета();
		Пока Субсчета.ПолучитьСчет() = 1 Цикл
			Если СальдоПолучить(Субсчета.ТекущийСчет(), ВремСНД, ВремСНК, ВремСКД, ВремСКК, ТипСуммы, ПарамВалюта) = 1 Тогда
				Найдено = 1;
				СНД = СНД + ВремСНД;
				СНК = СНК + ВремСНК;
				СКД = СКД + ВремСКД;
				СКК = СКК + ВремСКК;
			КонецЕсли;
			Если Ит.Счет = Субсчета.ТекущийСчет() Тогда
				Ит.ПолучитьСчет();
			КонецЕсли;
		КонецЦикла;

        // Восстановить позицию в объекте бух. итогов
		Если ПарамВалюта <> ПолучитьПустоеЗначение() Тогда
			Ит.ПолучитьВалюту(, "");
			Ит.ПолучитьСчет(, Счет);
			Ит.ПолучитьВалюту(, Валюта);
		Иначе
			Ит.ПолучитьСчет(, Счет);
		КонецЕсли;

		Возврат Найдено;
	КонецЕсли;

	Возврат 1;
КонецФункции

//-------------------------------------------------------------------
Функция ИтогиПолучить(ПарамСчет, СНД, СНК, СКД, СКК, ДО, КО, ТипСуммы, ПарамВалюта)
	СНД = 0;
	СНК = 0;
	СКД = 0;
	СКК = 0;
	ДО = 0;
	КО = 0;

	Найдено = 1;
	Если ПарамСчет <> Ит.Счет Тогда
		Найдено = 0;
	КонецЕсли;
	Если (Найдено = 1) И (ПарамВалюта <> ПолучитьПустоеЗначение()) Тогда
		Если ПарамВалюта <> Ит.Валюта Тогда
			Найдено = 0;
		КонецЕсли;
	КонецЕсли;

	Если Найдено = 1 Тогда
		ДО = Ит.ДО(ТипСуммы);
		КО = Ит.КО(ТипСуммы);
	КонецЕсли;

	Если СальдоПолучить(ПарамСчет, СНД, СНК, СКД, СКК, ТипСуммы, ПарамВалюта) = 1 Тогда
		Найдено = 1;
	КонецЕсли;

	Возврат Найдено;
КонецФункции                      

//----------------------------------------------------------------------
Процедура ПоВалюте(ИтСб)
	Если ДанныеПоВалютам = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИтСб.ВыбратьВалюты();
	Пока ИтСб.ПолучитьВалюту() = 1 Цикл
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_11");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура ПоСубконто(Счет)
	Если ДанныеПоСубсчетам = 0 Тогда
		Возврат;
	КонецЕсли;
	Стр = 0;
	Если тРазворотСубконто.НайтиЗначение(Счет, Стр, "Счет") = 0 Тогда
		Возврат;
	КонецЕсли;                               
	Субконто = СоздатьОбъект("СписокЗначений");
	тРазворотСубконто.ПолучитьСтрокуПоНомеру(Стр);
	ИтСб = СоздатьОбъект("БухгалтерскиеИтоги");
	ИтСб.ИспользоватьРазделительУчета(РазделительУчета);
	КолСубконто = 0;
	Для А=1 По тРазворотСубконто.Список.РазмерСписка() Цикл
		Если тРазворотСубконто.Список.Пометка(А) = 1 Тогда
			ИтСб.ИспользоватьСубконто(тРазворотСубконто.Список.ПолучитьЗначение(А),, 1);
			КолСубконто = КолСубконто+1;         
			Субконто.ДобавитьЗначение(тРазворотСубконто.Список.ПолучитьЗначение(А));
		КонецЕсли;
	КонецЦикла;
	
	Если КолСубконто = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИтСб.Опции(ДанныеПоЗабалансовым);
	ИтСб.ВыполнитьЗапрос(Дата1, Дата2, Счет);
	
	Расшифровка.Установить("Меню");
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	ИтСб.Опции(ДанныеПоЗабалансовым);

	Расшифровка.Установить("ВидСубконто1", Субконто.ПолучитьЗначение(1));
	Расшифровка.Установить("ОтборСубконто1", 2);
	ИтСб.ВыбратьСубконто(1);
	Пока ИтСб.ПолучитьСубконто(1) = 1 Цикл
		Расшифровка.Установить("Субконто1", ИтСб.Субконто(1));
		НомерСубконто = 1;
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_10");
		КонецЕсли;
		Если КолСубконто > 1 Тогда 
			Расшифровка.Установить("ВидСубконто2", Субконто.ПолучитьЗначение(2));
			Расшифровка.Установить("ОтборСубконто2", 2);
			ИтСб.ВыбратьСубконто(2);
			Пока ИтСб.ПолучитьСубконто(2) = 1 Цикл
				Расшифровка.Установить("Субконто2", ИтСб.Субконто(2));
				НомерСубконто = 2;
				Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
				Т.ВывестиСекцию("Секция_10");
				КонецЕсли;
				Если КолСубконто > 2 Тогда 
					Расшифровка.Установить("ВидСубконто3", Субконто.ПолучитьЗначение(3));
					Расшифровка.Установить("ОтборСубконто3", 2);
					ИтСб.ВыбратьСубконто(3);
					Пока ИтСб.ПолучитьСубконто(3) = 1 Цикл
						НомерСубконто = 3;  
						Расшифровка.Установить("Субконто3", ИтСб.Субконто(3));
						Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
							Т.ВывестиСекцию("Секция_10");
						КонецЕсли;
						ПоВалюте(ИтСб);
					КонецЦикла;
					Расшифровка.Установить("ВидСубконто3");
					Расшифровка.Установить("Субконто3");
					Расшифровка.Установить("ОтборСубконто3");
				Иначе
					ПоВалюте(ИтСб);
				КонецЕсли;
			КонецЦикла;
			Расшифровка.Установить("ВидСубконто2");       
			Расшифровка.Установить("Субконто2");
			Расшифровка.Установить("ОтборСубконто2");
		Иначе
			ПоВалюте(ИтСб);
		КонецЕсли;
	КонецЦикла;
	Расшифровка.Установить("Субконто1");
	Расшифровка.Установить("ВидСубконто1");
	Расшифровка.Установить("ОтборСубконто1");
	Расшифровка.Установить("ПоГруппам1");

    Расшифровка.Установить("Меню", Меню);
	Расшифровка.Установить("Отчет");
КонецПроцедуры

//----------------------------------------------------------------------
Функция ПроверкаПериода()
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		"Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//----------------------------------------------------------------------
Функция РасшифровкаДтКт(ДтКт)
	Расшифровка.Установить("ДтКт", ДтКт);
	Возврат Расшифровка;
КонецФункции

//----------------------------------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//----------------------------------------------------------------------
Функция ПечататьПосчету(Сч)
	Если (ДанныеПоСубсчетам = 0) И (Сч.Уровень() > 1) Тогда
		Возврат 0;
	КонецЕсли;            
	Сч1 = Сч.Родитель();
	Пока Сч1.Выбран() = 1 Цикл                                          
		Стр = 0;
	    Если тРазворотСубконто.НайтиЗначение(Сч1, Стр, "Счет") = 1 Тогда
			тРазворотСубконто.ПолучитьСтрокуПоНомеру(Стр);
			Если тРазворотСубконто.СубСчета = " " Тогда
				Возврат 0;
			КонецЕсли; 
		КонецЕсли;
		Сч1 = Сч1.Родитель();
	КонецЦикла;
	Возврат 1;
КонецФункции
		
//----------------------------------------------------------------------
Процедура Сформировать(ФлагЗакрытияФормы = 0)
	Перем ВремСНД, ВремСНК, ВремСКД, ВремСКК, ВремДО, ВремКО, МаскаСубконто[5];

	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	РазвСальдоИт = СоздатьОбъект("БухгалтерскиеИтоги");

	СНД = 0;
	СНК = 0;
	СКД = 0;
	СКК = 0;
	Если ВыбПланСчетов = 1 Тогда
		ПланСчетов = ВыбранныйПланСчетов();
	КонецЕсли;
	Ит.ИспользоватьПланСчетов(ПланСчетов);
	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Ит.ВключатьСубсчета(1);
	Ит.Опции(ДанныеПоЗабалансовым);
	Если Ит.ВыполнитьЗапрос(Дата1, Дата2) = 0 Тогда
		Возврат;
	КонецЕсли;
	Ит.Опции(ДанныеПоЗабалансовым);

	тРазворотСубконто = СоздатьОбъект("ТаблицаЗначений");
	тРазворотСубконто.НоваяКолонка("Счет",,,, "Счет", 16);
	тРазворотСубконто.НоваяКолонка("Субсчета",,,, "Субсчета", 9);
	тРазворотСубконто.НоваяКолонка("Субконто",,,, "Субконто", 9);
	тРазворотСубконто.НоваяКолонка("Список");
	тРазворотСубконто.ВидимостьКолонки("Список", 0);
	ВосстановитьЗначение("ОСВРазворотСубконто", тРазворотСубконто);

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
		Т = СоздатьОбъект("Таблица");
	Иначе
		Т.Очистить();
	КонецЕсли;
	Если Константа.ЖирныйШрифтВОтчетах=Да Тогда
	 	Т.ИсходнаяТаблица("Таблица");
	Иначе
	 	Т.ИсходнаяТаблица("Таблица1");
	КонецЕсли;
                  
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("КарточкаСчета", "Карточка счета");
	Меню.ДобавитьЗначение("ОборотноСальдоваяВедомостьПоСчету", "Ведомость по счету");
	Меню.ДобавитьЗначение("АнализСчета", "Анализ счета");
	Меню.ДобавитьЗначение("ОтчетПоПроводкам", "Отчет по проводкам");
	Меню.ДобавитьЗначение("ГлавнаяКнига", "Обороты счета (Гл. книга)");
	Меню.ДобавитьЗначение("ЖурналОрдер", "Журнал-ордер (ведомость) по счету");

	Если РазвернутоеСальдо = 1 Тогда
		РазвСальдоИт.ИспользоватьПланСчетов(ПланСчетов);
		РазвСальдоИт.ИспользоватьРазделительУчета(РазделительУчета);
		РассчитатьСальдоПоПравилам(ПланСчетов);
		Счет = СоздатьОбъект("Счет");

		Расшифровка.Установить("Дата1", Дата1);
		Расшифровка.Установить("Дата2", Дата2);
		Расшифровка.Установить("РазделительУчета", РазделительУчета);
		Расшифровка.Установить("ПланСчетов", ПланСчетов);
		Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомость");
		Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
		Расшифровка.Установить("ДанныеПоВалютам", ДанныеПоВалютам);
		Расшифровка.Установить("РазвернутоеСальдо", РазвернутоеСальдо);
		Расшифровка.Установить("ФормРазворот", ФормРазворот);
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_6");
		КонецЕсли;
	    Расшифровка.Установить("Отчет");
	    Расшифровка.Установить("ДанныеПоСубсчетам");
	    Расшифровка.Установить("ДанныеПоВалютам");
		Расшифровка.Установить("РазвернутоеСальдо");
		Расшифровка.Установить("ФормРазворот");
	    Расшифровка.Установить("Обновить");
		
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_1");
		КонецЕсли;
	    Расшифровка.Установить("Меню", Меню);

		Счет.ВыбратьСчета();
		Ит.ВыбратьСчета();
		Ит.ПолучитьСчет();
		Пока Счет.ПолучитьСчет() = 1 Цикл
			Сч = Счет.ТекущийСчет();
			Если ПечататьПоСчету(Сч) = 0 Тогда
				Если Ит.Счет = Сч Тогда
					Ит.ПолучитьСчет();
				КонецЕсли;
				Продолжить;
			КонецЕсли;

			Расшифровка.Установить("Счет", Сч);
			Если ИтогиПолучить(Сч, ВремСНД, ВремСНК, ВремСКД, ВремСКК, ВремДО, ВремКО, 1, ПолучитьПустоеЗначение()) = 1 Тогда
				Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
					Т.ВывестиСекцию("Секция_7");
				КонецЕсли;
				Если (Сч.Уровень() = 1) И (Сч.Забалансовый = 0) Тогда
					СНД = СНД + ВремСНД;
					СНК = СНК + ВремСНК;
					СКД = СКД + ВремСКД;
					СКК = СКК + ВремСКК;
				КонецЕсли;
				Если (Сч.Валютный = 1) И (ДанныеПоВалютам = 1) Тогда
					Валюта = СоздатьОбъект("Справочник.Валюты");
					ТекВалюта = Валюта.ТекущийЭлемент();
					Валюта.ВыбратьЭлементы();
					Ит.ВыбратьВалюты();
					Ит.ПолучитьВалюту();
					Пока 1=1 Цикл
					    Расшифровка.Установить("Валюта", ТекВалюта);
					    Расшифровка.Установить("ПоВалюте", 1);
						Если ИтогиПолучить(Сч, ВремСНД, ВремСНК, ВремСКД, ВремСКК, ВремДО, ВремКО, 1, ТекВалюта) = 1 Тогда
							Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
								Т.ВывестиСекцию("Секция_8");
							КонецЕсли;
						КонецЕсли;
						Если ИтогиПолучить(Сч, ВремСНД, ВремСНК, ВремСКД, ВремСКК, ВремДО, ВремКО, 2, ТекВалюта) = 1 Тогда
							Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
								Т.ВывестиСекцию("Секция_9");
							КонецЕсли;
						КонецЕсли;
						Если Ит.Валюта = ТекВалюта Тогда
							Ит.ПолучитьВалюту();
						КонецЕсли;
						Если Валюта.ПолучитьЭлемент() = 0 Тогда
							Прервать;
						КонецЕсли;
						ТекВалюта = Валюта.ТекущийЭлемент();
					КонецЦикла;
				    Расшифровка.Установить("Валюта");
				    Расшифровка.Установить("ПоВалюте");
				КонецЕсли;
			КонецЕсли;
			ПоСубконто(Сч);
			Если Ит.Счет = Сч Тогда
				Ит.ПолучитьСчет();
			КонецЕсли;
		КонецЦикла;
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_5");
		КонецЕсли;
		РазвСальдоИт = 0;
		ТЗСальдо = 0;
	Иначе
		Расшифровка.Установить("Дата1", Дата1);
		Расшифровка.Установить("Дата2", Дата2);
		Расшифровка.Установить("РазделительУчета", РазделительУчета);
		Расшифровка.Установить("ПланСчетов", ПланСчетов);
		Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомость");
		Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
		Расшифровка.Установить("ДанныеПоВалютам", ДанныеПоВалютам);
		Расшифровка.Установить("РазвернутоеСальдо", РазвернутоеСальдо);
		Расшифровка.Установить("ФормРазворот", ФормРазворот);
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			Т.ВывестиСекцию("Секция_6");
		КонецЕсли;
	    Расшифровка.Установить("Отчет");
	    Расшифровка.Установить("ДанныеПоСубсчетам");
	    Расшифровка.Установить("ДанныеПоВалютам");
		Расшифровка.Установить("РазвернутоеСальдо");
		Расшифровка.Установить("ФормРазворот");
	    Расшифровка.Установить("Обновить");
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
		    Т.ВывестиСекцию("Секция_1");
		КонецЕсли;
	    Расшифровка.Установить("Меню", Меню);
	    Ит.ВыбратьСчета();
	    Пока Ит.ПолучитьСчет() = 1 Цикл
			Если ПечататьПоСчету(Ит.Счет) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
		    Расшифровка.Установить("Счет", Ит.Счет);
			
			Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
	        	Т.ВывестиСекцию("Секция_2");
			КонецЕсли;

	        Если (Ит.Счет.Уровень() = 1) и (Ит.Счет.Забалансовый = 0) Тогда
			    СНД = СНД + Ит.СНД();
			    СНК = СНК + Ит.СНК();
			    СКД = СКД + Ит.СКД();
			    СКК = СКК + Ит.СКК();
			КонецЕсли;

			Если ДанныеПоВалютам = 1 Тогда
		        Ит.ВыбратьВалюты();
		        Пока Ит.ПолучитьВалюту() = 1 Цикл
				    Расшифровка.Установить("Валюта", Ит.Валюта);
				    Расшифровка.Установить("ПоВалюте", 1);
					Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			            Т.ВывестиСекцию("Секция_3");
					КонецЕсли;
		            Если Ит.Валюта.Выбран() = 1 Тогда
						Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
			            	Т.ВывестиСекцию("Секция_4");
						КонецЕсли;
			        КонецЕсли;
		        КонецЦикла;
			    Расшифровка.Установить("Валюта");
			    Расшифровка.Установить("ПоВалюте");
		    КонецЕсли;
			ПоСубконто(Ит.Счет);
	    КонецЦикла;
		Если Не ((Ит.Счет.Забалансовый=1) И (ДанныеПоЗабалансовым=0)) Тогда
		    Т.ВывестиСекцию("Секция_5");
		КонецЕсли;
	КонецЕсли;
	Т.ВывестиСекцию("Подпись");
    Ит = 0;
    Т.ТолькоПросмотр(1);
    Т.Опции(0, 0, 5, 2,,"ОСВР");
	Т.ОбластьПечати(2);
	Т.ПараметрыСтраницы(1,,,,,,,,, 1);
    Т.Показать("Оборотно-Сальдовая ведомость ("+ПериодСтр(Дата1, Дата2)+?(ТипЗначения(РазделительУчета)=0, "", " "+РазделительУчета)+")", "");

	СохранитьЗначение("ОСВФормРазворот", ФормРазворот);
	СохранитьЗначение("ОСВДанныеПоСубсчетам", ДанныеПоСубсчетам);
	СохранитьЗначение("ОСВДанныеПоВалютам", ДанныеПоВалютам);
	СохранитьЗначение("ОСВРазвернутоеСальдо", РазвернутоеСальдо);
	СохранитьЗначение("ОСВДанныеПоЗабалансовым", ДанныеПоЗабалансовым);

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ОткрытьНастройку()              
	ОткрытьФормуМодально("Обработка.РазворотСубконто", 1);
КонецПроцедуры

//----------------------------------------------------------------------
Процедура ПриОткрытии(ИспНастройки)

	// Установим закладки...
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Параметры", "Параметры");
	Форма.Закладки.ДобавитьЗначение("Сальдо", "Развернутое сальдо");
	Форма.ИспользоватьСлой("Основной,Параметры", 2);

	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если ИспНастройки = 0 Тогда
		ДанныеПоСубсчетам = ВосстановитьЗначение("ОСВДанныеПоСубсчетам");
		Если ТипЗначения(ДанныеПоСубсчетам) = 0 Тогда
			ДанныеПоСубсчетам = 1;
		КонецЕсли;
			
		ДанныеПоВалютам = ВосстановитьЗначение("ОСВДанныеПоВалютам");
		РазвернутоеСальдо = ВосстановитьЗначение("ОСВРазвернутоеСальдо");
		ДанныеПоЗабалансовым=ВосстановитьЗначение("ОСВДанныеПоЗабалансовым");

		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		
	КонецЕсли;

	// Инициализируем таблицу значений...
	ФормРазворот.Очистить();
	ФормРазворот.НоваяКолонка("Счет",,,, "Счет", 16);
	ФормРазворот.НоваяКолонка("Субсчета",,,, "Субсчета", 9);
	ФормРазворот.НоваяКолонка("Субконто",,,, "Субконто", 9);
	ФормРазворот.НоваяКолонка("Список");
	ВосстановитьЗначение("ОСВФормРазворот", ФормРазворот);
	ФормРазворот.ВидимостьКолонки("Список", 0);
	
	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВыбПланСчетов = 0;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
			ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
			ДанныеПоВалютам = глРасшифровка.Получить("ДанныеПоВалютам");
			РазвернутоеСальдо = глРасшифровка.Получить("РазвернутоеСальдо");
			глРасшифровка.Получить("ФормРазворот").Выгрузить(ФормРазворот);
			Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = БухИтоги.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РазделительУчета = БухИтоги.ИспользоватьРазделительУчета();
	КонецЕсли;

КонецПроцедуры

//----------------------------------------------------------------------
Процедура ПриЗакрытии()
	СохранитьЗначение("ОСВФормРазворот", ФормРазворот);
КонецПроцедуры


//----------------------------------------------------------------------
Процедура ВводНового() //Считывание существующей настройки
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
КонецПроцедуры

//----------------------------------------------------------------------
Процедура РазворотИзменить(Колонка)
	Перем Счет, Вариант;

	Если ФормРазворот.КоличествоСтрок()=0 Тогда
		Возврат;
	КонецЕсли;

	Если Колонка = "Счет" Тогда
		Счет = ФормРазворот.Счет;
		Если ВвестиЗначение (Счет, "Введите счет", "Счет",,) = 0 Тогда
			Если Счет = "<Счет>" Тогда
				ФормРазворот.УдалитьСтроку(ФормРазворот.ТекущаяСтрока());
			КонецЕсли;
			Возврат;
		КонецЕсли;
		ФормРазворот.Счет = Счет;

		// Заполним список субконто для счета...
		Субконто = СоздатьОбъект("СписокЗначений");
		Для Индекс = 1 по 3 Цикл
			ВрСуб = ФормРазворот.Счет.ВидСубконто(Индекс);
			Если ПустоеЗначение(ВрСуб) = 0 Тогда
				Субконто.ДобавитьЗначение(ФормРазворот.Счет.ВидСубконто(Индекс));
			КонецЕсли;
		КонецЦикла;

		ФормРазворот.Список = Субконто;

		Если Счет.ЭтоГруппа() = 0 Тогда
			ФормРазворот.Субсчета = "-";
		Иначе
			ФормРазворот.Субсчета = " ";
		КонецЕсли;

		Если Субконто.РазмерСписка() = 0 Тогда
			ФормРазворот.Субконто = "-";
		Иначе
			ФормРазворот.Субконто = " ";
		КонецЕсли;

	ИначеЕсли (Колонка = "Субсчета") и (ФормРазворот.Субсчета <> "-") Тогда
		ФормРазворот.Субсчета = "Х";
		Если ФормРазворот.Субконто <> "-" Тогда
			ФормРазворот.Субконто = " ";
		КонецЕсли;

	ИначеЕсли (Колонка = "Субконто") и (ФормРазворот.Субконто <> "-") Тогда
		ФормРазворот.Субконто = "Х";
		Если ФормРазворот.Субсчета <> "-" Тогда
			ФормРазворот.Субсчета = " ";
		КонецЕсли;
		// Выберем субконто...
		СЗ = СоздатьОбъект("СписокЗначений");
		Для А=1 По ФормРазворот.Счет.КоличествоСубконто() Цикл
			СЗ.ДобавитьЗначение(ФормРазворот.Счет.ВидСубконто(А));
			Если ФормРазворот.Список.НайтиЗначение(ФормРазворот.Счет.ВидСубконто(А)) <> 0 Тогда
				СЗ.Пометка(А, 1);
			КонецЕсли;
		КонецЦикла;
		СЗ.ОтметитьЗначения(,"Разворачивать сальдо по:");
		ФормРазворот.Список.УдалитьВсе();
		Для А=1 По СЗ.РазмерСписка() Цикл
			Если СЗ.Пометка(А) = 1 Тогда
				ФормРазворот.Список.ДобавитьЗначение(СЗ.ПолучитьЗначение(А));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура РазворотДобавить()
	ФормРазворот.НоваяСтрока();
	ФормРазворот.Счет = "<Счет>";
	ФормРазворот.ТекущаяКолонка(1);
	ФормРазворот.ТекущаяСтрока(ФормРазворот.КоличествоСтрок());

	РазворотИзменить("Счет");
КонецПроцедуры

//----------------------------------------------------------------------
Процедура РазворотУдалить()
	Если ФормРазворот.КоличествоСтрок() > 0 Тогда
		ФормРазворот.УдалитьСтроку(ФормРазворот.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура ПриВыбореЗакладки(Номер, Значение)
	Если Значение = "Параметры" Тогда
		Форма.ИспользоватьСлой("Основной, Параметры", 2);
	ИначеЕсли Значение = "Сальдо" Тогда
		Форма.ИспользоватьСлой("Основной, Разворот", 2);
	КонецЕсли;
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		Форма.Текст.Видимость(0);
    Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;
КонецПроцедуры