Перем СписокДействий;

Процедура ВводНового()
	Автор=глПользователь;
КонецПроцедуры

Процедура ВводНаОсновании(ДокОс)
	Автор=глПользователь;
	ДокументПоступления=ДокОс;
КонецПроцедуры	

Процедура ОпрВидаСубконто()
	НазначитьВид(Субконто1,КоррСчет.ВидСубконто(1));
	Форма.Субконто1.НеИзменятьВид(1);
	Если КоррСчет.КоличествоСубконто()=2 Тогда
		НазначитьВид(Субконто2,КоррСчет.ВидСубконто(2));
		Форма.Субконто2.НеИзменятьВид(1);
		Форма.Субконто2.Доступность(1);
	Иначе
		Форма.Субконто2.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Функция ПоказатьКурс()
	Если Валюта.Выбран()=1 Тогда
		Возврат Валюта.ТекКурс.Получить(ДатаДок);
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

Процедура ДостСумВал()
	Если Валюта=Леи Тогда
		Форма.СумВал.Доступность(0);
	Иначе
		Форма.СумВал.Доступность(1);
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСуммы()
	ТекКурс=Валюта.ТекКурс.Получить(ДатаДок);
	Кратность=Валюта.Кратность.Получить(ДатаДок);
	СумЛей=СумВал*ТекКурс/Кратность;
КонецПроцедуры

Процедура ПриРедактированииНовойСтроки()
	КоррСчет=СчетПоКоду("539.3");Валюта=Леи;
КонецПроцедуры

Функция Итоги()
	ВалСумма=Формат(Итог("СумВал"),"Ч010.2");
	СтрВалСум=?(Итог("СумВал")>0,"Валютная сумма "+СокрЛ(ВалСумма)+"    ","");
	ЛейСумма=Формат(Итог("СумЛей"),"Ч010.2");
	СтрЛейСум=?(Итог("СумЛей")>0,"Леевая сумма "+СокрЛ(ЛейСумма)+"    ","");
	ИтНДС=Формат(Итог("НДС"),"Ч010.2");
	СтрНДС=?(Итог("НДС")>0,"НДС "+СокрЛ(ИтНДС)+"    ","");
	Возврат СтрВалСум+СтрЛейСум+СтрНДС;
КонецФункции

Процедура УстДокПост()
	ОткрытьПодбор("Журнал.ПриходныеНакладные",,,0);
КонецПроцедуры

Процедура ОбработкаПодбора(Выб)
	Если Выб.Выбран()=1 Тогда
		ДокументПоступления=Выб;
	КонецЕсли;
КонецПроцедуры

Процедура Печать()
	Если КоличествоСтрок()=0 Тогда
		Предупреждение("Документ пуст!");
		Возврат;
	КонецЕсли;
    
	// Добавлено для расчета по импортным группам
	Табл=СоздатьОбъект("ТаблицаЗначений");
	Табл.НоваяКолонка("СумЛей","Число");
	ДокументПоступления.ВыгрузитьТабличнуюЧасть(Табл,);
	Табл.Свернуть("Импорт","СумЛей");
	//
	
	СпсДолей=СоздатьОбъект("СписокЗначений");
	СумЛейТМЦБезНДС=ДокументПоступления.Итог("СумЛей")-ДокументПоступления.Итог("НДС");
	ДокументПоступления.ВыбратьСтроки();
	Пока ДокументПоступления.ПолучитьСтроку()=1 Цикл
		ТекДоля=(ДокументПоступления.СумЛей-ДокументПоступления.НДС)/СумЛейТМЦБезНДС;
		СпсДолей.ДобавитьЗначение(ТекДоля);
	КонецЦикла;

	СпсРасходов=СоздатьОбъект("СписокЗначений");
	Стлб=0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		СумЛейБезНДС=СумЛей-НДС;
		Стлб=Стлб+1;Стр=0;
		ДокументПоступления.ВыбратьСтроки();
		Пока ДокументПоступления.ПолучитьСтроку()=1 Цикл
			Стр=Стр+1;
			ТекКод=Число(Строка(Стр)+Строка(Стлб));
			ТекЗнач=Формат(СумЛейБезНДС*СпсДолей.ПолучитьЗначение(Стр),"Ч10.2");
			СпсРасходов.ДобавитьЗначение(ТекКод,ТекЗнач);
		КонецЦикла;
	КонецЦикла;
	СпсДолей=0;

	Пропись(КаталогИБ()+"Spl\"+Леи.Spl);
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Расход");

	Заголовок=СтрДок(ТекущийДокумент());
	Если ДокументПоступления.Вид()="ПрихНалоговаяНакладная" Тогда
		Заг="Распределение дополнительных расходов к документу "+СокрЛП(ДокументПоступления.СерияСФ)+" № "+СокрЛП(ДокументПоступления.НомерСФ)+" от "+СокрЛП(ДокументПоступления.ДатаДок);
	Иначе
		Заг="Распределение дополнительных расходов к документу "+СокрЛП(ДокументПоступления.СерияТТН)+" № "+СокрЛП(ДокументПоступления.НомерТТН)+" от "+СокрЛП(ДокументПоступления.ДатаДок);
	КонецЕсли;
	Таб.ВывестиСекцию("Шапка|ИсхВерт");
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		ЗагРасход=НаименованиеЗатрат;
		Таб.ПрисоединитьСекцию("Шапка|Расход");
	КонецЦикла;
	Таб.ПрисоединитьСекцию("Шапка|Сумма");
	Таб.ПрисоединитьСекцию("Шапка|Цена");

	Стр=0;Стлб=0;ИтСумма=0;
	ДокументПоступления.ВыбратьСтроки();
	Пока ДокументПоступления.ПолучитьСтроку()=1 Цикл
		Стр=Стр+1;
		Номер=ДокументПоступления.НомерСтроки;
		ТМЦ=ДокументПоступления.ТМЦ1;
		Колво=ДокументПоступления.Количество;
		СуммаБезНДС=ДокументПоступления.СумЛей-ДокументПоступления.НДС;
		Таб.ВывестиСекцию("ТМЦ|ИсхВерт");
		Стлб=0;Сумма=СуммаБезНДС;
		Для Ном=1 По КоличествоСтрок() Цикл
			ПолучитьСтрокуПоНомеру(Ном);
			Стлб=Стлб+1;Расход=0;
			ТекКод=Число(Строка(Стр)+Строка(Стлб));
			Если Импорт.Выбран()=0 Тогда
				СпсРасходов.ПолучитьЗначение(СпсРасходов.НайтиЗначение(ТекКод),Расход);
			ИначеЕсли ДокументПоступления.Импорт=Импорт Тогда
				НС=0;
				Если Табл.НайтиЗначение(ДокументПоступления.Импорт,НС,"Импорт")=1 Тогда
					Табл.ТекущаяСтрока(НС);
					Расход=Окр(СумЛей*ДокументПоступления.СумЛей/Табл.ПолучитьЗначение(НС,"СумЛей"),2);
				Иначе
					Расход=0;
				КонецЕсли;
			Иначе
				Расход=0;
			КонецЕсли;
			Таб.ПрисоединитьСекцию("ТМЦ|Расход");
			Сумма=Сумма+Число(Расход);
		КонецЦикла;
		Цена=Сумма/Колво;ИтСумма=ИтСумма+Сумма;
		Сумма=Формат(Сумма,"Ч10.2");Цена=Формат(Цена,"Ч10.2");
		Таб.ПрисоединитьСекцию("ТМЦ|Сумма");
		Таб.ПрисоединитьСекцию("ТМЦ|Цена");
	КонецЦикла;

	ИтСуммаБезНДС=ДокументПоступления.Итог("СумЛей")-ДокументПоступления.Итог("НДС");
	Таб.ВывестиСекцию("Итог|ИсхВерт");
	Для Ном=1 По КоличествоСтрок() Цикл
		ПолучитьСтрокуПоНомеру(Ном);
		ИтРасход=СумЛей-НДС;
		Таб.ПрисоединитьСекцию("Итог|Расход");
	КонецЦикла;
	ИтСумма=Формат(ИтСумма,"Ч10.2");
	Таб.ПрисоединитьСекцию("Итог|Сумма");
	Таб.Опции(0,0,4,0);
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(2,,,10,5,5,10,,,1);
	Таб.ПовторятьПриПечатиСтроки(4,4);
	Таб.Показать("Дополнительные расходы","");
КонецПроцедуры

Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
	
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если Форма.Параметр="Расход" Тогда
			Печать();
			СтатусВозврата(0);
		Иначе
			глПроверкаРазрешенияРедактирования(Контекст);
		КонецЕсли;
	Иначе
		глПроверкаРазрешенияРедактирования(Контекст);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	ЖурналДокумента=глВыбратьЖурнал(ТекущийДокумент());
КонецПроцедуры

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Открыть  в журнале");