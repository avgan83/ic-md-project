Перем ВыбВидСубконто1, ВыбВидСубконто2, ВыбВидСубконто3;
Перем ТекстСубконто1, ТекстСубконто2, ТекстСубконто3;
Перем Периодичность;
Перем Таб;
Перем Обновить;
Перем Расшифровка;
Перем ПредставлениеРУ;
Перем СекцияКол;
Перем ПутьКОтчету;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ВыбНачПериода > ВыбКонПериода Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если ВыбКонПериода > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Процедура УстановитьЗаголовокРамки(ВыбВидСубконто, ВыбУчитывать, ВыбРежим, ВыбСубконто, ТекстСубконто)
	Если ВыбУчитывать = 1 Тогда
		Если ПустоеЗначение(ВыбСубконто) = 1 Тогда
			Если ВыбРежим = 1 Тогда
				Стр = ": показать по всем субконто";
			Иначе
				Стр = ": показать по пустому субконто";
			КонецЕсли;
		Иначе                                                           
			Стр = ": показать по субконто "+ВыбСубконто;
			Если ТипЗначенияСтр(ВыбСубконто) = "Справочник" Тогда
				Если ВыбСубконто.ЭтоГруппа() = 1 Тогда
					Если ВыбРежим = 1 Тогда
						Стр = ": показать все субконто группы "+ВыбСубконто;
					Иначе
						Стр = ": показать в целом по группе "+ВыбСубконто;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВыбВидСубконто.Выбран() = 1 Тогда
		Стр = ": неучитывать";
	Иначе     
		Стр = "";
	КонецЕсли;
	ТекстСубконто = Строка(ВыбВидСубконто)+Стр;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореУчитывать1()
	Форма.ВыбРежим1.Доступность(ВыбУчитывать1);
	Форма.Отобрать1.Доступность(ВыбУчитывать1);
	Форма.ВыбСубконто1.Доступность(ВыбУчитывать1);
	Форма.ВыбПоГруппам1.Доступность(ВыбУчитывать1);
	Форма.КнОчистить1.Доступность(ВыбУчитывать1);
	УстановитьЗаголовокРамки(ВыбВидСубконто1, ВыбУчитывать1, ВыбРежим1, ВыбСубконто1, ТекстСубконто1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореРазвернуть1()
	УстановитьЗаголовокРамки(ВыбВидСубконто1, ВыбУчитывать1, ВыбРежим1, ВыбСубконто1, ТекстСубконто1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореОтобрать1()
	УстановитьЗаголовокРамки(ВыбВидСубконто1, ВыбУчитывать1, ВыбРежим1, ВыбСубконто1, ТекстСубконто1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто1()
	УстановитьЗаголовокРамки(ВыбВидСубконто1, ВыбУчитывать1, ВыбРежим1, ВыбСубконто1, ТекстСубконто1);
КонецПроцедуры             

//-----------------------------------------------
Процедура ПриОчиститьСубконто1()
	ВыбСубконто1 = "";
	УстановитьЗаголовокРамки(ВыбВидСубконто1, ВыбУчитывать1, ВыбРежим1, ВыбСубконто1, ТекстСубконто1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореУчитывать2()
	Форма.ВыбРежим2.Доступность(ВыбУчитывать2);
	Форма.Отобрать2.Доступность(ВыбУчитывать2);
	Форма.ВыбСубконто2.Доступность(ВыбУчитывать2);
	Форма.ВыбПоГруппам2.Доступность(ВыбУчитывать2);
	Форма.КнОчистить2.Доступность(ВыбУчитывать2);
	УстановитьЗаголовокРамки(ВыбВидСубконто2, ВыбУчитывать2, ВыбРежим2, ВыбСубконто2, ТекстСубконто2);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореРазвернуть2()
	УстановитьЗаголовокРамки(ВыбВидСубконто2, ВыбУчитывать2, ВыбРежим2, ВыбСубконто2, ТекстСубконто2);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореОтобрать2()
	УстановитьЗаголовокРамки(ВыбВидСубконто2, ВыбУчитывать2, ВыбРежим2, ВыбСубконто2, ТекстСубконто2);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто2()
	УстановитьЗаголовокРамки(ВыбВидСубконто2, ВыбУчитывать2, ВыбРежим2, ВыбСубконто2, ТекстСубконто2);
КонецПроцедуры             

//-----------------------------------------------
Процедура ПриОчиститьСубконто2()
	ВыбСубконто2 = "";
	УстановитьЗаголовокРамки(ВыбВидСубконто2, ВыбУчитывать2, ВыбРежим2, ВыбСубконто2, ТекстСубконто2);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореУчитывать3()
	Форма.ВыбРежим3.Доступность(ВыбУчитывать3);
	Форма.Отобрать3.Доступность(ВыбУчитывать3);
	Форма.ВыбСубконто3.Доступность(ВыбУчитывать3);
	Форма.ВыбПоГруппам3.Доступность(ВыбУчитывать3);
	Форма.КнОчистить3.Доступность(ВыбУчитывать3);
	УстановитьЗаголовокРамки(ВыбВидСубконто3, ВыбУчитывать3, ВыбРежим3, ВыбСубконто3, ТекстСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореРазвернуть3()
	УстановитьЗаголовокРамки(ВыбВидСубконто3, ВыбУчитывать3, ВыбРежим3, ВыбСубконто3, ТекстСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореОтобрать3()
	УстановитьЗаголовокРамки(ВыбВидСубконто3, ВыбУчитывать3, ВыбРежим3, ВыбСубконто3, ТекстСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто3()
	УстановитьЗаголовокРамки(ВыбВидСубконто3, ВыбУчитывать3, ВыбРежим3, ВыбСубконто3, ТекстСубконто3);
КонецПроцедуры             

//-----------------------------------------------
Процедура ПриОчиститьСубконто3()
	ВыбСубконто3 = "";
	УстановитьЗаголовокРамки(ВыбВидСубконто3, ВыбУчитывать3, ВыбРежим3, ВыбСубконто3, ТекстСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	ВыбВидСубконто1 = ВыбСчет.ВидСубконто(1);
	Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);       
	ВыбУчитывать1 = ВыбВидСубконто1.Выбран();
	Форма.ВыбУчитывать1.Доступность(ВыбУчитывать1);
	ПриВыбореУчитывать1();        
	
	ВыбВидСубконто2 = ВыбСчет.ВидСубконто(2);
	Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);       
	ВыбУчитывать2 = ВыбВидСубконто2.Выбран();
	Форма.ВыбУчитывать2.Доступность(ВыбУчитывать2);
	ПриВыбореУчитывать2();        

	ВыбВидСубконто3 = ВыбСчет.ВидСубконто(3);
	Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);       
	ВыбУчитывать3 = 0;
	
	Если (ВыбВидСубконто1.Выбран() = 1) И (ВыбВидСубконто2.Выбран() = 1) Тогда
		Форма.КнВверх1.Доступность(1);
		Форма.КнВверх2.Доступность(1);
		Форма.КнВниз1.Доступность(1);
		Форма.КнВниз2.Доступность(1);
	Иначе
		Форма.КнВверх1.Доступность(0);
		Форма.КнВверх2.Доступность(0);
		Форма.КнВниз1.Доступность(0);
		Форма.КнВниз2.Доступность(0);
	КонецЕсли;
	
	Если ВыбВидСубконто3.Выбран() = 1 Тогда
		Форма.КнВверх3.Доступность(1);
		Форма.КнВниз3.Доступность(1);
	Иначе
		Форма.КнВверх3.Доступность(0);
		Форма.КнВниз3.Доступность(0);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура Поменять(П1, П2)
	П = П1;
	П1 = П2;
	П2 = П;
КонецПроцедуры

//-----------------------------------------------
Процедура Поменять12()
	Поменять(ВыбУчитывать1, ВыбУчитывать2);
	Поменять(ВыбВидСубконто1, ВыбВидСубконто2);
	Субконто = ВыбСубконто1;
	Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);       
	ВыбСубконто1 = ВыбСубконто2;
	Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);       
	ВыбСубконто2 = Субконто;
	Поменять(ВыбРежим1, ВыбРежим2);
	Поменять(ВыбПоГруппам1, ВыбПоГруппам2);
	ПриВыбореУчитывать1();
	ПриВыбореУчитывать2();
КонецПроцедуры

//-----------------------------------------------
Процедура Поменять23()
	Поменять(ВыбУчитывать2, ВыбУчитывать3);
	Поменять(ВыбВидСубконто2, ВыбВидСубконто3);
	Поменять(ВыбСубконто2, ВыбСубконто3);
	Поменять(ВыбРежим2, ВыбРежим3);
	Поменять(ВыбПоГруппам2, ВыбПоГруппам3);
	ПриВыбореУчитывать2();
	ПриВыбореУчитывать3();
КонецПроцедуры

//-----------------------------------------------
Процедура Поменять13()
	Поменять(ВыбУчитывать1, ВыбУчитывать3);
	Поменять(ВыбВидСубконто1, ВыбВидСубконто3);
	Поменять(ВыбСубконто1, ВыбСубконто3);
	Поменять(ВыбРежим1, ВыбРежим3);
	Поменять(ВыбПоГруппам1, ВыбПоГруппам3);
	ПриВыбореУчитывать1();
	ПриВыбореУчитывать3();
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВверх1()
	Если ВыбВидСубконто3.Выбран() = 1 Тогда
		Поменять13();
	ИначеЕсли ВыбВидСубконто2.Выбран() = 1 Тогда
		Поменять12();
	КонецЕсли;
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВверх2()
	Поменять12();
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВверх3()
	Поменять23();
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВниз1()
	Если ВыбВидСубконто2.Выбран() = 1 Тогда
		Поменять12();
	КонецЕсли;
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВниз2()
	Если ВыбВидСубконто3.Выбран() = 1 Тогда
		Поменять23();
	Иначе               
		Поменять12();
	КонецЕсли;
КонецПроцедуры      

//-----------------------------------------------
Процедура ПриВыбореВниз3()
	Поменять13();
КонецПроцедуры      

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаДтКт(ДтКт)
	Расшифровка.Установить("ДтКт", ДтКт);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура ПоПериодам(Ит)
	Ит.ВыбратьПериоды();
	Пока Ит.ПолучитьПериод() = 1 Цикл
		Расшифровка.Установить("Дата1", Ит.НачДата);
		Расшифровка.Установить("Дата2", Ит.КонДата);
		Если Периодичность = "День" Тогда
			Период = Ит.НачДата;
		Иначе
			Период = ПериодСтр(Ит.НачДата, Ит.КонДата);
		КонецЕсли;
		Таб.ВывестиСекцию("Период"+СекцияКол);
		Ит.ВыбратьКорСчета();
		Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
			Расшифровка.Установить("КорСчет", Ит.КорСчет);
			Таб.ВывестиСекцию("КорСчет"+СекцияКол);
		КонецЦикла;
		Расшифровка.Установить("КорСчет");
		Расшифровка.Установить("ДтКт");
		Расшифровка.Установить("Отчет", "КарточкаСчета");
		Таб.ВывестиСекцию("ПдвПериод"+СекцияКол);
	КонецЦикла;
	Расшифровка.Установить("Дата1", ВыбНачПериода);
	Расшифровка.Установить("Дата2", ВыбКонПериода);
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать(Ручн = 0, ФлагЗакрытияФормы = 0)
	Если ВыбСчет.Выбран() = 0 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;

	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;           

	СохранитьЗначение("ОтчРабСчет", ВыбСчет);
	    
	СекцияКол = "";
	Если ВыбСчет.Количественный = 1 Тогда
		СекцияКол = "Кол";
	КонецЕсли;

	ВидСубконто1 = ВыбВидСубконто1;
	Субконто1 = ВыбСубконто1;
	ПоГруппам1 = ВыбПоГруппам1;
	Если (ВыбУчитывать1 = 0) Или (ВидСубконто1.Выбран() = 0) Тогда
		Режим1 = 3;
	Иначе
		Режим1 = ВыбРежим1;
	КонецЕсли;

	ВидСубконто2 = ВыбВидСубконто2;
	Субконто2 = ВыбСубконто2;
	ПоГруппам2 = ВыбПоГруппам2;
	Если (ВыбУчитывать2 = 0) Или (ВидСубконто2.Выбран() = 0) Тогда
		Режим2 = 3;
	Иначе
		Режим2 = ВыбРежим2;
	КонецЕсли;            
	
	ВидСубконто3 = ВыбВидСубконто3;
	Субконто3 = ВыбСубконто3;
	ПоГруппам3 = ВыбПоГруппам3;
	Если (ВыбУчитывать3 = 0) Или (ВидСубконто3.Выбран() = 0) Тогда
		Режим3 = 3;
	Иначе
		Режим3 = ВыбРежим3;
	КонецЕсли;                 
	
	Если Режим1 > Режим2 Тогда
		Поменять(ВидСубконто1, ВидСубконто2);
		Поменять(Субконто1, Субконто2);
		Поменять(ПоГруппам1, ПоГруппам2);
		Поменять(Режим1, Режим2);
	КонецЕсли;

	Если Режим2 > Режим3 Тогда
		Поменять(ВидСубконто3, ВидСубконто2);
		Поменять(Субконто3, Субконто2);
		Поменять(ПоГруппам3, ПоГруппам2);
		Поменять(Режим3, Режим2);
		Если Режим1 > Режим2 Тогда
			Поменять(ВидСубконто1, ВидСубконто2);
			Поменять(Субконто1, Субконто2);
			Поменять(ПоГруппам1, ПоГруппам2);
			Поменять(Режим1, Режим2);
		КонецЕсли;
	КонецЕсли;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") Или (Обновить = 0) Тогда
	   	Таб = СоздатьОбъект("Таблица");
	Иначе
	 	Таб.Очистить();
	КонецЕсли;
	Таб.ИсходнаяТаблица("Сформировать");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Заголовок = "Счет "+ВыбСчет;
	Если Режим1 <> 3 Тогда
		Заголовок = Заголовок+", "+ВидСубконто1+?(ПустоеЗначение(Субконто1)=0, " "+Субконто1, "");
		Ит.ИспользоватьСубконто(ВидСубконто1, Субконто1, Режим1, ПоГруппам1);
		Если Режим2 <> 3 Тогда
			Заголовок = Заголовок+", "+ВидСубконто2+?(ПустоеЗначение(Субконто2)=0, " "+Субконто2, "");
			Ит.ИспользоватьСубконто(ВидСубконто2, Субконто2, Режим2, ПоГруппам2);
			Если Режим3 <> 3 Тогда
				Заголовок = Заголовок+", "+ВидСубконто3+?(ПустоеЗначение(Субконто3)=0, " "+Субконто3, "");
				Ит.ИспользоватьСубконто(ВидСубконто3, Субконто3, Режим3, ПоГруппам3);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	    
	Периодичность = "День";
	Если ВыбПериодичность.ТекущаяСтрока() > 0 Тогда
		Периодичность = ВыбПериодичность.ПолучитьЗначение(ВыбПериодичность.ТекущаяСтрока());
	КонецЕсли;
	Ит.ВключатьСубсчета(0, 1);
	Ит.ВыполнитьЗапрос(ВыбНачПериода, ВыбКонПериода, ВыбСчет,,, 3, Периодичность, "СК");
	     
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Путь = "";
	РасположениеФайла(Путь);
	Если Путь = "" Тогда              
		Расшифровка.Установить("Отчет", "АнализCчетаПоСубконтоИПериодам");
		Расшифровка.Установить("Дата1", ВыбНачПериода);
		Расшифровка.Установить("Дата2", ВыбКонПериода);
		Расшифровка.Установить("Счет", ВыбСчет);
		Расшифровка.Установить("Периодичность", Периодичность);
	    Расшифровка.Установить("РазделительУчета", РазделительУчета);

		Расшифровка.Установить("Учитывать1", ВыбУчитывать1);
		Расшифровка.Установить("ВидСубконто1", ВыбВидСубконто1);
		Расшифровка.Установить("Субконто1", ВыбСубконто1);
		Расшифровка.Установить("Режим1", ВыбРежим1);
		Расшифровка.Установить("ПоГруппам1", ВыбПоГруппам1);
		Расшифровка.Установить("ТекстСубконто1", ТекстСубконто1);
		
		Расшифровка.Установить("Учитывать2", ВыбУчитывать2);
		Расшифровка.Установить("ВидСубконто2", ВыбВидСубконто2);
		Расшифровка.Установить("Субконто2", ВыбСубконто2);
		Расшифровка.Установить("Режим2", ВыбРежим2);
		Расшифровка.Установить("ПоГруппам2", ВыбПоГруппам2);
		Расшифровка.Установить("ТекстСубконто2", ТекстСубконто2);
		
		Расшифровка.Установить("Учитывать3", ВыбУчитывать3);
		Расшифровка.Установить("ВидСубконто3", ВыбВидСубконто3);
		Расшифровка.Установить("Субконто3", ВыбСубконто3);
		Расшифровка.Установить("Режим3", ВыбРежим3);
		Расшифровка.Установить("ПоГруппам3", ВыбПоГруппам3);
		Таб.ВывестиСекцию("Обновить");
		Расшифровка.УдалитьВсе();
	КонецЕсли;
	
	Таб.ВывестиСекцию("Шапка");
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	Расшифровка.Установить("Дата1", ВыбНачПериода);
	Расшифровка.Установить("Дата2", ВыбКонПериода);
	Расшифровка.Установить("Счет", ВыбСчет);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Если (Режим1 = 2) Или ((Режим1 = 1) И (ПустоеЗначение(Субконто1) = 0)) Тогда
		Расшифровка.Установить("ВидСубконто1", ВидСубконто1);
		Расшифровка.Установить("Субконто1", Субконто1);
		Расшифровка.Установить("ОтборСубконто1", 2);
	КонецЕсли;
	
	Если (Режим2 = 2) Или ((Режим2 = 1) И (ПустоеЗначение(Субконто2) = 0)) Тогда
		Расшифровка.Установить("ВидСубконто2", ВидСубконто2);
		Расшифровка.Установить("Субконто2", Субконто2);
		Расшифровка.Установить("ОтборСубконто2", 2);
	КонецЕсли; 
	
	Если (Режим3 = 2) Или ((Режим3 = 1) И (ПустоеЗначение(Субконто3) = 0)) Тогда
		Расшифровка.Установить("ВидСубконто3", ВидСубконто3);
		Расшифровка.Установить("Субконто3", Субконто3);
		Расшифровка.Установить("ОтборСубконто3", 2);
	КонецЕсли; 
	
	Если Режим1 = 1 Тогда
		Расшифровка.Установить("ВидСубконто1", ВидСубконто1);
		Ит.ВыбратьСубконто(ВидСубконто1);
		Пока Ит.ПолучитьСубконто(ВидСубконто1) = 1 Цикл
			Расшифровка.Установить("Субконто1", Ит.Субконто(1));
			Расшифровка.Установить("ОтборСубконто1", 2);
			Таб.ВывестиСекцию("Субконто1"+СекцияКол);
			Если Режим2 = 1 Тогда
				Расшифровка.Установить("ВидСубконто2", ВидСубконто2);
				Ит.ВыбратьСубконто(ВидСубконто2);
				Пока Ит.ПолучитьСубконто(ВидСубконто2) = 1 Цикл
					Расшифровка.Установить("Субконто2", Ит.Субконто(2));
					Расшифровка.Установить("ОтборСубконто2", 2);
					Таб.ВывестиСекцию("Субконто2"+СекцияКол);
					Если Режим3 = 1 Тогда
						Расшифровка.Установить("ВидСубконто3", ВидСубконто3);
						Ит.ВыбратьСубконто(ВидСубконто3);
						Пока Ит.ПолучитьСубконто(ВидСубконто3) = 1 Цикл
							Расшифровка.Установить("Субконто3", Ит.Субконто(3));
							Расшифровка.Установить("ОтборСубконто3", 2);
							Таб.ВывестиСекцию("Субконто3"+СекцияКол);
							ПоПериодам(Ит);
							Таб.ВывестиСекцию("ПдвСубконто3"+СекцияКол);
						КонецЦикла;                           
						Если ПустоеЗначение(Субконто3) = 1 Тогда
							Расшифровка.Установить("ВидСубконто3");
							Расшифровка.Установить("Субконто3");
							Расшифровка.Установить("ОтборСубконто3");
						КонецЕсли;
					Иначе
						ПоПериодам(Ит);
					КонецЕсли;
					Таб.ВывестиСекцию("ПдвСубконто2"+СекцияКол);
				КонецЦикла;
				Если ПустоеЗначение(Субконто2) = 1 Тогда
					Расшифровка.Установить("ВидСубконто2");
					Расшифровка.Установить("Субконто2");
					Расшифровка.Установить("ОтборСубконто2");
				КонецЕсли;
			Иначе
				ПоПериодам(Ит);
			КонецЕсли;
			Таб.ВывестиСекцию("ПдвСубконто1"+СекцияКол);
		КонецЦикла;
		Если ПустоеЗначение(Субконто1) = 1 Тогда
			Расшифровка.Установить("ВидСубконто1");
			Расшифровка.Установить("Субконто1");
			Расшифровка.Установить("ОтборСубконто1");
		КонецЕсли;
	Иначе
		ПоПериодам(Ит)
	КонецЕсли;
	Таб.ВывестиСекцию("Подвал");
	Таб.ВывестиСекцию("Подпись");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Анализ счета по субконто и периодам "+Заголовок+"("+ВыбНачПериода+"-"+ВыбКонПериода+")","");
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии(ФлагЧтенияНастройки)
	РасположениеФайла(ПутьКОтчету, "");
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;
	Если ФлагЧтенияНастройки = 0 Тогда
		ВыбПериодичность.УдалитьВсе();
		ВыбПериодичность.ДобавитьЗначение("День");
		ВыбПериодичность.ДобавитьЗначение("Неделя");
		ВыбПериодичность.ДобавитьЗначение("Декада");
		ВыбПериодичность.ДобавитьЗначение("Месяц");
		ВыбПериодичность.ДобавитьЗначение("Квартал");
		ВыбПериодичность.ДобавитьЗначение("Год");
		ВыбПериодичность.ТекущаяСтрока(1);
	КонецЕсли;
	
	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		ВыбНачПериода = глРасшифровка.Получить("Дата1");
		ВыбКонПериода = глРасшифровка.Получить("Дата2");
		ВыбСчет = глРасшифровка.Получить("Счет");
		ПриВыбореСчета();
		
	   	ВыбВидСубконто1 = глРасшифровка.Получить("ВидСубконто1");
		Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
		ВыбСубконто1 = глРасшифровка.Получить("Субконто1");
		ВыбРежим1 = глРасшифровка.Получить("Режим1");
		ВыбУчитывать1 = глРасшифровка.Получить("Учитывать1");
		ВыбПоГруппам1 = глРасшифровка.Получить("ПоГруппам1");
		ТекстСубконто1 = глРасшифровка.Получить("ТекстСубконто1");
		Форма.ВыбУчитывать1.Доступность(1);
		Форма.ВыбРежим1.Доступность(ВыбУчитывать1);
		Форма.ВыбПоГруппам1.Доступность(ВыбУчитывать1);
		Форма.ВыбСубконто1.Доступность(ВыбУчитывать1);
		Форма.Отобрать1.Доступность(ВыбУчитывать1);
					
	   	ВыбВидСубконто2 = глРасшифровка.Получить("ВидСубконто2");
		Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
		ВыбСубконто2 = глРасшифровка.Получить("Субконто2");
		ВыбРежим2 = глРасшифровка.Получить("Режим2");
		ВыбУчитывать2 = глРасшифровка.Получить("Учитывать2");
		ВыбПоГруппам2 = глРасшифровка.Получить("ПоГруппам2");
		ТекстСубконто2 = глРасшифровка.Получить("ТекстСубконто2");
		Форма.ВыбУчитывать2.Доступность(1);
		Форма.ВыбРежим2.Доступность(ВыбУчитывать2);
		Форма.ВыбПоГруппам2.Доступность(ВыбУчитывать2);
		Форма.ВыбСубконто2.Доступность(ВыбУчитывать2);
		Форма.Отобрать2.Доступность(ВыбУчитывать2);
	   	
		ВыбВидСубконто3 = глРасшифровка.Получить("ВидСубконто3");
		Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
		ВыбСубконто3 = глРасшифровка.Получить("Субконто3");
		ВыбРежим3 = глРасшифровка.Получить("Режим3");
		ВыбУчитывать3 = 0;
		ВыбПоГруппам3 = глРасшифровка.Получить("ПоГруппам3");
	    НомВСп=ВыбПериодичность.НайтиЗначение(глРасшифровка.Получить("Периодичность")); 
		ВыбПериодичность.ТекущаяСтрока(НомВСп);
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		ВыбНачПериода = НачалоПериодаБИ();
		ВыбКонПериода = КонецПериодаБИ();
		Если ФлагЧтенияНастройки = 0 Тогда
			ВыбРежим1 = 1;
			ВыбРежим2 = 1;
			ВыбРежим3 = 1;     
		    ВосстановитьЗначение("ОтчРабСчет", ВыбСчет);
			ПриВыбореСчета();
		КонецЕсли;
	КонецЕсли;
	ВыбУчитывать3 = 0;
//	ПриВыбореСчета();
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	ВыбНачПериода = НачалоПериодаБИ();
	ВыбКонПериода = КонецПериодаБИ();
	ВыбВидСубконто1 = Выбсчет.ВидСубконто(1);
	Форма.ВыбУчитывать1.Доступность(ВыбВидСубконто1.Выбран());
	ВыбВидСубконто2 = Выбсчет.ВидСубконто(2);
	Форма.ВыбУчитывать2.Доступность(ВыбВидСубконто2.Выбран());
	ВыбВидСубконто3 = Выбсчет.ВидСубконто(3);
	Форма.ВыбУчитывать3.Доступность(ВыбВидСубконто3.Выбран());
	ПриВыбореУчитывать1();
	ПриВыбореУчитывать2();
	ПриВыбореУчитывать3();
КонецПроцедуры
