Процедура Сформировать()
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	
	Таб.ИспользоватьФормат("Ч015.2");
	БИ=СоздатьОбъект("БухгалтерскиеИтоги");
	Аморт=СоздатьОбъект("БухгалтерскиеИтоги");
	Если ВидСпр.ТекущаяСтрока()=1 Тогда
	    БИ.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства,Спр,,Итоги);
		Аморт.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства,Спр,,Итоги);
		Если Спр.Выбран()=0 Тогда
			ВидАктива="основным средствам";
		Иначе
			ВидАктива="ОС "+Спр.Наименование;
		КонецЕсли;
	Иначе
	    БИ.ИспользоватьСубконто(ВидыСубконто.НематАктивы,Спр,,Итоги);
		Аморт.ИспользоватьСубконто(ВидыСубконто.НематАктивы,Спр,,Итоги);
		Если Спр.Выбран()=0 Тогда
			ВидАктива="нематериальным активам";
		Иначе
			ВидАктива="НМА "+Спр.Наименование;
		КонецЕсли;
	КонецЕсли; 
	
	Таб.ВывестиСекцию("Шапка");
	
	БИ.ВыполнитьЗапрос(НачДата,КонДата,?(ВидСпр.ТекущаяСтрока()=1,"123","111"),,,3);
	Аморт.ВыполнитьЗапрос(НачДата,КонДата,?(ВидСпр.ТекущаяСтрока()=1,"124","113"),,,3);
	ИтНачАморт=0;
	ИтОбАморт=0;
	ИтКонАморт=0;
	БИ.ВыбратьСубконто();
	Аморт.ВыбратьСубконто();
	Пока БИ.ПолучитьСубконто()=1 Цикл
		Если Аморт.ПолучитьСубконто(,,БИ.Субконто())=1 Тогда
		    НачАморт=Аморт.СНК();
			ОбАморт=Аморт.КО();
			КонАморт=Аморт.СКК();
		Иначе
		    НачАморт=0;
			ОбАморт=0;
			КонАморт=0;
		КонецЕсли;
		Если БИ.Субконто().ЭтоГруппа()=1 Тогда
		    Таб.ВывестиСекцию("Группа");
		Иначе
		    Таб.ВывестиСекцию("Строка");
			ИтНачАморт=ИтНачАморт+НачАморт;
			ИтОбАморт=ИтОбАморт+ОбАморт;
			ИтКонАморт=ИтКонАморт+КонАморт;
		КонецЕсли;
		
	КонецЦикла;
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.Показать();
КонецПроцедуры

Процедура ПриОткрытии()
	Если ВидСпр.РазмерСписка()=0 Тогда
	    ВидСпр.ДобавитьЗначение("ОС","Основные средства");
		ВидСпр.ДобавитьЗначение("НМА","Нематериальные активы");
	КонецЕсли; 
	Форма.Спр.НеИзменятьВид(1);
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Стр= Форма.Параметр;
	    Если Лев(Стр,2)="ОС" Тогда
			ВидСпр.ТекущаяСтрока(1);
	        НазначитьВид(Спр,"ОсновныеСредства");
		Иначе
			ВидСпр.ТекущаяСтрока(2);
	        НазначитьВид(Спр,"НематАктивы");
		КонецЕсли;
		Если Прав(Стр,1)="1" Тогда
		    Итоги=1
		Иначе
		    Итоги=0
		КонецЕсли;
		Если ПустоеЗначение(НачДата)=1 Тогда
		    НачДата=НачГода(РабочаяДата());
		КонецЕсли;
		Если ПустоеЗначение(КонДата)=1 Тогда
		    КонДата=РабочаяДата();
		КонецЕсли;
		Сформировать();
		Форма.Закрыть();
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьВид()
	Если ВидСпр.ТекущаяСтрока()=1 Тогда
	    НазначитьВид(Спр,"ОсновныеСредства");
	Иначе
	    НазначитьВид(Спр,"НематАктивы");
	КонецЕсли; 
КонецПроцедуры