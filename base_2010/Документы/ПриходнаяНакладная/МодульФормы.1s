Перем СписокДействий;

Процедура ПроверкаСФПолучена()
	Если СФПолучена=Да Тогда
		СчетНДС=СчетПоКоду("534.2");
	Иначе
		СчетНДС=СчетПоКоду("226.1");
	КонецЕсли;
КонецПроцедуры

Процедура ДоступСумВал()
	Если Валюта=Леи Тогда
		Форма.СумВал.Доступность(0);
	Иначе
		Форма.СумВал.Доступность(1);
	КонецЕсли;
КонецПроцедуры

Процедура ОпрВидаСубкПост()
	НазначитьВид(СубкПоставщика1,СчетПоставщика.ВидСубконто(1));
	Форма.СубкПоставщика1.НеИзменятьВид(1);
	Если СчетПоставщика.КоличествоСубконто()=2 Тогда
		НазначитьВид(СубкПоставщика2,СчетПоставщика.ВидСубконто(2));
		Форма.СубкПоставщика2.НеИзменятьВид(1);
		Форма.СубкПоставщика2.Доступность(1);
	Иначе
		Форма.СубкПоставщика2.Доступность(0);
	КонецЕсли;
	Если СчетПоставщика.Валютный=0 Тогда
		Валюта=Леи;
	КонецЕсли;
КонецПроцедуры

Процедура ОпрВидаСубкТМЦ()
	НазначитьВид(ТМЦ1,СчетТМЦ.ВидСубконто(1));
	Форма.ТМЦ1.НеИзменятьВид(1);
	Если СчетТМЦ.КоличествоСубконто()=2 Тогда
		НазначитьВид(ТМЦ2,СчетТМЦ.ВидСубконто(2));
		Форма.ТМЦ2.НеИзменятьВид(1);
		Форма.ТМЦ2.Доступность(1);
	Иначе
		Форма.ТМЦ2.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Процедура РасчетСуммы()
	СумЛей=Цена*Количество;
КонецПроцедуры

Процедура РасчетСумЛей()
	СумЛей=СумВал*Валюта.ТекКурс.Получить(ДатаКурса)/Валюта.Кратность.Получить(ДатаКурса);
КонецПроцедуры

Процедура РасчетНДС()
	Если Константа.СуммаВключаетНДС=Да Тогда
		НДС=СумЛей*СтНДС.Ставка/(100+СтНДС.Ставка)
	Иначе
		НДС=СумЛей*СтНДС.Ставка/100
	КонецЕсли;
КонецПроцедуры

Процедура ОпрЕдИзм()
	Если ТМЦ1.Вид()="Товары" Тогда
		Цена=ТМЦ1.Цена.Получить(ДатаДок);
		Импорт=ТМЦ1.Импорт;
	КонецЕсли;
	Попытка	ЕдИзм=ТМЦ1.ЕдИзм Исключение	ЕдИзм=0	КонецПопытки;
		
	Попытка	СтНДС=ТМЦ1.НДС.Получить(ДатаДок) Исключение
		Если ПустоеЗначение(СтНДС)=1 Тогда
			СтНДС=Константа.СтавкаНДС
		КонецЕсли;
	КонецПопытки;
	
	Форма.ЕдИзм.Доступность(0);
	ДоступСумВал();
КонецПроцедуры

Процедура ПриРедактированииНовойСтроки()
	Если Не((СчетТМЦпоУм=СчетПоКоду("00"))ИЛИ(ПустаяСтрока(СчетТМЦпоУм)=1)) Тогда
		СчетТМЦ=СчетТМЦпоУм;
	КонецЕсли;
КонецПроцедуры

Процедура ПриНачалеРедактированияСтроки()
	Если СчетТМЦ.Выбран()=1 Тогда
		ОпрВидаСубкТМЦ();
	КонецЕсли;
КонецПроцедуры

Функция Итоги()
	ВалСумма=Формат(Итог("СумВал"),"Ч010.2");
	СтрВалСум=?(Итог("СумВал")>0,"Валютная сумма "+СокрЛ(ВалСумма)+"    ","");
	ЛейСумма=Формат(Итог("СумЛей"),"Ч010.2");
	СтрЛейСум=?(Итог("СумЛей")>0,"Леевая сумма "+СокрЛ(ЛейСумма)+"    ","");
	ИтНДС=Формат(Итог("НДС"),"Ч010.2");
	СтрНДС=?(Итог("НДС")>0,"НДС "+СокрЛ(ИтНДС)+"    ","");
	Возврат СтрВалСум+СтрЛейСум+СтрНДС;
КонецФункции

Процедура ВводНового(ПК)
	Автор=глПользователь;
	Если ПК=0 Тогда
		ДатаКурса=ДатаДок;
		Валюта=Леи;
		СчетПоставщика=СчетПоКоду("521.1");
		СчетТМЦпоУм=СчетПоКоду("00");
		СФПолучена=Да;
	КонецЕсли;
	ПрСчетТМЦ=СчетТМЦпоУм;
	ОпрВидаСубкПост();
	ДоступСумВал();
	ПроверкаСФПолучена();
КонецПроцедуры

Процедура Печать()
	Пропись(КаталогИБ()+"Spl\"+Валюта.Spl);
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Bon de primire");
	Таб.ВывестиСекцию("Шапка");
	ВыбратьСтроки();ИтСБН=0;
	Пока ПолучитьСтроку()=1 Цикл
		Если ТМЦ1.Вид()="ОсновныеСредства" Тогда
			ИнвНомер=0;
			ИнвНомер=ТМЦ1.ИнвНомер;
		Иначе            
			ИнвНомер=ТМЦ1.Код;
		КонецЕсли;
		Если Константа.СуммаВключаетНДС=Да Тогда
			ЦБН=Формат((СумЛей-НДС)/Количество,"Ч10.3");
			СБН=Формат(СумЛей-НДС,"Ч10.3");
		Иначе
			ЦБН=Формат(СумЛей/Количество,"Ч10.3");
			СБН=Формат(СумЛей,"Ч10.3");
		КонецЕсли;
		ИтСБН=ИтСБН+СБН;
		Таб.ВывестиСекцию("Товар");
	КонецЦикла;
	Таб.ВывестиСекцию("Итог");
	Таб.Опции(0,0,8,0);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Приходная накладная");
КонецПроцедуры

Процедура ДополнительныеРасходы() 
	Перем ФормаСчФ;
	Записать();
	ДокПодч=НайтиПодчиненный(ТекущийДокумент(),"ДополнительныеРасходы");
	Если (ПустоеЗначение(ДокПодч)=0) Тогда
		ОткрытьФорму(ДокПодч.ТекущийДокумент(),ФормаСчФ);
	Иначе
		ОткрытьФорму("Документ.ДополнительныеРасходы",ФормаСчФ,ТекущийДокумент());
	КонецЕсли;
КонецПроцедуры           

Процедура ПриОткрытии()
	ПрТМЦСчета="";ПрТМЦКоды="";ПрНомСтр="";
	Если Проведен()=1 Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			ПрТМЦСчета=ПрТМЦСчета+Строка(СчетТМЦ.Код)+Симв(13)+Симв(10);
			ПрТМЦКоды=ПрТМЦКоды+ТМЦ1.ПолныйКод()+Симв(13)+Симв(10);
			ПрНомСтр=ПрНомСтр+НомерСтроки+Симв(13)+Симв(10);
		КонецЦикла;
	КонецЕсли;
	ПрСчетТМЦ=СчетТМЦпоУм;
	ОпрВидаСубкПост();
	ДоступСумВал();
	ПриЗаписиПерепроводить(1);
	
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если Форма.Параметр="Bon de primire" Тогда
			Печать();
			СтатусВозврата(0);
		Иначе
			глПроверкаРазрешенияРедактирования(Контекст);
		КонецЕсли;
	Иначе
		глПроверкаРазрешенияРедактирования(Контекст);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);
	КонецЕсли;
	
	ЖурналДокумента=глВыбратьЖурнал(ТекущийДокумент());
КонецПроцедуры

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Открыть  в журнале");
