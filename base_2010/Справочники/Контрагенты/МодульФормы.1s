Процедура ВводНового()
	Если Наименование="" Тогда
		ВидКонтрагента=Перечисление.ВидыКонтрагентов.Организация;
	КонецЕсли;
КонецПроцедуры

Процедура СделатьОсновным()
	Если СпсРасчСчетов.РазмерСписка()=0 Тогда
	    Сообщить("Не найдено ни одного расчетного счета!")
	ИначеЕсли СпсРасчСчетов.РазмерСписка()=1 Тогда
	    ОснРасчСчет=СпсРасчСчетов.ПолучитьЗначение(1);
	ИначеЕсли СпсРасчСчетов.ТекущаяСтрока()=0 Тогда
		СпсРасчСчетов.ВыбратьЗначение(ОснРасчСчет,"Выберите основной расчетный счет:",1,0)
	Иначе
		ОснРасчСчет=СпсРасчСчетов.ПолучитьЗначение(СпсРасчСчетов.ТекущаяСтрока());
	КонецЕсли; 
КонецПроцедуры

Процедура Проверка()
	Если ВидКонтрагента=Перечисление.ВидыКонтрагентов.ЧастноеЛицо Тогда
		КолвоЗакл=Форма.Закладки.РазмерСписка();
		Если КолвоЗакл=3 Тогда
			Возврат;
		КонецЕсли;
		Форма.Закладки.ДобавитьЗначение(3,"Паспортные данные");
	Иначе
		КолвоЗакл=Форма.Закладки.РазмерСписка();
		Если КолвоЗакл=3 Тогда
			Форма.Закладки.УдалитьЗначение(3);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнениеСписка()
	Если Выбран()=1 Тогда
		СпсРасчСчетов.УдалитьВсе();
		РасчСчета=СоздатьОбъект("Справочник.РасчетныеСчета");
		РасчСчета.ИспользоватьВладельца(ТекущийЭлемент());
		РасчСчета.ПорядокНаименований();
		РасчСчета.ВыбратьЭлементы();
		Пока РасчСчета.ПолучитьЭлемент()=1 Цикл
			Если РасчСчета.ПометкаУдаления()=1 Тогда
				Продолжить;
			КонецЕсли;
			СпсРасчСчетов.ДобавитьЗначение(РасчСчета.ТекущийЭлемент(),
					РасчСчета.Наименование+" р/с 1 № "+СокрЛП(РасчСчета.РасчетныйСчет1)+
					?(ПустаяСтрока(РасчСчета.РасчетныйСчет2)=0,
						" р/с 2 № "+СокрЛП(РасчСчета.РасчетныйСчет2),"")+
					" в банке "+СокрЛП(РасчСчета.Банк));
		КонецЦикла;
	КонецЕсли;
	Если СпсРасчСчетов.РазмерСписка()>0 Тогда
		Форма.КнИзменить.Доступность(1);
		Форма.КнУдалить.Доступность(1);
		СпсРасчСчетов.ТекущаяСтрока(1);
	Иначе
		Форма.КнИзменить.Доступность(0);
		Форма.КнУдалить.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Процедура Добавить()
	Если Выбран()=0 Тогда
		Если Вопрос("Расчетные счета можно редактировать
					|только после записи контрагента.
					|Записать контрагента?", "Да+Нет") = "Да" Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ОткрытьФормуМодально("Элемент.РасчетныеСчета",Контекст,,,ТекущийЭлемент());
	ЗаполнениеСписка();
КонецПроцедуры

Процедура Изменить()
	ТекРасчСчет=СпсРасчСчетов.ПолучитьЗначение(СпсРасчСчетов.ТекущаяСтрока());
	ОткрытьФормуМодально(ТекРасчСчет, Контекст);
	ЗаполнениеСписка();
КонецПроцедуры

Процедура Удалить()
	Если Вопрос("Вы действительно хотите удалить счет?
				|(счет будет помечен на удаление)","Да+Нет") = "Нет" Тогда
		Возврат;
	КонецЕсли;
	ТекРасчСчет=СпсРасчСчетов.ПолучитьЗначение(СпсРасчСчетов.ТекущаяСтрока());
	РасчСчета=СоздатьОбъект("Справочник.РасчетныеСчета");
	РасчСчета.ИспользоватьВладельца(ТекущийЭлемент());
	Если РасчСчета.НайтиЭлемент(ТекРасчСчет)=1 Тогда
		РасчСчета.Удалить(0);
	КонецЕсли;
	ЗаполнениеСписка();
КонецПроцедуры

Процедура ОткрытьДоговора()
	Если Выбран()=0 Тогда
		Если Вопрос("Список договоров можно редактировать
					|только после записи контрагента.
					|Записать контрагента?", "Да+Нет") = "Да" Тогда
	    	Записать();
		Иначе
			Возврат;
		КонецЕсли;
    КонецЕсли;
	ОткрытьФормуМодально("Справочник.Договора", Контекст);
КонецПроцедуры

Процедура ОткрытьПлатежи()
	Если Выбран()=0 Тогда
		Если Вопрос("Список платежей можно редактировать
					|только после записи контрагента.
					|Записать контрагента?", "Да+Нет") = "Да" Тогда
	    	Записать();
		Иначе
			Возврат;
		КонецЕсли;
    КонецЕсли;
	ОткрытьФормуМодально("Справочник.Платежи", Контекст);
КонецПроцедуры

Процедура ПриОткрытии()
	Проверка();
	ЗаполнениеСписка();
КонецПроцедуры

Процедура ПриВыбореЗакладки(Ном,Значен);
	Если Значен=1 Тогда
		Форма.ИспользоватьСлой("Основной, Общие ",2);
	ИначеЕсли Значен=2 Тогда
		Форма.ИспользоватьСлой("Основной, РасчетныеСчета ",2);
	ИначеЕсли Значен=3 Тогда
		Форма.ИспользоватьСлой("Основной, ПаспортныеДанные ",2);
		Активизировать("Наименование",0);
	КонецЕсли;
	Форма.Закладки.ТекущаяСтрока(Ном);
КонецПроцедуры

Процедура ПриЗаписи()
	СтатусВозврата(глЗаписьПериодическихРеквизитов(Контекст));
КонецПроцедуры

Форма.ИспользоватьЗакладки(1);
Форма.Закладки.ДобавитьЗначение(1,"Общие");
Форма.Закладки.ДобавитьЗначение(2,"Расчетные счета");
Форма.ИспользоватьСлой("Основной,Общие",2);