Перем Дест, Таб, Вид, Значение, ИмяРеквизита, Периодический, ДатаЗаполнения, Номер, СпЗн;

Процедура ТипРеквизита(Рекв,Тип,Длина,Точность)
	ТекВид=Рекв.Вид; Тип=Рекв.Тип; Длина=Рекв.Длина; Точность=Рекв.Точность;
	Если (Тип<>"Число") и (Тип<>"Строка") Тогда Тип=Тип+"."+ТекВид; КонецЕсли;
КонецПроцедуры

Процедура ВклВыкл(Реж)
	Для Ин=1 По Рекв.РазмерСписка() Цикл
		Рекв.Пометка(Ин,Реж);
	КонецЦикла;
КонецПроцедуры

Процедура Плюс()
Перем Вид, ТекФорма;
	Вид=Виды.ПолучитьЗначение(Виды.ТекущаяСтрока());
	ОткрытьПодбор("Справочник."+Вид,,ТекФорма);
КонецПроцедуры

Процедура Минус()
	Если Элементы.ТекущаяСтрока()=0 Тогда Возврат; КонецЕсли;
	Элементы.УдалитьЗначение(Элементы.ТекущаяСтрока());
	Форма.Удалить.Доступность(?(Элементы.РазмерСписка()=0,0,1));
	Форма.Очистить.Доступность(?(Элементы.РазмерСписка()=0,0,1));
КонецПроцедуры

Процедура Очистить()
	Элементы.УдалитьВсе();
	Форма.Добавить.Доступность(1);
	Форма.Удалить.Доступность(0);
	Форма.Очистить.Доступность(0);
КонецПроцедуры

Процедура ПриВыбореВсе()
	Если Все=1 Тогда
		Форма.Добавить.Доступность(0); Форма.Удалить.Доступность(0); Форма.Очистить.Доступность(0);
		Форма.Начало.Доступность(0); Форма.Конец.Доступность(0);
	Иначе
		Форма.Добавить.Доступность(1);
		Если Элементы.РазмерСписка()=0
		Тогда Форма.Удалить.Доступность(0); Форма.Очистить.Доступность(0);
		Иначе Форма.Удалить.Доступность(1); Форма.Очистить.Доступность(1);
		КонецЕсли;
		Форма.Начало.Доступность(1); Форма.Конец.Доступность(1);
	КонецЕсли;
КонецПроцедуры

Процедура Открыть()
	ТекЭл=Элементы.ПолучитьЗначение(Элементы.ТекущаяСтрока());
	ОткрытьФормуМодально(ТекЭл,,1);
КонецПроцедуры

Процедура ОбработкаПодбора(Элемент)
	Если Элементы.НайтиЗначение(Элемент)>0 Тогда Возврат; КонецЕсли;
	Элементы.ДобавитьЗначение(Элемент,""+Элемент);
	Форма.Удалить.Доступность(1);
	Форма.Очистить.Доступность(1);
КонецПроцедуры

Процедура Действие(Элемент)
	Состояние("Обрабатывается элемент : "+Элемент);
	Спр=СоздатьОбъект("Справочник."+Вид);
	Спр.НайтиЭлемент(Элемент);
	Если Спр.Выбран()=0 Тогда Сообщить("Не найден элемент справочника -- "+Элемент); Возврат; КонецЕсли;
	Если Режим=1 Тогда Спр.Удалить(0);
	ИначеЕсли Режим=2 Тогда Спр.Удалить(0); СпЗн.ДобавитьЗначение(Спр.ТекущийЭлемент());
	ИначеЕсли Режим=3 Тогда Спр.СнятьПометкуУдаления();
	ИначеЕсли Режим=4 Тогда
		Спр.Родитель=Дест;
		Спр.Записать();
	ИначеЕсли Режим=5 Тогда
		Спр.ИспользоватьДату(РабочаяДата(),1);
		Таб.ВывестиСекцию("Строка|Начало");
		Содержимое=Спр.Код; Таб.ПрисоединитьСекцию("Строка|СекцияВ");
		Содержимое=Спр.Наименование+?(Спр.ЭтоГруппа()=1," (ГРУППА)",""); Таб.ПрисоединитьСекцию("Строка|СекцияВ");
		Для Ин=1 По Рекв.РазмерСписка() Цикл
			Если Рекв.Пометка(Ин)=0 Тогда Продолжить; КонецЕсли;
			Имя=Рекв.ПолучитьЗначение(Ин); Содержимое=Строка(Спр.ПолучитьАтрибут(Имя));
			Таб.ПрисоединитьСекцию("Строка|СекцияВ");
		КонецЦикла;
		Номер=Номер+1;
	ИначеЕсли Режим=6 Тогда
		Если Периодический=1 Тогда
			Спр.ИспользоватьДату(ДатаЗаполнения,1);
		КонецЕсли;
		Спр.УстановитьАтрибут(ИмяРеквизита,Значение);
		Спр.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура Обработать()
Перем Предст,Тип,Длина,Точность;
	Вид=Виды.ПолучитьЗначение(Виды.ТекущаяСтрока(),Предст);// получим вид справочника
	Рез=Все+?(Элементы.РазмерСписка()=0,0,1)+?(ПустоеЗначение(Начало)+ПустоеЗначение(Конец)=0,1,0);
	Если Рез=0 Тогда Возврат; КонецЕсли;
	Спр=СоздатьОбъект("Справочник."+Вид);
	Спр.ПорядокНаименований();
	Если Режим=4 Тогда
		ТекФорма=ФормаВыбора.ПолучитьЗначение(ФормаВыбора.ТекущаяСтрока());
		Рез=Спр.Выбрать("Выберите группу-приемник (ESC - корневая группа)",ТекФорма);
		Если Рез=0 Тогда Дест=0;
		ИначеЕсли Спр.ЭтоГруппа()=1 Тогда Дест=Спр.ТекущийЭлемент();
		Иначе Дест=Спр.Родитель;
		КонецЕсли;
	ИначеЕсли Режим=5 Тогда
		Таб=СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("Таблица"); Номер=1;
		Таб.ВывестиСекцию("Шапка"); Таб.ВывестиСекцию("Заголовок|Начало");
		 ИмяРеквизита="Код"; Таб.ПрисоединитьСекцию("Заголовок|СекцияВ");
		 ИмяРеквизита="Наименование"; Таб.ПрисоединитьСекцию("Заголовок|СекцияВ");
		Для Ин=1 По Рекв.РазмерСписка() Цикл
			Если Рекв.Пометка(Ин)=0 Тогда Продолжить; КонецЕсли;
			Рекв.ПолучитьЗначение(Ин,Предст); ИмяРеквизита=Предст;
			Таб.ПрисоединитьСекцию("Заголовок|СекцияВ");
		КонецЦикла;
	ИначеЕсли Режим=6 Тогда
		Выбор=СоздатьОбъект("СписокЗначений");
		Рекв.Выгрузить(Выбор);
		Рез=Выбор.ВыбратьЗначение(Значение,"Выберите значение к заполнению",,,0);
		Если Рез<>1 Тогда Возврат; КонецЕсли;
		ИмяРеквизита=Метаданные.Справочник(Вид).Реквизит(Значение).Идентификатор;
		Периодический=Метаданные.Справочник(Вид).Реквизит(Значение).Периодический;
		ТипРеквизита(Метаданные.Справочник(Вид).Реквизит(Значение),Тип,Длина,Точность);
		Рез=ВвестиЗначение(Значение,"Введите заполнитель",Тип,Длина,Точность);
		Если Рез=0 Тогда Возврат; КонецЕсли;
		Если Периодический=1 Тогда
			ДатаЗаполнения=РабочаяДата();
			Рез=ВвестиДату(ДатаЗаполнения,"Введите дату заполнения периодического реквизита");
			Если Рез<>1 Тогда Возврат; КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	СпЗн=СоздатьОбъект("СписокЗначений");
	Если Режим=2 Тогда
		Если Вопрос("Удаление произойдет без контроля целостности ! Вы уверены ?","Да+Нет")="Нет" Тогда Возврат; КонецЕсли;
	КонецЕсли;
	Если Все=1 Тогда
		Спр.ВыбратьЭлементы(0);
		Пока Спр.ПолучитьЭлемент()=1 Цикл
			Действие(Спр.ТекущийЭлемент());
		КонецЦикла;
	ИначеЕсли Элементы.РазмерСписка()<>0 Тогда
		Для Ин=1 По Элементы.РазмерСписка() Цикл
			Спр.НайтиЭлемент(Элементы.ПолучитьЗначение(Ин));
			Действие(Спр.ТекущийЭлемент());
		КонецЦикла;
	ИначеЕсли (ПустоеЗначение(Начало)=0) и (ПустоеЗначение(Конец)=0) Тогда
		Спр.ВыбратьЭлементы(0);
		Пока Спр.ПолучитьЭлемент()=1 Цикл
			Если (Число(Спр.Код)>=Число(Начало)) и (Число(Спр.Код)<=Число(Конец)) Тогда Действие(Спр.ТекущийЭлемент()); КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если (Режим=2) и (СпЗн.РазмерСписка()<>0) Тогда
		УдалитьОбъекты(СпЗн,0); СпЗн.УдалитьВсе();
	КонецЕсли;
	Если Режим=5 Тогда
		Таб.Опции(0,0,4,0);
		Таб.ТолькоПросмотр(1);
		Таб.Показать("Содержимое справочника "+Вид);
	КонецЕсли;
	Если Очистка=1 Тогда
		Элементы.УдалитьВсе();
		Форма.Удалить.Доступность(0);
		Форма.Очистить.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Процедура ПриВыборе()
	Рекв.УдалитьВсе(); Стр=Виды.ПолучитьЗначение(Виды.ТекущаяСтрока());
	Для Ин=1 По Метаданные.Справочник(Стр).Реквизит() Цикл
		Рекв.ДобавитьЗначение(Метаданные.Справочник(Стр).Реквизит(Ин).Идентификатор,Метаданные.Справочник(Стр).Реквизит(Ин).Представление());
		Рекв.Пометка(Ин,1);
	КонецЦикла;
	Рекв.Сортировать();
	ФормаВыбора.УдалитьВсе();
	Для Ин=1 По Метаданные.Справочник(Стр).ФормаСписка() Цикл
		ФормаВыбора.ДобавитьЗначение(Метаданные.Справочник(Стр).ФормаСписка(Ин).Идентификатор,Метаданные.Справочник(Стр).ФормаСписка(Ин).Представление());
	КонецЦикла;
	ФормаВыбора.ТекущаяСтрока(1);
КонецПроцедуры

Процедура ПриОткрытии(ИспНастройку)
	Для Ин=1 По Метаданные.Справочник() Цикл
		Виды.ДобавитьЗначение(Метаданные.Справочник(Ин).Идентификатор,Метаданные.Справочник(Ин).Представление());
	КонецЦикла;
	Виды.ТекущаяСтрока(1); ПриВыборе(); ПриВыбореВсе();
	Если ИспНастройку=0 Тогда
		Очистка=1; Режим=1;
	КонецЕсли;
КонецПроцедуры