Перем Расшифровка;
Перем Обновить;
Перем Таб;
Перем СписокЗащитыОтЗацикливания;

Функция УстановитьРасшифровку(Докум)
	Расшифровка=СоздатьОбъект("СписокЗначений");
	Расшифровка.ДобавитьЗначение(Докум,"ДокументЖурнала");
	Возврат Расшифровка;
КонецФункции

Функция ПредставлениеДокумента(Док,ДокОснования)
	ВидДок=Док.Вид();
	ПечДок=""+Метаданные.Документ(ВидДок).Представление()+" № "+СокрЛП(Док.НомерДок)+" от "+Док.ДатаДок;
	Если Док.Проведен()=0 Тогда
		ПечДок=ПечДок+" (Не проведен!)";
	КонецЕсли;
	
	Валюта=глВалютаДок(Док);
	Сумма=0;
	СуммаНП=0;
	Если ВидДок="ДвиженияДенежныхСредств" Тогда
		Если ПустоеЗначение(ДокОснования)=0 Тогда
			Док.ВыбратьСтроки();
			Пока Док.ПолучитьСтроку()=1 Цикл
				Если Док.ДокументОснование=ДокОснования Тогда
					Если Док.ВидДвижения=Перечисление.ВидыДвиженийПоРасчетномуСчету.Поступление Тогда
						Сумма=Док.Приход;
					Иначе
						Сумма=Док.Расход;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Сумма=0;СуммаНП=0;
	КонецЕсли;
	
	Если ПустоеЗначение(Сумма)=0 Тогда
		ПечДок=ПечДок+" Сумма: "+Сумма+" ";
		Если ПустоеЗначение(Валюта)=0 Тогда
			ПечДок=ПечДок+СокрЛП(Валюта.Наименование);
		КонецЕсли;
		Если ПустоеЗначение(СуммаНП)=0 Тогда
			ПечДок=ПечДок+" (НП: "+СуммаНП+") ";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПечДок;
КонецФункции

Функция НайтиКорень(Док)
	СписокЗащитыОтЗацикливания.ДобавитьЗначение(Док);
	ВД=Док.Вид();
	КореньДок=0;
	Для Н=1 По Метаданные.Документ(ВД).РеквизитШапки() Цикл
		РеквДок=Док.ПолучитьАтрибут(Метаданные.Документ(ВД).РеквизитШапки(Н));
		Если ТипЗначенияСтр(РеквДок)="Документ" Тогда
			Если ПустоеЗначение(РеквДок)=0 Тогда
				Если СписокЗащитыОтЗацикливания.НайтиЗначение(РеквДок)>0 Тогда
					Продолжить;
				КонецЕсли;
				КореньДок=РеквДок;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустоеЗначение(КореньДок)=1 Тогда
		Возврат Док;
	Иначе
		Возврат НайтиКорень(КореньДок);
	КонецЕсли;
КонецФункции

Процедура ВывестиПодчиненные(Докум,ДокОснования,Док,Последний,Уровень,Уровни)
	ДокПодч = СоздатьОбъект("Документ");
	ПровДокПодч = СоздатьОбъект("Документ");
	
	Таб.ВывестиСекцию("GrEmpty|Empty");
	Если Уровень=1 Тогда
		Если Последний=1 Тогда
			Таб.ПрисоединитьСекцию("GrHook|Lines");
		Иначе
			Таб.ПрисоединитьСекцию("GrTSect|Lines");
		КонецЕсли;
	Иначе
		Если Уровни.ПолучитьЗначение(1)=1 Тогда
			Таб.ПрисоединитьСекцию("GrLine|Lines");
		Иначе
			Таб.ПрисоединитьСекцию("GrEmpty|Lines");
		КонецЕсли;
		Для Сч=2 По Уровень-1 Цикл
			Если Уровни.ПолучитьЗначение(Сч)=1 Тогда
				Таб.ПрисоединитьСекцию("GrLine|Lines");
			Иначе
				Таб.ПрисоединитьСекцию("GrEmpty|Lines");
			КонецЕсли;
		КонецЦикла;
		Если Последний=1 Тогда
			Таб.ПрисоединитьСекцию("GrHook|Lines");
		Иначе
			Таб.ПрисоединитьСекцию("GrTSect|Lines");
		КонецЕсли;
	КонецЕсли;
	ПечДок=ПредставлениеДокумента(Док,ДокОснования);
	Если Докум=Док Тогда
		Таб.ПрисоединитьСекцию("ТекДок|Body");
	Иначе
		Таб.ПрисоединитьСекцию("Group|Body");
	КонецЕсли;
	Уровень=Уровень+1;
	ДокПодч.ВыбратьПодчиненныеДокументы(,,Док);
	ПровДокПодч.ВыбратьПодчиненныеДокументы(,,Док);
	Начало=1;
	Пока ДокПодч.ПолучитьДокумент()=1 Цикл
		Если СписокЗащитыОтЗацикливания.НайтиЗначение(ДокПодч.ТекущийДокумент())>0 Тогда
			Продолжить;
		КонецЕсли;
		СписокЗащитыОтЗацикливания.ДобавитьЗначение(ДокПодч.ТекущийДокумент());
		Если Начало=1 Тогда
			А=ПровДокПодч.ПолучитьДокумент();
			Начало=0;
		КонецЕсли;
		Если Док=ДокПодч.ТекущийДокумент() Тогда
			А=ПровДокПодч.ПолучитьДокумент();
			Продолжить;
		КонецЕсли;
		Если ПровДокПодч.ПолучитьДокумент()=1 Тогда
			Если Док=ПровДокПодч.ТекущийДокумент() Тогда
				Уровни.УстановитьЗначение(Уровень,0);
				Последний=1;
			Иначе
				Уровни.УстановитьЗначение(Уровень,1);
				Последний=0;
			КонецЕсли;
		Иначе
			Уровни.УстановитьЗначение(Уровень,0);
			Последний=1;
		КонецЕсли;
		ВывестиПодчиненные(Докум,Док,ДокПодч.ТекущийДокумент(),Последний,Уровень,Уровни);
	КонецЦикла;
	Уровень=Уровень-1;
КонецПроцедуры

Процедура СформироватьДерево()
	СписокЗащитыОтЗацикливания=СоздатьОбъект("СписокЗначений");
	Докум=ВыбДокумент;
	Если ПустоеЗначение(Докум)=1 Тогда
		Предупреждение("Не указан документ для построения структуры подчиненности!");
		Возврат;
	КонецЕсли;
	
	Если Обновить = 2 Тогда
		СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ДеревоДокументов");
	Расшифровка.Установить("Док", ВыбДокумент);
	Расшифровка.Установить("Обновить", 1);
	
	КореньДок=НайтиКорень(Докум);
	Уровень=0;
	Уровни=СоздатьОбъект("СписокЗначений");
	Для Сч=1 По 20 Цикл
		Уровни.ДобавитьЗначение(0);
	КонецЦикла;
	
	Если (ТипЗначенияСтр(Таб) <> "Таблица") или (Обновить = 0) Тогда
		Таб=СоздатьОбъект("Таблица");
	Иначе
		Таб.Очистить();
	КонецЕсли;
	
	Таб.ИсходнаяТаблица("ДеревоДокументов");
	Таб.ВывестиСекцию("Кнопки");
	ДокПодч=СоздатьОбъект("Документ");
	ПровДокПодч=СоздатьОбъект("Документ");
	ДокПодч.ВыбратьПодчиненныеДокументы(,,КореньДок);
	ПровДокПодч.ВыбратьПодчиненныеДокументы(,,КореньДок);
	
	Уровень=1;
	ПечДок=ПредставлениеДокумента(КореньДок,КореньДок);
	Если Докум=КореньДок Тогда
		Таб.ВывестиСекцию("КореньТекДок");
	Иначе
		Таб.ВывестиСекцию("Header");
	КонецЕсли;
	Начало=1;
	Пока ДокПодч.ПолучитьДокумент()=1 Цикл
		Если Начало=1 Тогда
			А=ПровДокПодч.ПолучитьДокумент();
			Начало=0;
		КонецЕсли;
		Если КореньДок=ДокПодч.ТекущийДокумент() Тогда
			А=ПровДокПодч.ПолучитьДокумент();
			Продолжить;
		КонецЕсли;
		Если ПровДокПодч.ПолучитьДокумент()=1 Тогда
			Если КореньДок=ПровДокПодч.ТекущийДокумент() Тогда
				Уровни.УстановитьЗначение(1,0);
				Последний=1;
			Иначе
				Уровни.УстановитьЗначение(1,1);
				Последний=0;
			КонецЕсли;
		Иначе
			Уровни.УстановитьЗначение(1,0);
			Последний=1;
		КонецЕсли;
		ВывестиПодчиненные(Докум,КореньДок,ДокПодч.ТекущийДокумент(),Последний,Уровень,Уровни);
	КонецЦикла;
	Таб.Опции(0,0,3,0,"PO0005","ДеревоДокументов");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Структура подчиненных документов",);
КонецПроцедуры

Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр)="Документ" Тогда
		ВыбДокумент=Форма.Параметр;
		СформироватьДерево();
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Обновить = 0;
	Если глФлагРасшифровки = 1 Тогда
		Обновить = Число(глРасшифровка.Получить("Обновить"));
		ВыбДокумент = глРасшифровка.Получить("Док");
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;
		
		Если Обновить <> 2 Тогда
			СформироватьДерево();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры